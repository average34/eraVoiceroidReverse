;==================================================
;
;	汎用関数置き場＠参照編
;		参照に用い、特定の数値や名称などを返すもの
;		あるいは、状況に応じた接頭語などを返すもの
;
;		口上向けのものもあったりなかったり。
;
;==================================================

;==================================================
;
;	TEQUIP・ITEM関連の参照
;
;==================================================

;-------------------------------------------------
;関数名:EQUIP_CARESS
;概　要:持続愛撫装備品取得関数
;引　数:ARGS…愛撫種別を入れると一致をみる。/区切りで複数指定可能。省略するか空文字列だとTEQUIPによる愛撫を全て表示。
;			　クリキャップ・オナホール等持続愛撫をもたらさないので含まない。
;       ARG…対象のキャラ（省略すると調教対象。現在調教対象のみサポート）
;戻り値:一致を見る場合、一致しなければ空文字列。一致したか省略した場合、愛撫に関わるTEQUIPすべてを/区切りで並べたもの
;備　考:式中関数
;-------------------------------------------------
@EQUIP_CARESS(ARGS, ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
ARG = ARG != -2 ? ARG # MASTER
VARSET LOCALS
SIF TEQUIP:バイブ
	LOCALS += "バイブ/"
SIF TEQUIP:アナルバイブ
	LOCALS += "アナルバイブ/"
SIF TEQUIP:アナルビーズ
	LOCALS += "アナルビーズ/"
SIF TEQUIP:浣腸器＋プラグ
	LOCALS += "浣腸器＋プラグ/"
SIF TEQUIP:ニプルキャップ
	LOCALS += "ニプルキャップ/"
SIF TEQUIP:三角木馬
	LOCALS += "三角木馬/"
SIF GETBIT(TEQUIP:自慰中, 0)
	LOCALS += "自慰中/"
IF TEQUIP:調教者ペニスバンド
	SIF RANGE(TEQUIP:性交中, 1, 5) && TEQUIP:性交中 != 3
		LOCALS += "ペニバンＶ性交中/"
	SIF TEQUIP:性交中 == 3
		LOCALS += "ペニバン騎乗位中/"
	SIF TEQUIP:性交中 == 6
		LOCALS += "ペニバンＡ性交中/"
ELSE
	SIF RANGE(TEQUIP:性交中, 1, 5) && TEQUIP:性交中 != 3
		LOCALS += "Ｖ性交中/"
	SIF TEQUIP:性交中 == 3
		LOCALS += "騎乗位中/"
	SIF TEQUIP:性交中 == 6
		LOCALS += "Ａ性交中/"
ENDIF
IF TEQUIP:ペニスバンド
	SIF RANGE(TEQUIP:性交奉仕中, 1, 5) && TEQUIP:性交奉仕中 != 3
		LOCALS += "ペニバンＶ性交奉仕中/"
	SIF TEQUIP:性交奉仕中 == 3
		LOCALS += "ペニバン逆レイプ中/"
	SIF TEQUIP:性交奉仕中 == 6
		LOCALS += "ペニバンＡ性交奉仕中/"
ELSE
	SIF RANGE(TEQUIP:性交奉仕中, 1, 5) && TEQUIP:性交奉仕中 != 3
		LOCALS += "Ｖ性交奉仕中/"
	SIF TEQUIP:性交奉仕中 == 3
		LOCALS += "逆レイプ中/"
	SIF TEQUIP:性交奉仕中 == 6
		LOCALS += "Ａ性交奉仕中/"
ENDIF
IF PENIS(MASTER)
	SIF TEQUIP:調教対象Ｃ使用 == 1
		LOCALS += "手淫中/"
	SIF TEQUIP:調教対象Ｃ使用 == 2
		LOCALS += "フェラ中/"
ELSE
	SIF TEQUIP:調教対象Ｃ使用 == 1
		LOCALS += "愛撫中/"
	SIF TEQUIP:調教対象Ｃ使用 == 2
		LOCALS += "クンニ中/"
ENDIF

SIF LOCALS == ""
	RETURNF ""

LOCALS = SUBSTRINGU(LOCALS, 0, STRLENSU(LOCALS)-1)
RETURNF \@ ARGS == "" || STRFIND(@"/%LOCALS%/", @"/%ARGS%/") == -1 ? LOCALS # \@


;-------------------------------------------------
;関数名:EFFECT
;概　要:薬剤などの効果を表す関数
;引　数:ARG:0…薬剤の効果量(TEQUIP)
;       ARG:1…0なら弱中強、1なら少中多を返す
;戻り値:効果の大きさを表す文字
;       10以上は強、5以上は中、5未満は弱
;備　考:式中関数
;-------------------------------------------------
@EFFECT(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE ARG:0
	CASE IS > 9
		RETURNF @"\@ ARG:1 ? 多 # 強 \@"
	CASE IS > 4
		RETURNF "中"
	CASEELSE
		RETURNF @"\@ ARG:1 ? 少 # 弱 \@"
ENDSELECT


;-------------------------------------------------
;関数名:GETINSERT
;概　要:性交状態を表す関数
;引　数:なし
;戻り値:性交中=1、性交奉仕中=2、助手性交中=3、助手性交奉仕中=4
;備　考:式中関数
;-------------------------------------------------
@GETINSERT(ARG:0, ARG:1)
#FUNCTION
SIF TEQUIP:性交中
	RETURNF 1
SIF TEQUIP:性交奉仕中
	RETURNF 2
SIF TEQUIP:助手性交中
	RETURNF 3
SIF TEQUIP:助手性交奉仕中
	RETURNF 4
RETURNF 0

;-------------------------------------------------
;関数名:EQUIPACTION_DESCRIPTION
;概　要:TEQUIP関係の調教者の行動描写
;引　数:なし
;戻り値:TEQUIPの状態を表す文字列
;備　考:式中関数
;使用例:%EQUIPACTION_DESCRIPTION()%
;-------------------------------------------------
@EQUIPACTION_DESCRIPTION
#FUNCTIONS
IF TEQUIP:顔面騎乗 || TEQUIP:顔面騎乗アナル
	RETURNF @"%CALLNAME:MASTER%の顔の上に跨りながら"
ELSEIF TEQUIP:自慰中
	SELECTCASE TEQUIP:自慰中
		CASE 1
			RETURNF @"%CALLNAME:MASTER%が自分を慰める様を鑑賞しながら"
		CASE 2
			RETURNF @"%CALLNAME:MASTER%に自慰を見せつけながら"
		CASE 3
			RETURNF @"%CALLNAME:MASTER%が自分を慰める様を鑑賞しながら"
	ENDSELECT
ELSEIF TEQUIP:性交中
	RETURNF @"腰を振って%CALLNAME:MASTER%を突き上げながら"
ELSEIF TEQUIP:性交奉仕中
	IF TEQUIP:ペニスバンド
		RETURNF @"玩具のペニスを\@ TEQUIP:性交奉仕中 == 6 ? アナル # 膣内 \@で味わいながら"
	ELSEIF TEQUIP:性交奉仕中 == 6
		RETURNF @"アナルに挿入させたペニスを締め付けながら"
	ELSE
		RETURNF @"膣内のペニスを貪りながら"
	ENDIF
ENDIF


;-------------------------------------------------
;関数名:ITEM_TYPE
;概　要:調教道具分類関数
;引　数:ARG…ITEM番号
;戻り値:道具分類番号
;       (0.非表示/1.性具/2.責め具/3.その他)
;備　考:式中関数
;調教道具分類テーブル。調教道具の分類番号を返す。
;調教道具整頓表示に用いる
;
;名前が若干DITEMTYPEとややこしいかもしれない
;R#仕様に変更@revkoishi(14/05/19)
;-------------------------------------------------
@ITEM_TYPE(ARG)
#FUNCTION
SELECTCASE ARG
	;0.非表示
	CASE 9, IS >= 50
		RETURNF 0
	;1.性具
	CASE 0 TO 3, 11 TO 14
		RETURNF 1
	;2.責め具
	CASE 20 TO 22, 30 TO 33, 40, 42
		RETURNF 2
ENDSELECT
;3.その他(現在未使用)
RETURNF 3


;-------------------------------------------------
;関数名:ITEM_TYPENAME
;概　要:調教道具分類名取得関数
;引　数:ARG…調教道具分類番号
;戻り値:調教道具分類を表す文字列
;       (0.非表示/1.性具/2.責め具/3.その他)
;備　考:式中関数
;調教道具の分類名を返す。調教道具整頓表示に用いる
;
;R#仕様に変更@revkoishi(14/05/19)
;-------------------------------------------------
@ITEM_TYPENAME(ARG:0)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS:0)
	SPLIT "非表示/性具/責め具/その他", "/", LOCALS:0, LOCAL:0
RETURNF LOCALS:(ARG:0)


;-------------------------------------------------
;関数名:GET_ITEMNAME
;概　要:調教道具名取得関数
;引　数:ARG:0…ITEM番号
;戻り値:調教道具名
;備　考:式中関数
;調教道具の名前を返す。カスタマイズ後の名前の処理などを行う
;
;新規作成@revkoishi(14/05/22)
;-------------------------------------------------
@GET_ITEMNAME(ARG:0)
#FUNCTIONS
SELECTCASE ARG:0
	CASE 1, 11 TO 14	;バイブ/アナルバイブ/クリキャップ/ニプルキャップ/オナホール
		RETURNF \@ ITEM:カスタム道具 ? 特注 # \@ + ITEMNAME:(ARG:0)
ENDSELECT
RETURNF ITEMNAME:(ARG:0)


;==================================================
;
;	方針・状態変化関連の参照
;
;==================================================

;-------------------------------------------------
;関数名:CONDITION
;概　要:状態値から文字列を取得する関数
;引　数:ARG…状態フラグ(TCVAR:状態変化)
;戻り値:状態を表す文字列
;備　考:式中関数
;-------------------------------------------------
@CONDITION(ARG)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "通常/疲弊/衰弱/無気力/朦朧/情欲/怒り/退屈/鬱屈", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG

;-------------------------------------------------
;関数名:IS_CONDITION
;概　要:ARGがARGSの状態なら真を返す
;引　数:ARG…キャラ登録番号。省略するとTARGET
;戻り値:その状態なら1、違えば0
;備　考:式中関数
;-------------------------------------------------
@IS_CONDITION(ARG = -2, ARGS)
#FUNCTION
ARG = ARG != -2 ? ARG # TARGET
RETURNF ARGS == CONDITION(TCVAR:ARG:状態変化)

;-------------------------------------------------
;関数名:GET_CONDITION
;概　要:ARGの状態変化名を取得
;引　数:ARG…キャラ登録番号。省略するとTARGET
;戻り値:状態を表す文字列
;備　考:式中関数
;-------------------------------------------------
@GET_CONDITION(ARG = -2)
#FUNCTIONS
ARG = ARG != -2 ? ARG # TARGET
RETURNF CONDITION(TCVAR:ARG:状態変化)

;-------------------------------------------------
;関数名:T_COND
;概　要:TARGETが指定した状態なら真を返す
;引　数:ARGS…状態名
;戻り値:その状態なら1、違えば0
;備　考:式中関数
;-------------------------------------------------
@T_COND(ARGS)
#FUNCTION
RETURNF ARGS == CONDITION(TCVAR:状態変化)

;-------------------------------------------------
;関数名:M_COND
;概　要:MASTERが指定した状態なら真を返す
;引　数:ARGS…状態名
;戻り値:その状態なら1、違えば0
;備　考:式中関数
;-------------------------------------------------
@M_COND(ARGS)
#FUNCTION
RETURNF ARGS == CONDITION(TCVAR:MASTER:状態変化)

;-------------------------------------------------
;関数名:POLICY
;概　要:調教方針を逆引きする関数
;引　数:ARGS…どの方針か（TODAY/今日、NOW/今、LAST/前）
;		ARG …ARGSに今日の方針・今回の方針を入れた場合数値からとれる
;戻り値:方針名
;備　考:式中関数
;-------------------------------------------------
@POLICY(ARGS, ARG)
#FUNCTIONS
#LOCALSSIZE 20
;前半６つは短期方針、後半は「今日の方針」
SPLIT "通常/休憩/ソフト/ノーマル/ハード/異常/通常/アナル/お仕置き/ハード/イチャイチャ/異常/野外/子作り/休憩/抱いて/ご奉仕/かわいがり", "/", LOCALS, LOCAL
IF ARGS == "今日の方針"
	RETURNF LOCALS:(ARG+6)
ELSEIF ARGS == "今回の方針"
	RETURNF LOCALS:ARG
ENDIF
SELECTCASE ARGS
	CASE "今日", "TODAY"
		RETURNF LOCALS:(TFLAG:今日の方針+6)
	CASE "今", "NOW"
		RETURNF LOCALS:(TFLAG:ターン方針)
	CASE "前", "LAST"
		RETURNF LOCALS:(TFLAG:前ターン方針)
	CASEELSE
		CALLF PRINT_ERRORMESSAGE(@"@POLICY に無効な方針指定(%ARGS%)が渡されました")
		;種別無指定を返す
		RETURNF "種別無指定"
ENDSELECT

;-------------------------------------------------
;関数名:IS_POLICY
;概　要:調教方針名称と合致すれば真を返す
;引　数:ARGS:0…どの方針か（TODAY/今日、NOW/今、LAST/前）
;		ARGS:1…方針名称(/区切りで複数代入が可能です)
;戻り値:真偽値(1/0)
;備　考:式中関数
;-------------------------------------------------
@IS_POLICY(ARGS:0, ARGS:1)
#FUNCTION
#LOCALSSIZE 10
#DIMS LTEMPS
LTEMPS = %POLICY(ARGS)%
SPLIT ARGS:1, "/", LOCALS, LOCAL
IF LTEMPS == "種別無指定"
	CALLF PRINT_ERRORMESSAGE(@"@IS_POLICY に無効な方針指定(%ARGS%)が渡されました")
ELSEIF FINDELEMENT(LOCALS, LTEMPS, 0, LOCAL, 1) != -1
	RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;関数名:SATIS_BONUS
;概　要:今日の満足ボーナスから文字列を取得する関数
;引　数:ARG…フラグ(TCVAR:今日の満足ボーナス)
;戻り値:満足ボーナスを表す文字列
;備　考:式中関数
;-------------------------------------------------
@SATIS_BONUS(ARG)
#FUNCTIONS
#LOCALSSIZE 20
SIF !STRLENS(LOCALS)
	SPLIT "通常/アナル/お仕置き/ハード/イチャイチャ/異常/野外/子作り/休憩/抱いて/ご奉仕/かわいがり", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG

;-------------------------------------------------
;関数名:IS_SATIS_BONUS
;概　要:ARGSの満足ボーナスなら真を返す
;引　数:ARGS…満足ボーナス名
;戻り値:その状態なら1、違えば0
;備　考:式中関数
;-------------------------------------------------
@IS_SATIS_BONUS(ARGS)
#FUNCTION
RETURNF ARGS == SATIS_BONUS(TCVAR:今日の満足ボーナス)

;-------------------------------------------------
;関数名:GET_SATIS_BONUS
;概　要:今日の満足ボーナス名を取得
;引　数:なし
;戻り値:満足ボーナスを表す文字列
;備　考:式中関数
;-------------------------------------------------
@GET_SATIS_BONUS
#FUNCTIONS
RETURNF SATIS_BONUS(TCVAR:今日の満足ボーナス)

;-------------------------------------------------
;関数名:TENSION
;概　要:BASE値を大雑把に表す関数
;引　数:ARG:0…キャラ登録番号
;       ARG:1…BASE番号
;戻り値:BASE値を大雑把に表す文字列
;       50%以上は高い、25%以上は一般、25%未満は低い
;備　考:式中関数
;調教者の理性や満足などを文字で表現する場合に使用
;-------------------------------------------------
@TENSION(ARG:0, ARG:1)
#FUNCTIONS
#LOCALSIZE 1
LOCAL = MAX(BASE:(ARG:0):(ARG:1), 0)
SIF LOCAL < MAXBASE:(ARG:0):(ARG:1) / 4
	RETURNF "低い"
RETURNF @"\@ LOCAL < MAXBASE:(ARG:0):(ARG:1) / 2 ? 一般 # 高い \@"


;-------------------------------------------------
;COMRANK(REACT負荷)
;-------------------------------------------------

;REACT負荷リストを返す
@COMRANKLIST()
#FUNCTIONS
RETURNF "自然な行動/無理のない行動/無理をした行動"

;現在のREACT負荷名称とARGSが合致すれば真を返す
@IS_NOWCOMRANKNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, COMRANKLIST(), TFLAG:REACT負荷)

;ARGSのREACT負荷なら真を返す
@IS_COMRANK(ARGS)
#FUNCTION
RETURNF ARGS == COM_RANK(TFLAG:REACT負荷)


;-------------------------------------------------
;関数名:COM_RANK
;概　要:REACT負荷から文字列を取得する関数
;引　数:ARG…(TFLAG:REACT負荷)
;戻り値:REACT負荷を表す文字列
;-------------------------------------------------
@COM_RANK(ARG)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "自然な行動/無理のない行動/無理をした行動", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG


;-------------------------------------------------
;関数名:GET_COM_RANK
;概　要:REACT負荷名称と合致すれば真を返す
;引　数:なし
;戻り値:REACT負荷を表す文字列
;備　考:式中関数
;-------------------------------------------------
@GET_COM_RANK()
#FUNCTIONS
RETURNF COM_RANK(TFLAG:REACT負荷)


;==================================================
;
;	システムデータ関連の参照
;
;==================================================

;-------------------------------------------------
;記入チェック関数
;	引数0：表示確率。省略すると100％。テンプレでは最初から0が入っている
;-------------------------------------------------
@口上記入チェック(ARG = 100)
#FUNCTION
RETURNF PERCENT(ARG) ? ARG # 0

;-------------------------------------------------
;関数名:CAPACITY
;概　要:いるキャラの数を返す
;引　数:なし
;戻り値:いるキャラの数(MASTERを含む)
;備　考:式中関数
;現状、一時退避中が無いので、CHARANUMと何も変わらない
;-------------------------------------------------
@CAPACITY
#FUNCTION
RETURNF CMATCH(CFLAG:92, 0)

;-------------------------------------------------
;関数名:GET_DAY
;概　要:曜日を表す文字列を取得する関数
;引　数:なし
;戻り値:曜日を表す文字列(日/月/火/水/木/金/土)
;備　考:式中関数
;-------------------------------------------------
@GET_DAY
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "日/月/火/水/木/金/土", "/", LOCALS, LOCAL
RETURNF LOCALS:(DAY % 7)

;-------------------------------------------------
;関数名:GET_SEASON
;概　要:季節を表す文字列を取得する関数
;引　数:なし
;戻り値:季節を表す文字列(春/夏/秋/冬)
;備　考:式中関数
;-------------------------------------------------
@GET_SEASON
#FUNCTIONS
SWAP LOCAL:1, RESULT
SIF !STRLENS(LOCALS)
	SPLIT "春/夏/秋/冬", "/", LOCALS, LOCAL
RETURNF LOCALS:(FLAG:季節)

;-------------------------------------------------
;関数名:GET_TIME
;概　要:時間を表す文字列を取得する関数
;引　数:なし
;戻り値:時間を表す文字列(昼/夜)
;備　考:式中関数
;-------------------------------------------------
@GET_TIME
#FUNCTIONS
RETURNF @"\@ TIME ? 夜 # 昼 \@"


