;==================================================
;
;	汎用関数置き場＠参照編
;		参照に用い、特定の数値や名称などを返すもの
;		あるいは、状況に応じた接頭語などを返すもの
;
;		口上向けのものもあったりなかったり。
;
;==================================================


;==================================================
;
;	TALENT、その他キャラクター関連の参照
;
;==================================================

;-------------------------------------------------
;キャラ間呼び名取得関数@CNAME
;式中で使用する関数です。TARGETが引数0のキャラを呼ぶ呼び名を表示します。省略するとMASTER
;引数1を入れるとTARGET以外からも参照できます。
;頻繁に呼ばれるのでRESULTを保護します。
;こんなゴツい処理を名前呼ぶたびに行う是非についてはとりあえず知らない。まあ重くなんてならないと信じる
;-------------------------------------------------
@CNAME(ARG = -2, ARG:1 = -2)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT, 1
#DIMS DYNAMIC 敬称対象, 100
#DIMS DYNAMIC 敬称内訳, 100
#DIMS DYNAMIC 呼び名対象, 100
#DIMS DYNAMIC 呼び名内訳, 100

;引数省略の処理
ARG = ARG != -2 ? ARG # MASTER
ARG:1 = ARG:1 != -2 ? ARG:1 # TARGET

;基本敬称の処理
SPLIT CSTR:(ARG:1):基本敬称, "/", 敬称対象, LOCAL
FOR LCOUNT, 0, LOCAL
	敬称内訳:LCOUNT = %REPLACE(REPLACE(SUBSTRING(敬称対象:LCOUNT, STRFIND(敬称対象:LCOUNT, "_")+1), "名前", NAME:ARG), "呼び名", CALLNAME:ARG)%
	敬称対象:LCOUNT = %SUBSTRING(敬称対象:LCOUNT,0, STRFIND(敬称対象:LCOUNT, "_")-1)%
NEXT

;呼び名の分類
SPLIT CSTR:(ARG:1):呼び方リスト, "/", 呼び名対象, LOCAL
FOR LCOUNT, 0, LOCAL
	呼び名内訳:LCOUNT = %REPLACE(REPLACE(SUBSTRING(呼び名対象:LCOUNT, STRFIND(呼び名対象:LCOUNT, "_")+1), "名前", NAME:ARG), "呼び名", CALLNAME:ARG)%
	呼び名対象:LCOUNT = %SUBSTRING(呼び名対象:LCOUNT,0, STRFIND(呼び名対象:LCOUNT, "_")-1)%
NEXT

;一時的にNAMEを入れる
LOCALS = %NAME:ARG%
$NAMELESS_LOOP
;呼び名対象にいる場合
IF MATCH(呼び名対象, LOCALS)
	FOR LCOUNT, 0, 100
		IF 呼び名対象:LCOUNT == LOCALS
			LOCALS = %呼び名内訳:LCOUNT%
			BREAK
		ENDIF
	NEXT
;大妖精・小悪魔の場合
ELSEIF LOCALS == "大妖精" || LOCALS == "小悪魔"
	;そのままでは呼べないのであなたにしてもう一度回す
	LOCALS = あなた
	GOTO NAMELESS_LOOP
;基本敬称の処理
ELSE
	;CALLNAMEにする
	LOCALS = %CALLNAME:ARG%
	;敬称対象検索
	FOR LCOUNT, 0, 100
		SELECTCASE 敬称対象:LCOUNT
			CASE ""
				BREAK
			CASE "基本"
				LOCALS = %敬称内訳:LCOUNT%
			CASEELSE
				SIF TALENT:ARG:(敬称対象:LCOUNT)
					LOCALS = %敬称内訳:LCOUNT%
		ENDSELECT
	NEXT
ENDIF
;RESULTを復元
RESULT = LOCAL
RETURNF LOCALS

;-------------------------------------------------
;ペニス判定関数@PENIS
;式中で使用する関数です。ペニスがある場合1を返します
;引数0は対象の登録番号。しょうもない中身
;引数のキャラがいない場合0で返すようにしました
;
;↑の仕様は正直どうなんだろと思わなくもないけれど、とりあえず下限しか見てなかったので修正@revkoishi(14/04/20)
;-------------------------------------------------
@PENIS(ARG)
#FUNCTION
RETURNF INRANGE(ARG, 0, CHARANUM - 1) ? TALENT:ARG:ふたなり || TALENT:ARG:オトコ # 0
;RETURNF ARG < 0 ? 0 # TALENT:ARG:ふたなり || TALENT:ARG:オトコ

;-------------------------------------------------
;ペニスバンド判定関数@PENISBAND
;式中で使用する関数です。ペニスバンド使用中1を返します
;破棄予定
;-------------------------------------------------
@PENISBAND(ARG)
#FUNCTION
RETURNF !(TALENT:ARG:ふたなり || TALENT:ARG:オトコ) && ITEM:ペニスバンド

;-------------------------------------------------
;アライメント判定関数@ALI
;式中で使用する関数です。アライメントが引数以上なら真になります
;引数0はアライメントの数値。0ならばアライメント正扱い
;引数1を省略すればTARGET。指定すればそのキャラを見ます。(FLAG:ご主人様)と指定することでEV111等でも使えます
;-------------------------------------------------
@ALI(ARG, ARG:1 = -2)
#FUNCTION
SIF ARG:1 == -2
	ARG:1 = TARGET
RETURNF CFLAG:(ARG:1):アライメント >= ARG

;-------------------------------------------------
;好感度判定関数@FAVOR
;式中で使用する関数です。好感度が指定以上なら真になります。ただし、0にした場合は条件に含みません
;引数0は調教対象から調教者、引数1は調教者から調教対象の好感度です
;引数2を指定した場合、調教者の代わりにそのキャラ（主に助手）を見ます
;-------------------------------------------------
@FAVOR(ARG, ARG:1, ARG:2 = -2)
#FUNCTION
SIF ARG:2 == -2
	ARG:2 = TARGET
RETURNF (!ARG || CFLAG:(ARG:2):M好意 >= ARG) && (!ARG:1 || CFLAG:(ARG:2):好意 >= ARG:1)

;-------------------------------------------------
;好感度参照関数@FAVOR2
;式中で使用する関数です。好感度が指定以上なら真になります。ただし、省略(-123456)した場合は好感度の数値自体を返します
;TARGET-ASSI間の好感度は存在しないため、引数にMASTERが含まれない場合は0で戻ります
;-------------------------------------------------
@FAVOR2(ARG, ARG:1, ARG:2 = -123456)
#FUNCTION
;引数不適
SIF ARG < 0 || ARG:1 < 0 || !GROUPMATCH(MASTER, ARG, ARG:1)
	RETURNF 0
;閾値がある
IF ARG:2 != -123456
	RETURNF ARG == MASTER ? CFLAG:(ARG:1):M好意 >= ARG:2 # CFLAG:ARG:好意 >= ARG:2
;閾値がない
ELSE
	RETURNF ARG == MASTER ? CFLAG:(ARG:1):M好意 # CFLAG:ARG:好意
ENDIF

;-------------------------------------------------
;ベース割合算出関数@BASERATIO
;式中で使用する関数です。ベース値がMAXBASEの何％あるか返します
;引数0は登録番号、引数1はベース番号、引数2を設定した場合はその％以上なら真を返します
;-------------------------------------------------
@BASERATIO(ARG:0, ARG:1, ARG:2)
#FUNCTION
RETURNF ARG:2 ? BASE:(ARG:0):(ARG:1) * 100 / MAXBASE:(ARG:0):(ARG:1) >= ARG:2 # BASE:(ARG:0):(ARG:1) * 100 / MAXBASE:(ARG:0):(ARG:1)

;-------------------------------------------------
;素質検索関数@FINDTALENT
;式中で使用する関数です。指定した素質をもつキャラを検索します。最初に見つかったキャラの登録番号を返します
;居なかった場合、-1を返します
;引数は文字列です。数値を使う場合はTALENTNAME:xxを引数にして呼んでください
;-------------------------------------------------
@FINDTALENT(ARGS)
#FUNCTION
#LOCALSIZE 1
;FINDCHARAが使いたい…けど真偽判定ができないのでFORを回す
FOR LOCAL, 0, CHARANUM
	SIF TALENT:LOCAL:ARGS
		RETURNF LOCAL
NEXT
RETURNF -1

;-------------------------------------------------
;助手番号検索関数@ASSISNO
;助手の誰かが指定したキャラ番号なら、その助手の登録番号を返します。
;いない場合、-1になります
;-------------------------------------------------
@ASSISNO(ARG)
#FUNCTION
#LOCALSIZE 1
FOR LOCAL, 0, 4
	SIF ASSI:LOCAL >= 0 && NO:(ASSI:LOCAL) == ARG
		RETURNF ASSI:LOCAL
NEXT
RETURNF -1
;-------------------------------------------------
;助手素質検索関数@ASSISTALENT
;助手の誰かが指定したTALENTを引数1の値以上で持っていれば、その助手の登録番号を返します。
;いない場合、-1になります
;-------------------------------------------------
@ASSISTALENT(ARG, ARG:1 = 1)
#FUNCTION
#LOCALSIZE 1
FOR LOCAL, 0, 4
	SIF ASSI:LOCAL >= 0 && TALENT:(ASSI:LOCAL):ARG >= ARG:1
		RETURNF ASSI:LOCAL
NEXT
RETURNF -1

;-------------------------------------------------
;助手能力検索関数@ASSISABL
;助手の誰かが指定したABLを引数1の値以上で持っていれば、その助手の登録番号を返します。
;いない場合、-1になります
;-------------------------------------------------
@ASSISABL(ARG, ARG:1 = 1)
#FUNCTION
#LOCALSIZE 1
FOR LOCAL, 0, 4
	SIF ASSI:LOCAL >= 0 && ABL:(ASSI:LOCAL):ARG >= ARG:1
		RETURNF ASSI:LOCAL
NEXT
RETURNF -1

;-------------------------------------------------
;関数名:ASSIID
;概　要:助手ID取得関数
;引　数:なし
;戻り値:ASSIとして扱われている助手のID(1～3)
;備  考:式中関数
;ASSIに入っているキャラ(＝口上主)が助手１か助手２か助手３かを判別する
;助手口上で使うかも
;-------------------------------------------------
@ASSIID
#FUNCTION
#LOCALSIZE 1
SIF ASSI < 0
	RETURNF 0
FOR LOCAL, 1, 4
	SIF ASSI == ASSI:LOCAL
		RETURNF LOCAL
NEXT

;-------------------------------------------------
;素質判定関数@GET_TALENT
;引数のキャラがが指定したTALENTを持っていれば、TALENTの数値を返します。
;いない場合、0になります
;TALENTの指定は文字列で行いますが、番号でもできます。その場合""囲みで文字列として数字を入れること
;-------------------------------------------------
@GET_TALENT(ARG, ARGS)
#FUNCTION
#LOCALSIZE 1
IF GETNUM(TALENT, ARGS) >= 0
	LOCAL = GETNUM(TALENT, ARGS)
ELSE
	LOCAL = TOINT(ARGS)
ENDIF
RETURNF ARG >= 0 ? TALENT:(ASSI:1):LOCAL # 0


;-------------------------------------------------
;関数名:ABL_TYPE
;概　要:能力分類関数
;引　数:ARG:0…ABL番号
;戻り値:能力分類番号
;       (0.非表示/1.基礎/2.感覚/3.性癖/4.中毒/5.調教/6.未分類)
;備　考:式中関数
;能力分類テーブル。能力の分類番号を返す
;今のところ色分けにしか使っていない
;
;新規作成@revkoishi(14/05/26)
;-------------------------------------------------
@ABL_TYPE(ARG:0)
#FUNCTION
SELECTCASE ARG:0
	;0.非表示(表示させない能力) ※特別な理由が無い限りは不要な枠
	;CASE -1
	;	RETURNF 0
	;1.基礎
	CASE 0 TO 2
		RETURNF 1
	;2.感覚
	CASE 3 TO 6
		RETURNF 2
	;3.性癖
	CASE 7 TO 11
		RETURNF 3
	;4.中毒
	CASE 12 TO 16
		RETURNF 4
	;5.調教
	CASE 20 TO 28
		RETURNF 5
ENDSELECT
;6.未分類(これがあるのは望ましくない)
RETURNF 6


;-------------------------------------------------
;関数名:EXP_TYPE
;概　要:経験分類関数
;引　数:ARG:0…EXP番号
;戻り値:経験分類番号
;       (0.非表示/1.快楽/2.絶頂/3.状況/4.能動/5.受動/6.異常/7.未分類)
;備　考:式中関数
;経験分類テーブル。経験の分類番号を返す
;今のところ色分けにしか使っていない
;
;新規作成@revkoishi(14/05/26)
;-------------------------------------------------
@EXP_TYPE(ARG:0)
#FUNCTION
SELECTCASE ARG:0
	;0.非表示(表示させない経験) ※特別な理由が無い限りは不要な枠
	;CASE -1
	;	RETURNF 0
	;1.快楽
	CASE 0 TO 3, 21, 30, 31
		RETURNF 1
	;2.絶頂
	CASE 4 TO 6, 14
		RETURNF 2
	;3.状況
	CASE 7, 20, 25, 26, 32, 40, 41
		RETURNF 3
	;4.能動
	CASE 8, 10, 22 TO 24, 63
		RETURNF 4
	;5.受動
	CASE 11 TO 13, 51, 60 TO 62
		RETURNF 5
	;6.異常
	CASE 50, 52, 53, 55
		RETURNF 6
ENDSELECT
;7.未分類(これがあるのは望ましくない)
RETURNF 7


;-------------------------------------------------
;関数名:GET_SEX
;概　要:性別取得関数
;引　数:ARG:0…キャラ登録番号
;戻り値:性別を表す文字列
;備　考:式中関数
;
;GET_TALENTNAMEを使用するように変更@revkoishi(14/04/28)
;-------------------------------------------------
@GET_SEX(ARG)
#FUNCTIONS
SIF TALENT:ARG:ふたなり
	RETURNF GET_TALENTNAME(ARG, GETNUM(TALENT, "ふたなり"))
RETURNF @"\@ TALENT:ARG:オトコ ? %GET_TALENTNAME(ARG, GETNUM(TALENT, "オトコ"))% # 女の子 \@"


;-------------------------------------------------
;関数名:GET_TALENTNAME
;概　要:素質名取得関数
;引　数:ARG:0…キャラ登録番号
;　　　:ARG:1…TALENT番号
;戻り値:素質名
;備　考:式中関数
;素質の名前を返す。上位素質名の処理などを行う
;
;日光嫌いと読心能力の処理を追加、SQR分をコメントアウト@revkoishi(14/04/20)
;-------------------------------------------------
@GET_TALENTNAME(ARG:0, ARG:1)
#FUNCTIONS
SELECTCASE ARG:1
	;CASE 0		;処女/処女(再生)
	;	RETURNF TALENTNAME:0 + \@ !INRANGE(TALENT:ARG:処女, 0, 1) ? (再生) # \@
	;CASE 109	;巨乳/爆乳
	;	RETURNF \@ TALENT:ARG:巨乳 == 2 ? 爆乳 # %TALENTNAME:109% \@
	CASE 119	;日光嫌い/日光苦手
		RETURNF "日光" + \@ TALENT:ARG:日光嫌い == 2 ? 苦手 # 嫌い \@
	CASE 132	;(読心能力) 空気を読む程度の能力/心を読む程度の能力
		RETURNF \@ TALENT:ARG:読心能力 == 2 ? 心 # 空気 \@ + "を読む程度の能力"
ENDSELECT
RETURNF TALENTNAME:(ARG:1)



;-------------------------------------------------
;関数名:TALENT_TYPE
;概　要:素質分類関数
;引　数:ARG…TALENT番号
;戻り値:素質分類番号
;       (0.非表示/1.性別/2.性格/3.体質/4.技能/5.後天素質/6.種族特性/7.未分類)
;備　考:式中関数
;素質分類テーブル。素質の分類番号を返す
;素質整頓表示に用いる
;
;輝針城分を追加、SQR分を削除、TALENT:220～222を暫定的に非表示に@revkoishi(14/04/20)
;-------------------------------------------------
@TALENT_TYPE(ARG)
#FUNCTION
SELECTCASE ARG
	;0.非表示(表示させない素質) ※基本的にはSQR由来枠なので、特別な理由が無い限りは不要な枠
	;CASE 220 TO 222	;TALENT:220～222にデータが入る問題が直ったのでコメントアウトで@revkoishi(14/05/19)
	;	RETURNF 0
	;1.性別
	CASE 0, 1, 121, 122
		RETURNF 1
	;2.性格
	CASE 10 TO 17, 20 TO 37, 63, 65, 80 TO 88, 90, 92, 93
		RETURNF 2
	;3.体質
	CASE 40 TO 43, 56, 60 TO 62, 64, 70, 71, 100 TO 114, 116, 118, 119, 123 TO 126
		RETURNF 3
	;4.技能
	CASE 50 TO 53, 55, 57 TO 59, 66, 91, 117, 130, 132, 133, 135
		RETURNF 4
	;5.後天素質
	CASE 2, 3, 72 TO 79, 89, 131, 140
		RETURNF 5
	;6.種族特性
	CASE 54, 115, 119, 134, 136 TO 138
		RETURNF 6
ENDSELECT
;7.未分類(これがあるのは望ましくない)
RETURNF 7

;-------------------------------------------------
;関数名:TALENT_TYPENAME
;概　要:素質分類名取得関数
;引　数:ARG…TALENT分類番号
;戻り値:素質分類を表す文字列
;       (0.非表示/1.性別/2.性格/3.体質/4.技能/5.後天素質/6.種族特性/7.未分類)
;備　考:式中関数
;素質の分類名を返す。素質整頓表示に用いる
;-------------------------------------------------
@TALENT_TYPENAME(ARG)
#FUNCTIONS
#LOCALSSIZE 10
SIF !STRLENS(LOCALS)
	SPLIT "非表示/性別/性格/体質/技能/後天素質/種族特性/未分類", "/", LOCALS, LOCAL
RETURNF LOCALS:ARG


;-------------------------------------------------
;ご主人様修飾関数@TARGET_DESCRIPTION
;式中で使用する関数です。TARGETがどのような状態かを描写します…とりあえず継続系にのみ対応
;引数は語尾。「、」「ながら」「たまま」「て」「つつ」「た」等に対応
;-------------------------------------------------
@TARGET_DESCRIPTION(ARGS)
#FUNCTIONS
VARSET LOCALS
;性交中(調教者挿入)
IF TEQUIP:性交中
	IF TEQUIP:調教者ペニスバンド
		LOCALS += "身に着けた張り型を"
	ELSE
		LOCALS += @"\@ TCVAR:TARGET:イきそう ? 今にもイきそうな # \@ペニスを"
	ENDIF
	;体位によって分岐
	SELECTCASE TEQUIP:性交中
		;正常位
		CASE 1
			LOCALS += @"体重を預けて%CALLNAME:MASTER%の膣内に埋め"
		;後背位
		CASE 2
			LOCALS += @"背後から%CALLNAME:MASTER%の膣内に埋め"
		;騎乗位
		CASE 3
			LOCALS += @"%CALLNAME:MASTER%の雌穴に根元まで咥え込まれ"
		;対面座位
		CASE 4
			LOCALS += @"密着した%CALLNAME:MASTER%の膣に愛撫され"
		;背面座位
		CASE 5
			LOCALS += @"%CALLNAME:MASTER%に挿入し背を抱き締め"
		;アナルセックス
		CASE 6
			LOCALS += @"%CALLNAME:MASTER%の肛門に埋め"
	ENDSELECT
;手淫・愛撫継続中
ELSEIF TEQUIP:調教対象Ｃ使用 == 1
	LOCALS += @"\@ TCVAR:MASTER:イきそう ? 今にもイきそうな # \@"
	LOCALS += @"%CALLNAME:MASTER%の\@ PENIS(MASTER) ? ペニス # \@"
	SIF !PENIS(MASTER)
		LOCALS += @"\@ TEQUIP:ペニスバンド ? 張り型 # クリトリス \@"
	LOCALS += "を愛撫し"
;フェラ・クンニ継続中
ELSEIF TEQUIP:調教対象Ｃ使用 == 2
	LOCALS += @"\@ TCVAR:MASTER:イきそう ? 今にもイきそうな # \@%CALLNAME:MASTER%の\@ PENIS(MASTER) ? ペニス # クリトリス \@を舐め"
;性交中(調教対象挿入)
ELSEIF TEQUIP:性交奉仕中
	IF TEQUIP:ペニスバンド
		LOCALS += @"%CALLNAME:MASTER%の身に着けた張り型を"
	ELSE
		LOCALS += @"%CALLNAME:MASTER%の\@ TCVAR:MASTER:イきそう ? 今にもイきそうな # \@ペニスを"
	ENDIF
	;体位によって分岐
	SELECTCASE TEQUIP:性交奉仕中
		;正常位
		CASE 1
			LOCALS += @"大きく開いた股の間に迎え入れ"
		;後背位
		CASE 2
			LOCALS += @"背後から膣内に挿入され"
		;騎乗位
		CASE 3
			LOCALS += @"ぬるぬるの膣できゅうきゅうと絞り上げ"
		;対面座位
		CASE 4
			LOCALS += @"体を密着させて膣奥に歓迎し"
		;背面座位
		CASE 5
			LOCALS += @"背を預けて堪能し"
		;アナルセックス
		CASE 6
			LOCALS += @"腸液にぬめる肛門で締め付け"
	ENDSELECT
;調教者自慰中
ELSEIF GETBIT(TEQUIP:自慰中, 1)
	IF TEQUIP:調教者ペニスバンド
		LOCALS += "身に着けた張り型を"
	ELSE
		LOCALS += @"\@ TCVAR:TARGET:イきそう ? 今にもイきそうな # \@\@ PENIS(TARGET) ? ペニス # クリトリス \@を"
	ENDIF
	LOCALS += "自ら愛撫するのを見せ付け"
ENDIF


SIF LOCALS != ""
	LOCALS += ARGS
RETURNF LOCALS

;-------------------------------------------------
;関数名:PENIS_DESCRIPTION
;概　要:ペニス描写関数
;引　数:なし
;戻り値:ペニスの状態を表す文字列
;備　考:式中関数
;なんとなくコードが煩雑になりそうな予感がしたので関数可してみた
;使用例
;PRINTFORML %CALLNAME:MASTER%の%PENIS_DESCRIPTION()%ペニスが(以下略
;現状の規模ならAUTO_SPLITでも十分だけど、拡張可能性と保守性を考えてSTRDATAにしてみる@revkoishi
;-------------------------------------------------
@PENIS_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
IF ARG == MASTER
	SELECTCASE TFLAG:ACT
		;愛撫系ACT
		CASE 10 TO 19
			;萎えてる
			IF TCVAR:MASTER:勃起度 < 1000
				;射精直後
				IF TCVAR:MASTER:射精後経過ターン == 1
					STRDATA LOCALS:0
						DATA イったばかりの
						DATA イったばかりで敏感な
						DATA 先程たっぷりと精を放った
					ENDDATA
				;その他
				ELSE
					STRDATA LOCALS:0
						DATA 萎えてしまった
						DATA 萎びた
						DATA ピクリともしない
					ENDDATA
				ENDIF
			;勃起
			ELSE
				;射精直後
				IF TCVAR:MASTER:射精後経過ターン == 1
					STRDATA LOCALS:0
						DATA ようやく射精が収まった
						DATA 衰えを知らない
						DATA 脈動が収まらない
					ENDDATA
				;勃起度の派生
				ELSEIF TCVAR:MASTER:勃起度 >= 1500
					STRDATA LOCALS:0
						DATA はちきれんばかりに怒張した
						DATA 反り返った
						DATA 限界まで勃起した
					ENDDATA
				ELSE
					STRDATA LOCALS:0
						DATA 怒張した
						DATA 屹立した
						DATA いきり立った
					ENDDATA
				ENDIF
			ENDIF
	ENDSELECT
ELSE
	SELECTCASE TCVAR:ARG:勃起
		CASE 0
			LOCALS:0 = まだ柔らかい
		CASE 1
			LOCALS:0 = 徐々に硬度を増しつつある
		CASE 2
			LOCALS:0 = 勃起した
		CASEELSE
			LOCALS:0 = 反り返った
	ENDSELECT
ENDIF
RETURNF LOCALS:0


;-------------------------------------------------
;関数名:PENIS_DESCRIPTION2
;概　要:ペニス描写関数
;引　数:なし
;戻り値:ペニスの状態を表す文字列
;備　考:式中関数
;
;TFLAG:166=エナジードレイン
;被エナジードレイン中のペニスの状態というモロにSQR仕様関数なのでコメントアウト
;関数定義だけ生きているのは現状で呼び出し箇所があるから手抜きという意味もあるのだけど、
;そのうち誰かがエナジードレインするようになったら(あるいは他に利用方法あれば)
;その時にまた考えましょうってことで@revkoishi(14/04/20)
;-------------------------------------------------
@PENIS_DESCRIPTION2(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
[SKIPSTART]
ARG = ARG != -2 ? ARG # MASTER
LOCALS:0 = 
IF ARG == MASTER && TFLAG:166
	IF BASE:MASTER:射精
		STRDATA LOCALS:0
			DATA イきっぱなしになった
			DATA 射精を続ける
			DATA 精液が垂れ流しになった
		ENDDATA
	ELSE
		STRDATA LOCALS:0
			DATA 空撃ちを続ける
			DATA イきっぱなしになった
		ENDDATA
	ENDIF
ENDIF
[SKIPEND]
RETURNF LOCALS:0


;-------------------------------------------------
;関数名:OPPAI_DESCRIPTION
;概　要:言葉でなく心で理解しろ
;引　数:ARG…キャラ登録番号
;戻り値:夢
;備　考:式中関数
;使用例:%OPPAI_DESCRIPTION(TARGET)%
;-------------------------------------------------
@OPPAI_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
;SQR由来部分につきコメントアウト。爆乳は今後も無いと思われるが参考に残しておく@revkoishi(14/04/20)
;SIF TALENT:ARG:巨乳 == 2	;爆乳
;	RETURNF RAND_SPLIT("はちきれんばかりの胸/大迫力のバスト/たっぷりとした質感の胸")
IF TALENT:ARG:巨乳	;巨乳
	STRDATA LOCALS:0
		DATA 豊満な胸
		DATA 自慢のバスト
		DATA 魅力的な豊かな膨らみ
		DATA 瑞々しく揺れる双丘
		DATA たわわに揺れる果実
		DATA 大きな乳房
		DATA ふくよかな胸
		DATA 弾力に富んだ豊かな胸
	ENDDATA
ELSEIF TALENT:ARG:貧乳	;貧乳
	STRDATA LOCALS:0
		DATA 控えめな胸
		DATA 可愛らしい胸
		DATA 可愛らしく勃起させた乳首
		DATA 掌に収まりそうな愛らしい乳房
		DATA 薄い乳房
		DATA ささやかな膨らみ
	ENDDATA
ELSE					;並乳
	STRDATA LOCALS:0
		DATA 柔らかい胸
		DATA 均整のとれた胸
		DATA 胸
		DATA 瑞々しく張りのいい胸
		DATA バランスのとれた美しい乳房
		DATA 美しい曲線の双丘
		DATA 形よく均整美を保った乳房
	ENDDATA
ENDIF
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:VAGINA_DESCRIPTION
;引　数:ARG…キャラ登録番号
;戻り値:夢
;備　考:式中関数
;使用例:%VAGINA_DESCRIPTION(TARGET)%
;-------------------------------------------------
@VAGINA_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
ARG = ARG != -2 ? ARG # MASTER
IF TALENT:ARG:小柄	;小柄体型
	STRDATA LOCALS:0
		DATA 幼いワレメ
		DATA 幼い性器
		DATA 未発達の縦スジ
	ENDDATA
ELSE				;一般
	STRDATA LOCALS:0
		DATA 陰唇
		DATA 女性器
		DATA 秘唇
	ENDDATA
ENDIF
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:NIPPLE_DESCRIPTION
;概　要:乳首描写関数
;引　数:キャラ番号。省略すればMASTER
;戻り値:文字列
;備　考:式中関数
;使用例:%CALLNAME:MASTER%の%NIPPLE_DESCRIPTION(MASTER)%乳首云々…
;-------------------------------------------------
@NIPPLE_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:快Ｂ
	CASE IS >= 8500
		STRDATA LOCALS:0
			DATA 絶頂への期待に震える
			DATA ピーンと尖りきった
			DATA ビクビクと震える
		ENDDATA
	CASE IS >= 4000
		STRDATA LOCALS:0
			DATA 膨らみきった
			DATA 硬くしこった
			DATA 弾けんばかりに膨らんだ
		ENDDATA
	CASEELSE
		STRDATA LOCALS:0
			DATA 微かに膨らんだ
			DATA 僅かに膨らみを増しつつある
			DATA 未だ柔らかい
		ENDDATA
ENDSELECT
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:CLITORIS_DESCRIPTION
;概　要:Ｃ描写関数
;引　数:キャラ番号。省略すればMASTER
;戻り値:文字列
;備　考:式中関数
;使用例:%CALLNAME:MASTER%の%CLITORIS_DESCRIPTION(MASTER)%陰核云々…
;-------------------------------------------------
@CLITORIS_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:快Ｃ
	CASE IS >= 8500
		STRDATA LOCALS:0
			DATA 絶頂への期待に震える
			DATA ヒクヒクと脈打つ
		ENDDATA
	CASE IS >= 4000
		STRDATA LOCALS:0
			DATA 膨らみきった
			DATA 充血しきった
			DATA 弾けんばかりに膨らんだ
		ENDDATA
	CASEELSE
		STRDATA LOCALS:0
			DATA 微かに膨らんだ
			DATA 僅かに膨らみつつある
			DATA ささやかに膨らんだ
		ENDDATA
ENDSELECT
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:WET_DESCRIPTION
;概　要:Ｖ描写関数
;引　数:キャラ番号。省略するとMASTER
;戻り値:文字列
;備　考:式中関数（v30601.001現在、MASTERのみ参照可能）
;使用例:%WET_DESCRIPTION(MASTER)%女性器(ry
;-------------------------------------------------
@WET_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:潤滑
	CASE IS >= 1500
		LOCALS:0 = "熱い蜜液の溢れ出す"
	CASE IS >= 1000
		LOCALS:0 = "愛液の溢れ出す"
	CASE IS >= 500
		LOCALS:0 = "濡れそぼった"
	CASEELSE
		LOCALS:0 = "しっとりと湿った"
ENDSELECT
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:VAGINA_SMELL
;概　要:性器の匂い描写関数
;引　数:キャラ番号。省略するとMASTER
;戻り値:文字列
;備　考:式中関数（v30601.001現在、MASTERのみ参照可能）
;使用例:%VAGINA_SMELL(MASTER)%を漂わせる(ry
;-------------------------------------------------
@VAGINA_SMELL(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:潤滑
	CASE IS >= 1000
		LOCALS:0 = "濃厚な女の香り"
	CASE IS >= 500
		LOCALS:0 = "芳香な香り"
	CASEELSE
		LOCALS:0 = "清らかな少女の香り"
ENDSELECT
RETURNF LOCALS:0
;eraWIZを見て作りたくなった
;と言うか、限定無し口上向けにMASTERの体格とかで分岐作ってもいいのかも
;小柄体型だと幼い扱いだしねぇ

;-------------------------------------------------
;関数名:VAGINA_DESCRIPTION2
;概　要:Ｖ描写関数
;引　数:なし
;戻り値:文字列
;備　考:式中関数
;使用例:%VAGINA_DESCRIPTION2(MASTER)%
;-------------------------------------------------
@VAGINA_DESCRIPTION2(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 秘唇
	DATA 秘裂
	DATA 秘所
	DATA 媚肉
	DATA 花弁
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:PANTY_DESCRIPTION
;概　要:パンティ描写関数
;引　数:なし
;戻り値:文字列
;備　考:式中関数（v30601.001現在、MASTERのみ参照可能）
;使用例:%PANTY_DESCRIPTION(MASTER)%パンティ(ry
;-------------------------------------------------
@PANTY_DESCRIPTION(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
SELECTCASE PALAM:ARG:潤滑
	CASE IS >= 1500
		LOCALS:0 = "熱い花蜜でドロドロに濡れた"
	CASE IS >= 1000
		LOCALS:0 = "蜜液でグッショリと濡れた"
	CASE IS >= 500
		LOCALS:0 = "花蜜で汚れた"
	CASEELSE
		LOCALS:0 = "しとどに濡れた"
ENDSELECT
RETURNF LOCALS:0
;ドロワ穿いたキャラの可能性に気付いたけど、出力上は関係ないから別にいいね

;-------------------------------------------------
;関数名:DHARNESS_DESCRIPTION
;概　要:ペニスバンド描写関数
;引　数:ARG…キャラ登録番号
;戻り値:文字列
;備　考:式中関数
;使用例:%DHARNESS_DESCRIPTION(TARGET)%
;-------------------------------------------------
@DHARNESS_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 無機質な得物
	DATA 無粋な玩具
	DATA 偽りの剛直
	DATA 紛い物の楔
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:ANUS_DESCRIPTION
;概　要:アヌス描写関数
;引　数:ARG…キャラ登録番号
;戻り値:文字列
;備　考:式中関数
;使用例:%ANUS_DESCRIPTION(TARGET)%
;-------------------------------------------------
@ANUS_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 不浄の花弁
	DATA 不浄の穴
	DATA 不浄の窄まり
	DATA アヌス
	DATA 谷間の蕾
	DATA 禁断の肉穴
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:PLEASURE_DESCRIPTION
;概　要:快楽状態描写関数
;引　数:ARG…キャラ登録番号
;戻り値:文字列
;備　考:式中関数
;使用例:%PLEASURE_DESCRIPTION(TARGET)%
;-------------------------------------------------
@PLEASURE_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 身体をビクビクと震わせながら
	DATA 全身をガクガクと震わせながら
	DATA 首を左右に振りながら
	DATA 手を唇にあてがい指を噛みながら
	DATA 細い肩を震わせながら
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:Ｃ描写
;概　要:ARGのＣ描写関数
;戻り値:文字列
;備　考:式中関数
;使用例:%Ｃ描写(MASTER)%
;-------------------------------------------------
@Ｃ描写(ARG = -2)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
ARG = ARG != -2 ? ARG # MASTER
IF PENIS(ARG)
	STRDATA LOCALS:0
		DATA ペニス
		DATA くさび
		DATA 怒張
	ENDDATA
ELSE
	STRDATA LOCALS:0
		DATA クリトリス
		DATA 陰核
		DATA 肉芽
		DATA 性感の急所
	ENDDATA
ENDIF
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:SEMEN_DESCRIPTION
;概　要:SPERMではなくSEMENなんだよねっていう
;引　数:ARG…キャラ登録番号
;戻り値:文字列
;備　考:式中関数
;使用例:%SEMEN_DESCRIPTION(MASTER)%
;-------------------------------------------------
@SEMEN_DESCRIPTION(ARG)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = 
STRDATA LOCALS:0
	DATA 官能のトロミ
	DATA 白い吐液
	DATA 汚濁の液体
	DATA 白い精
	DATA 白濁液
	DATA 青臭い粘液
ENDDATA
RETURNF LOCALS:0

;-------------------------------------------------
;関数名:AFFECTION
;概　要:ARGS:0でMASTERとARG:0との好意、好感度を参照する
;引　数:ARGS:0…文字列
;引　数:ARG :0…キャラ番号。省略するとTARGET
;-------------------------------------------------
@AFFECTION(ARGS:0, ARG:0 = -2)
#FUNCTION
ARG:0 = ARG:0 != -2 ? ARG:0 # TARGET
SELECTCASE ARGS:0
	CASE "INFINITE"
		RETURNF FAVOR(10000, 10000, ARG:0) ;好意・好感度10000
	CASE "相思相愛"
		RETURNF FAVOR(3000, 3000, ARG:0) ;好意・好感度3000
	CASE "盲愛"
		RETURNF FAVOR(0, 3000, ARG:0) ;好感度3000
	CASE "溺愛"
		RETURNF FAVOR(0, 2000, ARG:0) ;好感度2000
	CASE "眷愛"
		RETURNF FAVOR(0, 1000, ARG:0) ;好感度1000
	CASE "傀儡"
		RETURNF FAVOR(2500, 0, ARG:0) ;好意2500
	CASE "調教完了"
		RETURNF FAVOR(2000, 0, ARG:0) ;好意2000
	CASE "調教済み"
		RETURNF FAVOR(1000, 0, ARG:0) ;好意1000
	CASE "目覚め"
		RETURNF FAVOR(500, 0, ARG:0) ;好意500
	CASE "恐怖"
		RETURNF !FAVOR(200, 0, ARG:0) && MARK:MASTER:トラウマ >= 3 ;好意200未満・トラウマLv3以上
	CASE "順応"
		RETURNF FAVOR(100, 100, ARG:0) ;好意・好感度100
	CASE "興味"
		RETURNF FAVOR(0, 500, ARG:0) ;好感度500
	CASE "反抗"
		RETURNF !FAVOR(1, 0, ARG:0) && MARK:(ARG:0):反抗刻印 >= 1 ;好意0以下・反抗刻印Lv1以上
	CASE "暴君"
		RETURNF !FAVOR(-100, 0, ARG:0) && FAVOR(0, 1000, ARG:0) ;好意-100・好感度1000
	CASE "嫌悪"
		RETURNF !FAVOR(-100, 0, ARG:0) ;好意-100
	CASE "不倶戴天"
		RETURNF !FAVOR(-500, 0, ARG:0) ;好意-500
	CASE "天敵"
		RETURNF !FAVOR(-1000, 0, ARG:0) ;好意-1000
	CASEELSE
		CALLF PRINT_ERRORMESSAGE(@"@AFFECTIONに無効な定義(%ARGS%)が渡されました")
		RETURNF -1
ENDSELECT

;--------------------------------------------------
;相性検索関数@RELATION_QUERY
;	引数0：対象のキャラ番号（登録番号ではない。手元に存在している必要はない）
;	引数1：検索条件（0=指定しない, 1=手元にいないキャラのみ選ぶ, 2=手元にいるキャラのみ選ぶ）
;対象キャラと全キャラとの相性を検索して、最も相性の良い相手を抽出します
;--------------------------------------------------
;戻り値一覧
;	RESULT：発見されたキャラとの相性（0=発見不可, 1=対象への相性101～199, 2=対象への相性200以上）
;	RESULT:1：発見したキャラのキャラ番号（発見不可の場合=-1）
;	RESULT:2：発見したキャラの登録番号（手元にいない場合=-1）
;	RESULTS：発見したキャラの呼び名
;	RESULTS:1：発見したキャラの名前
;--------------------------------------------------
@RELATION_QUERY(ARG, ARG:1)
#DIM LCOUNT, 1
#LOCALSIZE 7
;検索対象の登録番号を抽出
LOCAL = GETCHARA(ARG, 1)
LOCAL:6 = 0
IF LOCAL == -1
	ADDCHARA ARG
	LOCAL = CHARANUM - 1
	LOCAL:6 = 1
ENDIF
LOCAL:1 = -1
IF ARG:1 == 2
	;手元から検索
	FOR LCOUNT, 0, CHARANUM
		LOCAL:2 = LOCAL:1 < 0 ? 100 # RELATION:LOCAL:(LOCAL:1)
		LOCAL:3 = NO:LCOUNT < 0 || NO:LCOUNT >= VARSIZE("RELATION") ? 100 # RELATION:LOCAL:(NO:LCOUNT)
		SIF LOCAL:2 == 0
			LOCAL:2 = 100
		SIF LOCAL:3 == 0
			LOCAL:3 = 100
		;同値の場合ランダムで残す
		;後に引いたほうが大きい場合それを保存
		SIF (LOCAL:2 == LOCAL:3 && RAND:2 && LOCAL:1 >= 0) || LOCAL:2 < LOCAL:3
			LOCAL:1 = NO:LCOUNT
	NEXT
ELSE
	;全体から検索
	;相性を総検索しキャラ番号と相性値を抽出
	FOR LCOUNT, 0, VARSIZE("RELATION")
		;そもそもCSVがないなら見る必要なし
		SIF EXISTCSV(LCOUNT, 0)  == 0
			CONTINUE
		;キャラがいるか確認して、いるかつ検索条件=1なら次へ飛ぶ
		SIF GETCHARA(LCOUNT, 1) != -1 && ARG:1 == 1
			CONTINUE
		LOCAL:2 = LOCAL:1 < 0 ? 100 # RELATION:LOCAL:(LOCAL:1)
		LOCAL:3 = RELATION:LOCAL:LCOUNT
		SIF LOCAL:2 == 0
			LOCAL:2 = 100
		SIF LOCAL:3 == 0
			LOCAL:3 = 100
		;同値の場合ランダムで残す
		;後に引いたほうが大きい場合それを保存
		SIF ((LOCAL:2 == LOCAL:3 && RAND:2 && LOCAL:1 >= 0) || LOCAL:2 < LOCAL:3) && LCOUNT < 149
			LOCAL:1 = LCOUNT
	NEXT
ENDIF
SIF LOCAL:6
	DELCHARA CHARANUM - 1

;名前と登録番号を取得
RESULT:1 = LOCAL:1
IF LOCAL:1 >= 0
	RESULT:2 = GETCHARA(LOCAL:1, 1)
	IF RESULT:2 >= 0
		RESULTS = %CALLNAME:(RESULT:2)%
		RESULTS:1 = %NAME:(RESULT:2)%
	ELSE
		RESULTS = %CSVCALLNAME(LOCAL:1, 0)%
		RESULTS:1 = %CSVNAME(LOCAL:1, 0)%
	ENDIF
ENDIF
;数値に応じてRETURN
SIF RELATION:LOCAL:(LOCAL:1) > 199
	RETURN 2
SIF RELATION:LOCAL:(LOCAL:1) > 100
	RETURN 1
RETURN 0
