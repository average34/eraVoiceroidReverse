;-------------------------------------------------
;KCOLOR関数 (R#専用)
;-------------------------------------------------
;・口上内で別キャラのセリフを記述する際の違和感を低減する目的であり、怪しくは無い。安全です(欺瞞)
;・KOJO_COLORとの違いは色値だけ返す式中関数であることと、キャラ名から引くので居ないキャラでも平気なこと
;・居ないキャラ分をまともにやろうとすると逐一 TRYCCALLFORM KOJO_COLOR_K{NO:ASSI} とかしないといけない雰囲気だったので仕方ないね
;・口上からイベント関数触るのは趣味じゃないのだけど、本体同梱ならまぁいいか的なノリ
;・そういった理由から何となく先行投入的な雰囲気
;・テーブルの仕様は正直微妙な感じも否めない
;-------------------------------------------------

;-------------------------------------------------
;関数名:KCOLOR
;概　要:メイン口上色取得関数
;引　数:ARGS:0…キャラ名(NAME)または呼び名(CALLNAME)。文字列でキャラ番号(NO)を渡すことも可能
;戻り値:指定キャラの口上色。未定義の場合は0xA0A0A0が返る
;備　考:式中関数
;指定キャラの口上色(特に条件の無い場合)を取得する
;つまり日常イベント重点な
;※タイトル画面やロード画面では使えない風味
;-------------------------------------------------
@KCOLOR(ARGS:0)
#FUNCTION
#LOCALSIZE 1
LOCAL:0 = ISNUMERIC(ARGS:0) ? TOINT(ARGS:0) # GETNUM(RELATION, ARGS:0)
SIF !EXISTCSV(LOCAL:0)
	THROW @KCOLORで例外が発生しました。存在しないキャラ名もしくはキャラ番号です（%ARGS:0%）
RETURNF _KCOLOR_TBL(LOCAL:0)


;-------------------------------------------------
;関数名:_LOAD_KCOLOR
;概　要:メイン口上色走査関数
;引　数:なし
;戻り値:なし(RESULT保護)
;備　考:内部関数
;内部関数なので他所から呼び出してはいけない。いいね？
;確実に使うには要所要所で呼ばないとダメみたいだけど、
;とりあえず以下の2地点で呼んでおけば大体はカバー出来そうな雰囲気
;KCOLOR呼び出し時にテーブル無かったら作る方式はTRYCCALLFORM使う都合上式中関数化出来なくなるので却下されました
;-------------------------------------------------
@_LOAD_KCOLOR
#LOCALSIZE 3
LOCAL:0 = RESULT
LOCAL:1 = GETCOLOR()
LOCAL:2 = 0
WHILE EXISTCSV(LOCAL:2)
	TRYCCALLFORM KOJO_COLOR_K{LOCAL:2}
		CALLF _KCOLOR_TBL(LOCAL:2++, GETCOLOR())
	CATCH
		CALLF _KCOLOR_TBL(LOCAL:2++, 0xA0A0A0)	;未定義の場合はデフォルト色
	ENDCATCH
WEND
SETCOLOR LOCAL:1
RETURN LOCAL:0

@EVENTFIRST
#PRI
SIF !KCOLOR("0")
	CALL _LOAD_KCOLOR()

@EVENTLOAD
#PRI
SIF !KCOLOR("0")
	CALL _LOAD_KCOLOR()


;-------------------------------------------------
;関数名:_KCOLOR_TBL
;概　要:メイン口上色テーブル
;引　数:ARG:0…キャラ番号(NO)
;　　　 ARG:1…[省略可]0以上の場合はSETモードとして動作、指定キャラの口上色を保持する
;戻り値:指定キャラの口上色。未定義の場合は0xA0A0A0が返る
;備　考:内部関数/式中関数
;内部関数なので他所から呼び出してはいけない。いいね？
;256なのが何とも微妙。まぁいいや
;-------------------------------------------------
@_KCOLOR_TBL(ARG:0, ARG:1 = -1)
#FUNCTION
#LOCALSIZE 256
LOCAL:(ARG:0) = ARG:1 >= 0 ? ARG:1 # LOCAL:(ARG:0)
RETURNF LOCAL:(ARG:0)
