;-----------------------------------------
;キャラメイクデータのセーブ&ロード画面
;ARGが真のときセーブ画面
;-----------------------------------------
@CHARAMAKE_DATA_MENU, ARG
#LOCALSIZE 2
#LOCALSSIZE 1
#DIMS LOCALS_A, 7
#DIMS LOCALS_B, 100
#DIM LINE
#DIM SAVE
#DIM CHARA
#DIM PAGE
LOADGLOBAL
PAGE = 0
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
FOR LOCAL, 0, 5
	RESETCOLOR
	;データが存在しないもの
	IF GLOBALS:LOCAL == ""
		LOCALS = [{LOCAL}] ＥＭＰＴＹ
		PRINTFORML ┌%LOCALS + (84 - STRLENS(LOCALS)) / 2 * "─"%┐
		PRINTFORML │%84 * " "%│
		LOCALS = 
	ELSE
		SPLIT GLOBALS:LOCAL, "_", LOCALS_A
		SPLIT LOCALS_A:1, "/", LOCALS_B
		LOCALS = [{LOCAL}] %LOCALS_B:1%
		;メモ機能封印
		; メモ:%LOCALS_A%
		SIF STRLENS(LOCALS) % 2
			LOCALS += " "
		PRINTFORM ┌
		PRINTBUTTON LOCALS, LOCAL
		PRINTFORML %(84 - STRLENS(LOCALS)) / 2 * "─"%┐
		LOCALS = 
		FOR LOCAL:1, 0, VARSIZE("TALENT")
			SIF TALENTNAME:(LOCAL:1) == ""
				CONTINUE
			SIF STRFIND(LOCALS_A:6, TALENTNAME:(LOCAL:1)) == -1
				CONTINUE
			IF STRLENS(LOCALS) + STRLENS(TALENTNAME:(LOCAL:1)) + 2 + 4 > 84
				PRINTFORML │%LOCALS,84,LEFT%│
				LOCALS = 
			ENDIF
		 	LOCALS += "["+TALENTNAME:(LOCAL:1)+"]" 
		NEXT
	ENDIF
 	IF LOCALS != ""
 		PRINTFORML │%LOCALS,84,LEFT%│
 		LOCALS = ""
 	ENDIF
	PRINTFORML └%42 * "─"%┘
NEXT
PRINTFORML [99] 戻る
INPUT
SIF RESULT == 99
	RETURN 0
IF RANGE(RESULT, 0, 4)
	SAVE = RESULT
	;データがある場合キャラクター表示は確実に行う
	IF GLOBALS:SAVE != ""
		CALL CHARAMAKE_LOAD, RESULT
		CHARA = RESULT
		CALL PRINTSTATE_CHARAMAKE(CHARA)
	ENDIF
	;ロード
	IF !ARG
		IF GLOBALS:SAVE != ""
			CALL PRINT_DIALOG("このキャラクターをロードしてもよろしいですか？", 1, 1, 1)
			PRINTL [0] はい
			PRINTL [1] いいえ
			CALL INPUTINT(0, 1)
			IF RESULT
				DELCHARA CHARA
				GOTO START
			ELSE
				;主人をサクる
				DELCHARA 0
				;紫と主人公を入れ替えておく
				SWAPCHARA 0, 1
				RETURN 1
			ENDIF
		ENDIF
	ELSE
		IF GLOBALS:SAVE != ""
			;どちらにしろ。消す
			DELCHARA CHARA
			CALL PRINT_DIALOG("データを上書きしてもよろしいですか？", 1, 1, 1)
			PRINTL [0] はい
			PRINTL [1] いいえ
			CALL INPUTINT(0, 1)
			SIF RESULT
				GOTO START
		ENDIF
		;キャラクター保存
		CALL CHARAMAKE_SAVE, SAVE, MASTER
		CALL PRINT_DIALOG("キャラクターを保存しました", 2, 2, 2)
	ENDIF
ENDIF
GOTO START

;=========================================================================
;@CHARA_MAKER_SAVE
;キャラメイカーで作成したデータをGLOBALSに保存をする関数
;ARG   GLOBALS:ARGにデータを保存する
;ARG:1 ARG:1のキャラ登録番号の素質をセーブする。省略した場合は0番のキャラ
;		0    1    2   3     4   5       6
;保存は メモ_名前_ABL_CFLAG_EXP_MAXBASE_TALENT
;==========================================================================
@CHARAMAKE_SAVE, ARG, ARG:1 = -1
#DIM CHARA, 1
CHARA = ARG:1 == -1 ? 0 # ARG:1
;初期化
GLOBALS:ARG = 
;とりあえずメモ機能いらないかな？
;PRINTFORML このセーブデータの内容を示すメモを入力してください
;$INPUT_LOOP
;INPUTS
;PRINTFORML 「%RESULTS%」でよろしいですか？
;PRINTL [0] はい
;PRINTL [1] いいえ
;LOCALS = %RESULTS%
;CALL INPUTINT(0, 1)
;IF RESULT
;	PRINTL では、入力しなおしてください
;	GOTO INPUT_LOOP
;ENDIF
GLOBALS:ARG += LOCALS + "_"
;名前関係
GLOBALS:ARG += NAME:(ARG:1) + "/" + CALLNAME:(ARG:1) + "_"
;ABL
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("ABL")
	SIF ABLNAME:LOCAL == ""
		CONTINUE
	IF ABL:CHARA:LOCAL
		SIF LOCAL:1++ != 0
			GLOBALS:ARG += "/"
		GLOBALS:ARG += @"%ABLNAME:LOCAL%,{ABL:CHARA:LOCAL}"
	ENDIF
NEXT
GLOBALS:ARG += "_"
;CFLAG
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("BASE")
	SIF CFLAGNAME:LOCAL == ""
		CONTINUE
	IF CFLAG:CHARA:LOCAL
		SIF LOCAL:1++ != 0
			GLOBALS:ARG += "/"
		GLOBALS:ARG += @"%CFLAGNAME:LOCAL%,{CFLAG:CHARA:LOCAL}"
	ENDIF
NEXT
GLOBALS:ARG += "_"
;EXP
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("EXP")
	SIF EXPNAME:LOCAL == ""
		CONTINUE
	IF EXP:CHARA:LOCAL
		SIF LOCAL:1++ != 0
			GLOBALS:ARG += "/"
		GLOBALS:ARG += @"%EXPNAME:LOCAL%,{EXP:CHARA:LOCAL}"
	ENDIF
NEXT
GLOBALS:ARG += "_"
;MAXBASE
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("MAXBASE")
	SIF BASENAME:LOCAL == ""
		CONTINUE
	IF MAXBASE:CHARA:LOCAL
		SIF LOCAL:1++ != 0
			GLOBALS:ARG += "/"
		GLOBALS:ARG += @"%BASENAME:LOCAL%,{MAXBASE:CHARA:LOCAL}"
	ENDIF
NEXT
GLOBALS:ARG += "_"
;TALENT
LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("TALENT")
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	IF TALENT:CHARA:LOCAL
		SIF LOCAL:1++ != 0
			GLOBALS:ARG += "/"
		GLOBALS:ARG += @"%TALENTNAME:LOCAL%,{TALENT:CHARA:LOCAL}"
	ENDIF
NEXT
SAVEGLOBAL

;=========================================================================
;@CHARA_MAKER_LOAD
;キャラメイカーで作成したデータをGLOBALSに保存をする関数
;ARG   GLOBALS:ARGのデータを読み込む
;		0    1    2   3     4   5       6
;保存は メモ_名前_ABL_CFLAG_EXP_MAXBASE_TALENT
;==========================================================================
@CHARAMAKE_LOAD, ARG
#DIMS RESULTS_A, 7
#DIMS RESULTS_B, 100
#DIMS RESULTS_C, 2
#DIM CHARA
ADDVOIDCHARA
CHARA = CHARANUM -1
SPLIT GLOBALS:ARG, "_", RESULTS_A
SPLIT RESULTS_A:1, "/", RESULTS_B
;名前
NAME:CHARA = %RESULTS_B%
CALLNAME:CHARA = %RESULTS_B:1%


SPLIT RESULTS_A:2, "/", RESULTS_B
;ABL
FOR LOCAL, 0, RESULT
	SIF RESULTS_B == ""
		CONTINUE
	SPLIT RESULTS_B:LOCAL, ",", RESULTS_C
	ABL:CHARA:RESULTS_C = TOINT(RESULTS_C:1)
NEXT

SPLIT RESULTS_A:3, "/", RESULTS_B
;CFLAG
FOR LOCAL, 0, RESULT
	SIF RESULTS_B == ""
		CONTINUE
	SPLIT RESULTS_B:LOCAL, ",", RESULTS_C
	CFLAG:CHARA:RESULTS_C = TOINT(RESULTS_C:1)
NEXT

SPLIT RESULTS_A:4, "/", RESULTS_B
;EXP
FOR LOCAL, 0, RESULT
	SIF RESULTS_B == ""
		CONTINUE
	SPLIT RESULTS_B:LOCAL, ",", RESULTS_C
	EXP:CHARA:RESULTS_C = TOINT(RESULTS_C:1)
NEXT

SPLIT RESULTS_A:5, "/", RESULTS_B
;MAXBASE
FOR LOCAL, 0, RESULT
	SIF RESULTS_B == ""
		CONTINUE
	SPLIT RESULTS_B:LOCAL, ",", RESULTS_C
	MAXBASE:CHARA:RESULTS_C = TOINT(RESULTS_C:1)
	BASE:CHARA:RESULTS_C = TOINT(RESULTS_C:1)
NEXT

SPLIT RESULTS_A:6, "/", RESULTS_B
;TALENT
FOR LOCAL, 0, RESULT
	SIF RESULTS_B == ""
		CONTINUE
	SPLIT RESULTS_B:LOCAL, ",", RESULTS_C
	TALENT:CHARA:RESULTS_C = TOINT(RESULTS_C:1)
NEXT
RETURN CHARA

;キャラデータ表示関数
@PRINTSTATE_CHARAMAKE(ARG)
DRAWLINE
PRINTFORM ■ %CALLNAME:ARG% ■
PRINTL 
PRINTFORML [LV {CFLAG:ARG:0,2}  EXP {CFLAG:ARG:1,6}/{CFLAG:ARG:4,6}  NEXT {CFLAG:ARG:必要経験値 - CFLAG:ARG:1,6}]
CALL PRINT_BASEL("体力", ARG, 0, 32, 1)
CALL PRINT_BASEL("気力", ARG, 1, 32, 1)
SIF MAXBASE:ARG:理性
	CALL PRINT_BASEL("理性", ARG, 5, 32, 1)
SIF MAXBASE:ARG:射精
	CALL PRINT_BASEL("精力", ARG, 2, 32, 1)
CALL NEW_PRINT_TALENT(ARG)
CALL NEW_PRINT_EXP(ARG)
CALL NEW_PRINT_ABL(ARG)

