;────────────────────────────────────
;ショップメニューの表示
;────────────────────────────────────
@SHOW_SHOP
CALL VERSION_UP

DRAWLINE
PRINTFORML {FLAG:年数+1}年{FLAG:日数+1}日目(%GET_TIME()%) %GET_SEASON()% %GET_DAY()%の行 　ご主人様：%CALLNAME:1%
SIF TARGET >= 0
	PRINTFORML %CALLNAME:MASTER%を%CALLNAME:TARGET%が調教中
SIF ASSI:1 >= 0
	PRINTFORML （助手１：%CALLNAME:(ASSI:1)%）\@ ASSI:2 >= 0 ? （助手２：%CALLNAME:(ASSI:2)%） # \@
HTML_PRINT HTML_BASE("体力", MASTER, 0, 16) + HTML_BASE("気力", MASTER, 1, 16)
SIF TARGET >= 0
	PRINTFORML (%CALLNAME%の魔力　{CFLAG:魔力})
[SKIPSTART]
要らないと思うのでコメントアウトしておきます@/L
IF TARGET >= 0
	PRINTFORML 【依存度　test版】　(%CALLNAME%の%CALLNAME:MASTER%への依存度：{CFLAG:依存度}　%CALLNAME:MASTER%の%CALLNAME:(FLAG:ご主人様)%たちへの依存度：{CFLAG:MASTER:依存度})
	PRINTL 　きりのいい日数での数値を、ご主人様の名前・主人公の性別、できれば大まかなプレイ方針もつけて
	PRINTL 　スレなどにご報告いただけると幸いです。エンディング分岐等に使う予定の値です。
ENDIF
[SKIPEND]
DRAWLINE
;調教開始
{
CALL HTMLPRINTL(HTMLAUTOBUTTON("[100] - 調教の時間が来た", "100", "調教を開始します", , , TARGET >= 0, 
	"[---] - 調教者を決めてください", "調教を開始する為には、調教者を決定する必要があります", 0xA0A0A0))
}
;能力表示
CALL HTMLPRINTL(HTMLBUTTON("[101] - 能力の表示", "101", "調教者と調教対象の各種経験値等を確認します"))
;魔力消費
SIF TARGET >= 0
	CALL HTMLPRINTL(HTMLBUTTON("[102] - 魔力を消費する", "102", "魔力を使ってアイテムや能力等の取得、新たな調教者の追加を行います"))
;調教者切り替え
SIF CHARANUM > 2
	CALL HTMLPRINTL(HTMLBUTTON("[110] - 調教者/助手の切り替え", "110", "現在の調教者/助手を他の調教者に変更します"))
;衣装設定
SIF TARGET > 0
	CALL HTMLPRINTL(HTMLBUTTON("[155] - 衣装設定", "155", "調教者と調教対象の衣装を設定します"))
;データベース
CALL HTMLPRINTL(HTMLBUTTON("[177] - データベースを見る", "177", "各種調教データを確認します"))
;コンフィグ
CALL HTMLPRINTL(HTMLBUTTON("[178] - コンフィグ", "178", "ゲームの設定を行います"))
;セーブ
CALL HTMLPRINTL(HTMLBUTTON("[200] - セーブ", "200", "現在のデータを保存します"))
;ロード
CALL HTMLPRINTL(HTMLBUTTON("[300] - ロード", "300", "保存したデータを読み込みます"))

;Emueraデバッグモード起動時
[IF_DEBUG]
{
	CALL HTMLPRINTL(HTMLBUTTON("[777] - デバッグモード" + (FLAG:デバッグ ? "の終了" # ""), "777", 
		FLAG:デバッグ ? "通常画面に戻ります" # "バリアントのデバッグモードを起動します"))
}
	IF FLAG:デバッグ
		PRINTFORML \n--デバッグコマンドは手軽に状況を変えるだけのために作ったので、\n以下のコマンドによる異常は動作保障外にします--\n
		;魔力増加
		SIF TARGET >= 0
			CALL HTMLPRINTL(HTMLBUTTON("[778] - 魔力増加", "778", "調教者の魔力を増やします"))
		;転生の儀式
		CALL HTMLPRINTL(HTMLBUTTON("[779] - 転生の儀式", "779", "プレイヤーキャラの再設定画面へ進みます"))
		;フラグチェック
		CALL HTMLPRINTL(HTMLBUTTON("[780] - フラグチェック", "780", "CFLAGとFLAGの状況を確認します"))
		;オートモード
		CALL HTMLPRINTL(HTMLBUTTON("[781] - オートモード", "781", "調教対象の行動を自動で行うモードの設定画面を呼び出します"))
		;素質補正確認
		CALL HTMLPRINTL(HTMLBUTTON("[782] - 素質補正確認", "782", "各キャラのコマンド選択傾向を確認します"))
	ENDIF
[ENDIF]

PRINTL 
CALL HTMLPRINTL(HTMLBUTTON("[888] - 口上表示設定", "888", "口上の表示/非表示を設定します"))
DRAWLINE


;────────────────────────────────────
;ショップメニュー
;────────────────────────────────────
@USERSHOP
IF RESULT == 100 && TARGET >= 0
	BEGIN TRAIN
	RETURN 1
ELSEIF RESULT == 101
	CALL SHOW_CHARADATA
ELSEIF RESULT == 102 && TARGET >= 0
	CALL BONUS_GAIN
	RETURN 1
ELSEIF RESULT == 110 && CHARANUM > 2
	CALL CHANGE_TARGET_ASSI
ELSEIF RESULT == 155 && TARGET >= 0
	CALL CLOTHES_SETTING
ELSEIF RESULT == 177
	CALL DATABASE_OUTPUT
ELSEIF RESULT == 178
	CALL CONFIGURE
ELSEIF RESULT == 200
	;SAVEGAME
	;→SYSTEM_DATAMENU.ERB
	CALL DATAMENU("SAVE")
ELSEIF RESULT == 300
	;LOADGAME
	;→SYSTEM_DATAMENU.ERB
	CALL DATAMENU("LOAD")
ELSEIF RESULT == 888
	CALL KOJO_SET
ENDIF
;Emueraデバッグモード起動時
[IF_DEBUG]
	IF 0
	ELSEIF RESULT == 777
		PRINTFORML デバッグ用のメッセージを表示\@ FLAG:デバッグ ? しない # する \@ように設定しました
		FLAG:デバッグ = !FLAG:デバッグ ? 1 # 0
		WAIT
	ELSEIF RESULT == 778 && FLAG:デバッグ && TARGET >= 0
		CFLAG:魔力 += 10000
		PRINTFORML %CALLNAME%の魔力+10000されました
		WAIT
	ELSEIF RESULT == 779 && FLAG:デバッグ
		PRINTW 調教対象（主人公）を再設定できます
		CALL MASTER_CUSTOM
		CALL BASE_MASTER_SETUP
	ELSEIF RESULT == 780 && FLAG:デバッグ
		CALL FLAGCHECK
	ELSEIF RESULT == 781 && FLAG:デバッグ
		CALL SET_AUTO
	ELSEIF RESULT == 782 && FLAG:デバッグ
		CALL CHECK_TALENT_CRI
	ENDIF
[ENDIF]


@KOJO_SET
PRINTL キャラ口上をどう表示しますか？
PRINTL [0] - 表示OFF
PRINTL [1] - 表示ON　※推奨
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
		PRINTW キャラ口上を表示しません
	CASE 1
		PRINTW キャラ口上を表示します
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
FLAG:口上 = RESULT
PRINTL 

;────────────────────────────────────
;調教者/助手変更
;────────────────────────────────────
@CHANGE_TARGET_ASSI
#DIM OLD_T, 3

OLD_T = TARGET, (ASSI:1), (ASSI:2)

$CHANGE_LOOP
PRINTFORML 調教者/助手を変更できます
CALL KOJO_COLOR(TARGET)
PRINTBUTTON @"[  0] 調教者 - \@ TARGET >= 0 ? [{TARGET,2}] %CALLNAME:TARGET% # 調教者はいません \@", 0
PRINTL 
RESETCOLOR
CALL KOJO_COLOR(ASSI:1)
PRINTBUTTON @"[  1] 助手1  - \@ ASSI:1 >= 0 ? [{ASSI:1,2}] %CALLNAME:(ASSI:1)% # 助手1はいません \@", 1
PRINTL 
IF CHARANUM > 3
	RESETCOLOR
	CALL KOJO_COLOR(ASSI:2)
	PRINTBUTTON @"[  2] 助手2  - \@ ASSI:2 >= 0 ? [{ASSI:2,2}] %CALLNAME:(ASSI:2)% # 助手2はいません \@", 2
	PRINTL 
ENDIF
IF TARGET < 0
	SETCOLOR 160, 160, 160
	PRINTFORML [---] 調教者を決めてください
	RESETCOLOR
ELSE
	RESETCOLOR
	PRINTFORML [  9] 変更終了
ENDIF
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
		CALL CHANGE_TARGET
		GOTO CHANGE_LOOP
	CASE 1
		CALL CHANGE_ASSI(1)
		GOTO CHANGE_LOOP
	CASE 2
		IF CHARANUM > 3
			CALL CHANGE_ASSI(2)
			GOTO CHANGE_LOOP
		ENDIF
	CASE 9
		IF TARGET >= 0
			;助手1が空の場合、助手2を助手1とする
			SIF ASSI:1 == -1
				SWAP ASSI:1, ASSI:2
			CALL CHANGE_TARGET_ASSI_MESSAGE(OLD_T)
			RETURN RESULT
		ENDIF
ENDSELECT
CLEARLINE 1
GOTO INPUT_LOOP

@CHANGE_TARGET_ASSI_MESSAGE(OLD_T)
#DIM LCOUNT
#DIM REF OLD_T, 0
#DIM NEW_T, 3

NEW_T = TARGET, (ASSI:1), (ASSI:2)

;変更が全く行われていない場合
IF OLD_T:0 == NEW_T:0 && OLD_T:1 == NEW_T:1 && OLD_T:2 == NEW_T:2
	;調教者がご主人様の場合
	IF TALENT:TARGET:ご主人様
		PRINTFORMW %CALLNAME:TARGET%はこのまま自分で%CALLNAME:MASTER%を調教することにした…
	;それ以外
	ELSE
		PRINTFORMW このまま%CALLNAME:TARGET%に%CALLNAME:MASTER%を調教させることにした…
	ENDIF
ENDIF
;調教者の変更が行われている場合
IF OLD_T:0 != NEW_T:0
	;口上の為に専用変数に交代元・先の登録番号を入れる
	SHOP_OLDTARGET = OLD_T:0
	SHOP_NEWTARGET = NEW_T:0
	;新調教者カットイン
	PRINTL
	CALL CHARA_CUTIN(CSTR:10, CSTR:11, CSTR:ローマ字)
	PRINTL
	;一時的にTARGETを旧調教者に戻す
	TARGET = OLD_T:0
	;旧調教者口上呼び出し
	CALL KOJO_EVENT(102)
	;ご主人様→ご主人様以外への変更
	IF TARGET >= 0 && TALENT:TARGET:ご主人様
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:SHOP_NEWTARGET%に%CALLNAME:MASTER%の調教を委託した…
	;ご主人様以外→ご主人様への変更
	ELSEIF TALENT:SHOP_NEWTARGET:ご主人様
		PRINTFORMW %CALLNAME:SHOP_NEWTARGET%は自分で%CALLNAME:MASTER%を調教することにした…
	;ご主人様以外→ご主人様以外への変更
	ELSE
		PRINTFORMW \@ TARGET >= 0 ? %CALLNAME:TARGET%に代わり、 # \@%CALLNAME:SHOP_NEWTARGET%が%CALLNAME:MASTER%を調教することになった…
	ENDIF
	;TARGETを新調教者に
	TARGET = NEW_T:0
	;新調教者口上呼び出し
	CALL KOJO_EVENT(102, 1)
ENDIF
;新助手全員を調査
FOR LCOUNT, 1, 3
	;助手LCOUNTが新規任命者の場合
	IF FINDLASTELEMENT(OLD_T, NEW_T:LCOUNT, 1) == -1
		;ASSIに新規任命者を指定
		ASSI = NEW_T:LCOUNT
		;助手任命口上呼び出し
		CALL KOJO_EVENT_ASSI(102)
		CALL VOIDLINE_IF(RESULT)
		;ASSIを元に戻す
		ASSI = ASSI:1
	ENDIF
NEXT

;調教者選択
@CHANGE_TARGET(ARG)
LOCALS = 
LOCALS += "<識別>CHANGE_TARGET"
LOCALS += "<上部>CHANGE_TARGET_SHOW"
LOCALS += "<条件>CHANGE_TARGET_REQU"
LOCALS += "<口上>調教者"
LOCALS += ARG ? "<取消>キャンセル不可" # "<取消>戻る"
LOCALS += "<項目>名前/体力・気力/経験値"
CALL SELECT_CHARA_LIST(LOCALS)
SIF RESULT != -1
	CALL SET_TARGET_ASSI(TARGET, 0, RESULT)

@CHANGE_TARGET_SHOW
PRINTFORML 現在 \@ TARGET >= 0 ? [{TARGET,2}] %CALLNAME:TARGET%が調教中 # 調教者はいません \@
PRINTL 今回の調教者は誰ですか？

@CHANGE_TARGET_REQU(ARG)
RETURN ARG == 0 ? 0 # 1

;助手選択
@CHANGE_ASSI(ARG)
LOCALS = 
LOCALS += "<識別>CHANGE_TARGET"
LOCALS += @"<上部>CHANGE_ASSI_SHOW_{ARG}"
LOCALS += "<条件>CHANGE_TARGET_REQU"
LOCALS += "<口上>調教者"
LOCALS += "<選択>CHANGE_ASSI_CHOI"
LOCALS += "<取消>戻る"
LOCALS += "<項目>名前/体力・気力/経験値"
CALL SELECT_CHARA_LIST(LOCALS)
IF RESULT != -1
	SIF RESULT == -2
		RESULT = -1
	CALL SET_TARGET_ASSI(ASSI, ARG, RESULT)
ENDIF

@CHANGE_ASSI_SHOW_1
CALL CHANGE_ASSI_SHOW(1)
@CHANGE_ASSI_SHOW_2
CALL CHANGE_ASSI_SHOW(2)
@CHANGE_ASSI_SHOW(ARG)
IF ASSI:1 >= 0
	PRINTFORML 現在 [{ASSI:1,3}] %CALLNAME:(ASSI:1)%\@ ASSI:2 >= 0 ? 、[{ASSI:2,3}] %CALLNAME:(ASSI:2)% # \@が助手
ELSE
	PRINTL 現在 助手はいません
ENDIF
PRINTFORML 今回の助手{ARG}は誰ですか？
PRINTFORML 　[0]「なし」※助手{ARG}を指定しません

@CHANGE_ASSI_CHOI(ARGS)
IF ARGS == "0"
	RESULTS '= "-2"
	RETURN 0
ENDIF
RETURN 1

;調教者/助手の任命に応じて適宜異動・解任を行う
@SET_TARGET_ASSI(LREF, ARG, ARG:1)
#DIM REF LREF,0
;前任者と役割の交換を行う
LOCAL = LREF:ARG
IF ARG:1 != -1
	TARGET = TARGET == ARG:1 ? LOCAL # TARGET
	ASSI:1 = ASSI:1 == ARG:1 ? LOCAL # ASSI:1
	ASSI:2 = ASSI:2 == ARG:1 ? LOCAL # ASSI:2
ENDIF
;役割の任命を行う
LREF:ARG = ARG:1
;ASSIを設定
ASSI = ASSI:1

;────────────────────────────────────
;キャラのステータス表示
;────────────────────────────────────
@SHOW_CHARADATA(ARG)
LOCALS = 
LOCALS += "<識別>SHOW_CHARADATA"
LOCALS += "<口上>調教者"
LOCALS += "<取消>戻る"
LOCALS += "<項目>名前/体力・気力/経験値"
CALL SELECT_CHARA_LIST(LOCALS)
;戻る選択時脱出
SIF RESULT < 0
	RETURN 0
LOCAL = RESULT
;ステータス表示
CALL PRINT_STATUS(RESULT)
;再描画
RESTART

@SHOW_CHARADATA_PAD
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM L_SELECT
#DIM L_CHARA, 4
#DIM LINE
LINE = LINECOUNT
L_SELECT = 0
L_CHARA = MASTER, TARGET, ASSI:1, ASSI:2
$START
CLEARLINE LINECOUNT - LINE

DRAWLINE
FOR LOCAL, 0, 4
	SIF L_CHARA:LOCAL == -1
		CONTINUE
	SIF LOCAL == L_SELECT
		SETCOLOR 0x7070C0
	LOCALS = 
	LOCALS += @" %CALLNAME:LOCAL,16,LEFT% 体力({MAX(BASE:LOCAL:0,0),4}/{MAXBASE:LOCAL:0,4}) "
	LOCALS += @" [LV {CFLAG:LOCAL:0,2} NEXT {CFLAG:LOCAL:必要経験値 - CFLAG:LOCAL:1,6}]"
	LOCALS += AUTO_SPLIT("_調教者_助手１_助手２", "_", LOCAL)
	SIF TALENT:(L_CHARA:LOCAL):ご主人様
		LOCALS += "<ご主人様>"
	PRINTBUTTON LOCALS, UNICODE(10000 + LOCAL)
	RESETCOLOR
	PRINTL 
	SIF LOCAL == 0
		DRAWLINE
NEXT
DRAWLINE
PRINTBUTTON "[" + KEYMARK_B+ "] - 戻る", KEY_B
ONEINPUTS
SELECTCASE RESULTS
	CASE KEY_A
		CALL PRINT_STATUS, L_CHARA:L_SELECT
	CASE KEY_B
		RETURN 0
	CASE KEY_UP
		DO 
			SIF --L_SELECT == -1
				L_SELECT = 3
		LOOP L_CHARA:L_SELECT == -1
	CASE KEY_DOWN
		L_SELECT++
		SIF L_CHARA:L_SELECT == -1
			L_SELECT = 0
ENDSELECT
GOTO START
@FLAGCHECK
PRINTL [0] - 戻る
PRINTL [1] - FLAG
PRINTL [2] - CFLAG
PRINTL [3] - FLAGのダンプ
PRINTL [4] - CFLAGのダンプ
$INPUT_LOOP1
INPUT
SELECTCASE RESULT
	CASE 0
	CASE 1
		VARSIZE FLAG
		LOCAL = RESULT
		PRINTFORML FLAG番号(0～{LOCAL-1})を入力してください。
		$INPUT_LOOP2
		INPUT
		IF RESULT < 0 || LOCAL <= RESULT
			CLEARLINE 1
			REUSELASTLINE FLAGの範囲は(0～{LOCAL-1})です。
			GOTO INPUT_LOOP2
		ENDIF
		PRINTFORMW FLAG:{RESULT} = {FLAG:RESULT}
		RESTART
	CASE 2
		PRINTFORML 対象キャラの登録番号(0～{CHARANUM-1})を入力してください。
		$INPUT_LOOP3
		INPUT
		IF RESULT < 0 || CHARANUM <= RESULT
			CLEARLINE 1
			REUSELASTLINE 存在しません。
			GOTO INPUT_LOOP3
		ENDIF
		LOCAL = RESULT
		VARSIZE CFLAG
		LOCAL:1 = RESULT
		PRINTFORML %CALLNAME:LOCAL%が選択されました。
		PRINTFORML CFLAG番号(0～{LOCAL:1-1})を入力してください。
		$INPUT_LOOP4
		INPUT
		IF RESULT < 0 || LOCAL:1 <= RESULT
			CLEARLINE 1
			REUSELASTLINE CFLAGの範囲は(0～{LOCAL:1-1})です
			GOTO INPUT_LOOP4
		ENDIF
		PRINTFORMW %CALLNAME:LOCAL%のCFLAG:{RESULT} = {CFLAG:LOCAL:RESULT}
		RESTART
	CASE 3
		PRINTL FLAG一覧(値が0のものは省略)
		FOR LOCAL, 0, VARSIZE("FLAG")
			SIF FLAG:LOCAL
				PRINTFORMLC [FLAG:{LOCAL,4}={FLAG:LOCAL,10}]
		NEXT
		WAIT
		RESTART
	CASE 4
		PRINTFORML 対象キャラの登録番号(0～{CHARANUM-1})を入力してください。
		$INPUT_LOOP5
		INPUT
		IF RESULT < 0 || CHARANUM <= RESULT
			CLEARLINE 1
			REUSELASTLINE 存在しません。
			GOTO INPUT_LOOP5
		ENDIF
		PRINTFORML %CALLNAME:RESULT%のCFLAG一覧(値が0のものは省略)
		FOR LOCAL, 0, VARSIZE("CFLAG")
			SIF CFLAG:RESULT:LOCAL
				PRINTFORMLC [CFLAG:{LOCAL,4}={CFLAG:RESULT:LOCAL,10}]
		NEXT
		WAIT
		RESTART
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP1
ENDSELECT

@SET_AUTO
#DIM SELECT
#DIM LINE
#DIMS セット名, 5
#LOCALSSIZE 100
REDRAW 0
SELECT = -1
VARSET LOCALS
LINE = LINECOUNT
FOR LOCAL, 0, 5
	SIF !STRCOUNT(GLOBALS:@"オートモード登録{LOCAL}", "_")
		GLOBALS:@"オートモード登録{LOCAL}" = 登録{LOCAL}_%GLOBALS:@"オートモード登録{LOCAL}"%
	SPLIT GLOBALS:@"オートモード登録{LOCAL}", "_", RESULTS
	セット名:LOCAL = %RESULTS%
NEXT
SPLIT GLOBALS:@"オートモード登録{FLAG:選択セット}", "_", RESULTS
SPLIT RESULTS:1, "/", LOCALS
$START
;削除コマンド及び異常値のチェック
FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL == ""
		CONTINUE
	SIF GETNUM(TRAINNAME, LOCALS:LOCAL) >= 0 || LOCALS:LOCAL == "なすがまま"
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
;空行の整理
FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
	SIF LOCALS:LOCAL != ""
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
;新規追加及されたコマンドのチェック
FOR LOCAL, 0, 100
	SIF TRAINNAME:LOCAL == ""
		CONTINUE
	;仕様上通常選択できると問題があるのでカットしておく
	SIF TRAINNAME:LOCAL == "なすがまま"
		CONTINUE
	SIF FINDELEMENT(LOCALS, TRAINNAME:LOCAL) >= 0
		CONTINUE
	LOCALS:(100 - MATCH(LOCALS, "")) = %TRAINNAME:LOCAL%
NEXT
CLEARLINE LINECOUNT - LINE
DRAWLINE
PRINTBUTTON @"オートモード<%\@FLAG:オートモード > 0?ＯＮ#ＯＦＦ\@%>", 1000
IF FLAG:オートモード > 0
	PRINT 　
	PRINTBUTTON @"経過ターン数<{FLAG:オートモード}>", 1001
ENDIF
PRINTL 
DRAWLINE
PRINTFORML <%セット名:(FLAG:選択セット)%>
FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL == ""
		BREAK
	SIF LOCAL == SELECT
		SETCOLOR 0xC07070
	PRINTBUTTON @"%"["+LOCALS:LOCAL+"]",24,LEFT% > ", LOCAL
	SIF LOCAL % 4 == 3
		PRINTL 
	RESETCOLOR
NEXT
PRINTL 
DRAWLINE
PRINTL <セット>
FOR LOCAL, 0, 5
	SIF LOCAL == FLAG:選択セット
		SETCOLOR 0xC07070
	PRINTBUTTON @"%"["+セット名:LOCAL+"]",25,LEFT%", 2000 + LOCAL
	RESETCOLOR
	SIF LOCAL % 3 == 2
		PRINTL 
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTL <セット操作>
PRINTBUTTON "[名称変更]　", 2010
PRINT 　
PRINTBUTTON "[登録]　　", 2011
PRINT 　
PRINTBUTTON "[リセット]", 2012
PRINTL 
DRAWLINE
PRINTL <デフォルトセット>
PRINTBUTTON "[番号順]　　", 2020
PRINT 　
PRINTBUTTON "[従順]　　", 2021
PRINT 　
PRINTBUTTON "[反抗的]　", 2022
PRINTL 
DRAWLINE
PRINTL <表示設定>
SIF FLAG:オートモード表示設定 == 0
	SETCOLOR 0xC07070
PRINTBUTTON "[通常]　　", 2030
RESETCOLOR
PRINT 　
SIF FLAG:オートモード表示設定 == 1
	SETCOLOR 0xC07070
PRINTBUTTON "[簡略]　　", 2031
PRINT 　
RESETCOLOR
SIF FLAG:オートモード表示設定 == 2
	SETCOLOR 0xC07070
;PRINTBUTTON "[デバッグ非表示]", 2032
RESETCOLOR
PRINTL 
DRAWLINE
PRINTL <優先大満足ボーナス>
PRINTBUTTON "["+AUTO_SPLIT("いつも通り/アナルをいじめて/お仕置きして/激しくして/甘えさせて/壊れるまでして/外に出たい/孕ませたい/休ませて/ランダム", "/", FLAG:優先大満足ボーナス % 100)+"]", 3000
PRINTL 
PRINTBUTTON "["+AUTO_SPLIT("いつも通り/アナルをいじめて/お仕置きして/激しくして/甘えさせて/壊れるまでして/外に出たい/孕ませたい/休ませて/ランダム", "/", FLAG:優先大満足ボーナス / 100)+"]", 3001
PRINTL (予備)
DRAWLINE
PRINTBUTTON "[戻る]", 999
INPUT
SELECTCASE RESULT
	CASE 2000 TO 2004
		IF FLAG:選択セット != RESULT - 2000
			FLAG:選択セット = RESULT - 2000
			SPLIT GLOBALS:@"オートモード登録{FLAG:選択セット}", "_", RESULTS
			SPLIT RESULTS:1, "/", LOCALS
		ENDIF
	CASE 2010
		PRINTL 名称を入力してください
		INPUTS
		SIF RESULTS == ""
			RESULTS = 登録{FLAG:選択セット}
		セット名:(FLAG:選択セット) = %RESULTS%
	CASE 2011, 999
		PRINTW 現在のセットを登録します
		GLOBALS:@"オートモード登録{FLAG:選択セット}" = %セット名:(FLAG:選択セット)%_
		FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
			SIF LOCALS:LOCAL == ""
				BREAK
			GLOBALS:@"オートモード登録{FLAG:選択セット}" += LOCALS:LOCAL
			GLOBALS:@"オートモード登録{FLAG:選択セット}" += "/"
		NEXT
		;末尾の区切り記号を削る
		GLOBALS:@"オートモード登録{FLAG:選択セット}" = %SUBSTRING(GLOBALS:@"オートモード登録{FLAG:選択セット}", 0, STRLENS(GLOBALS:@"オートモード登録{FLAG:選択セット}") - 1)%
		SAVEGLOBAL
		IF RESULT == 999
			CALL AUTO_MODE, 1
			REDRAW 1
			RETURN 0
		ENDIF
	CASE 2012
		SPLIT GLOBALS:@"オートモード登録{FLAG:選択セット}", "_", RESULTS
		SPLIT RESULTS:1, "/", LOCALS
	CASE 2020
		VARSET LOCALS
	CASE 2021
		VARSET LOCALS
		LOCALS += "愛情を込めてやる/腰を振る/イかせて！/中に出して！/積極的やる/快感を受け入れる/気持ち良くして/愛想よく応答する/もっとハードにして/"
		LOCALS += "消極的やる/許しを乞う/恥ずかしがる/気弱く応答する/沈黙する/無愛想に応答する/強気に応答する/皮肉を言う/快感を我慢する/"
		LOCALS += "悲鳴を上げる/痛みを我慢する/自慰し始める/嫌がる/下手だと罵る/不敵に笑う/怯える/"
		LOCALS += "中に出さないで！/拒否する/暴れる/道具を外す"
		SPLIT LOCALS, "/", LOCALS
	CASE 2022
		VARSET LOCALS
		LOCALS += "暴れる/道具を外す/拒否する/中に出さないで！/"
		LOCALS += "下手だと罵る/嫌がる/不敵に笑う/悲鳴を上げる/痛みを我慢する/自慰し始める/怯える/"
		LOCALS += "消極的やる/許しを乞う/恥ずかしがる/気弱く応答する/沈黙する/無愛想に応答する/強気に応答する/皮肉を言う/快感を我慢する/"
		LOCALS += "中に出して！/積極的やる/快感を受け入れる/気持ち良くして/愛想よく応答する/もっとハードにして/愛情を込めてやる/腰を振る/イかせて！"
		SPLIT LOCALS, "/", LOCALS
	CASE 2030, 2031, 2032
		FLAG:オートモード表示設定 = RESULT - 2030
	CASE 3000
		IF FLAG:優先大満足ボーナス % 100 == 9
			FLAG:優先大満足ボーナス -= 9
		ELSE
			FLAG:優先大満足ボーナス++
		ENDIF
	CASE 3001
		IF FLAG:優先大満足ボーナス / 100 == 9
			FLAG:優先大満足ボーナス -= 900
		ELSEIF FLAG:優先大満足ボーナス / 100 == 5
			FLAG:優先大満足ボーナス += 300
		ELSE
			FLAG:優先大満足ボーナス += 100
		ENDIF
		
	CASE 1001
		IF FLAG:オートモード > 0
			CALL INPUT_MANY, 1, 100
			FLAG:オートモード = RESULT
		ENDIF
	CASE 1000
		IF !FLAG:オートモード
			FLAG:オートモード = 1
		ELSE
			FLAG:オートモード *= -1
		ENDIF
	CASEELSE
		IF SELECT == -1
			SELECT = RESULT
		ELSE
			SWAP LOCALS:SELECT, LOCALS:RESULT
			SELECT = -1
		ENDIF
ENDSELECT
GOTO START

@CHECK_TALENT_CRI
#LOCALSIZE 10
#LOCALSSIZE 200
#DIM LINE
#DIM LINENUM, 2
#DIM LCHARANUM
#DIM LMASTER
#DIM LTARGET
#DIM LTEMP
#DIMS LTEMPS

#DIM LVAR, 30
#DIM LSEX_CRI, 30
#DIM LMAIDEN_CRI, 30

#DIM LTALENT_CRI_EX, 1000
#DIM LTALENT_MENU_CRI, 1000, 20
#DIM LTALENT_CRI, 1000, 20, 30

#DIMS LSEX_OPTION
#DIM LFAVOR_OPTION
#DIM LMAIDEN_OPTION
#DIM LFUTANARI_OPTION

LCHARANUM = CHARANUM
LMASTER = MASTER
LTARGET = TARGET
LINENUM = 15, 35

LSEX_OPTION = 性別無視
LMAIDEN_OPTION = 1

VARSET LTALENT_MENU_CRI:0:0
VARSET LTALENT_CRI:0:0:0
FOR LOCAL, 0, VARSIZE("TALENT")
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	ADDVOIDCHARA
	TARGET = CHARANUM - 1
	TALENT:LOCAL = 1
	;変数を初期化
	CALL SET_CRI_VAR
	FOR LOCAL:1, 0, 11
		;休憩をはじく
		;使役は処理しない
		SIF LOCAL:1 == 8 || LOCAL:1 == 9
			CONTINUE
		CALL SET_CRI_VAR, MENU(LOCAL:1), 1
	NEXT
	CALL ACT_MENU_TALENT_T
	VARSET LVAR
	VARSET RESULT
	CALLF VARIABLE_OF_CRI, "GET", "固定累計"
	FOR LOCAL:1, 0, 20
		LTALENT_MENU_CRI:LOCAL:(LOCAL:1) += RESULT:(LOCAL:1) * 2
	NEXT
	VARSET RESULT
	CALLF VARIABLE_OF_CRI, "GET", "変動累計"
	FOR LOCAL:1, 0, 20
		LTALENT_MENU_CRI:LOCAL:(LOCAL:1) += RESULT:(LOCAL:1)
		SIF LTALENT_MENU_CRI:LOCAL:(LOCAL:1)
			LTALENT_CRI_EX:LOCAL = 1
	NEXT
	FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
		CALL RES_CRI_VAR, 1
		CALL ACT_M_INIT(LOCAL:1)
		TRYCCALLFORM ACT_ACTM{LOCAL:1}_TALENT_T
		CATCH
			CONTINUE
		ENDCATCH
		VARSET LVAR
		VARSET RESULT
		CALLF VARIABLE_OF_CRI, "GET", "固定累計"
		FOR LOCAL:2, 0, 30
			LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2) += RESULT:(LOCAL:2) * 2
		NEXT
		CALLF VARIABLE_OF_CRI, "GET", "変動累計"
		FOR LOCAL:2, 0, 30
			LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2) += RESULT:(LOCAL:2)
			SIF LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2)
				LTALENT_CRI_EX:LOCAL = 1
		NEXT
	NEXT
	DELCHARA TARGET
NEXT
TARGET= LTARGET
$START
DRAWLINE
LOCALS = 素質解析/キャラクター指定/終了
LOCAL = 0
WHILE AUTO_SPLIT(LOCALS, "/", LOCAL) != ""
	PRINTBUTTON "[" + AUTO_SPLIT(LOCALS, "/", LOCAL) +"]", AUTO_SPLIT(LOCALS, "/", LOCAL) 
	PRINTL 
	LOCAL++
WEND
CALL INPUT_SPLIT, LOCALS
SELECTCASE RESULTS
	CASE "素質解析"
		$START_TALENT
		LOCALS:1 = 
		LOCAL:1 = 0
		FOR LOCAL, 0, VARSIZE("TALENT") + 2
			IF LOCAL == VARSIZE("TALENT") + 1
				LOCALS:1 += "END"
				PRINTBUTTON @"%"[戻る]", 16, LEFT%", "END"
			ELSEIF LOCAL == VARSIZE("TALENT")
				LOCALS:1 += "ALL/"
				PRINTBUTTON @"%"[全素質]", 16, LEFT%", "ALL"
			ELSE
				SIF !LTALENT_CRI_EX:LOCAL
					CONTINUE
				LOCALS:1 += TALENTNAME:LOCAL + "/"
				PRINTBUTTON @"%"[" + TALENTNAME:LOCAL +"]", 16, LEFT%", TALENTNAME:LOCAL
			ENDIF
			SIF LOCAL:1 % 6 == 0
				PRINTL 
			LOCAL:1++
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		CALL INPUT_SPLIT, LOCALS:1
		SIF RESULTS == "END"
			GOTO START
		FOR LOCAL, 0, VARSIZE("TALENT")
			IF RESULTS == "ALL"
				SIF !LTALENT_CRI_EX:LOCAL
					CONTINUE
			ELSE
				SIF TALENTNAME:LOCAL != RESULTS
					CONTINUE
			ENDIF
			DRAWLINE
			PRINTFORML %TALENTNAME:LOCAL%
			FOR LOCAL:1, 0, 20
				LTEMP = LTALENT_MENU_CRI:LOCAL:(LOCAL:1)
				SIF !LTEMP
					CONTINUE
				PRINTFORM %MENU(LOCAL:1), 24, LEFT%
				IF LTEMP > 0
					SETCOLOR 0x7070C0
				ELSE
					SETCOLOR 0xC07070
				ENDIF
				PRINTFORML {LTEMP,4}
				RESETCOLOR
			NEXT
			FOR LOCAL:1, 0, 20
				FOR LOCAL:2, 0, 30
					LTEMP = LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2)
					SIF !LTEMP
						CONTINUE
					PRINTFORM %GET_ACTABBNAME(ACTM_NUM(LOCAL:1, LOCAL:2)) + "(" + MENU(LOCAL:1) + ")", 24, LEFT%
					IF LTEMP > 0
						SETCOLOR 0x7070C0
					ELSE
						SETCOLOR 0xC07070
					ENDIF
					PRINTFORML {LTEMP,4}
					RESETCOLOR
				NEXT
			NEXT
		NEXT
		WAIT
		GOTO START_TALENT
	CASE "キャラクター指定"
		$START_CHARA
		LOCALS:1 = END/調教者をふたなりにする/性別無視/調教対象が男/調教対象が女/調教対象がふたなり/初体験無視/調教対象が処女/好意少/好意中/好意多
		DRAWLINE
		FOR LOCAL, 1, FLAG:Cha総数 + 1
			LOCALS:1 += "/" + CSVCALLNAME(LOCAL, 0)
			PRINTBUTTON @"%"[" + CSVCALLNAME(LOCAL, 0) +"]", 24, LEFT%", CSVCALLNAME(LOCAL, 0)
			SIF LOCAL % 4 == 0
				PRINTL 
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		DRAWLINE
		FOR LOCAL, 0 ,4
			LTEMPS = %AUTO_SPLIT("性別無視/調教対象が男/調教対象が女/調教対象がふたなり", "/", LOCAL)%
			SIF LTEMPS == LSEX_OPTION
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		SIF LFUTANARI_OPTION
			SETCOLOR 0x70C070
		PRINTBUTTON @"%"[調教者をふたなりにする]", 24, LEFT%", "調教者をふたなりにする"
		RESETCOLOR
		PRINTL 
		DRAWLINE
		FOR LOCAL, 0 ,2
			LTEMPS = %AUTO_SPLIT("初体験無視/調教対象が処女", "/", LOCAL)%
			SIF GETBIT(LMAIDEN_OPTION, LOCAL)
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		PRINTL 
		FOR LOCAL, 0 ,3
			LTEMPS = %AUTO_SPLIT("好意少/好意中/好意多", "/", LOCAL)%
			SIF LFAVOR_OPTION == LOCAL
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		PRINTL 
		DRAWLINE
		PRINTBUTTON @"%"[戻る]", 24, LEFT%", "END"
		PRINTL 
		CALL INPUT_SPLIT, LOCALS:1
		SIF RESULTS == "END"
			GOTO START
		IF GROUPMATCH(RESULTS, "性別無視", "調教対象が男", "調教対象が女", "調教対象がふたなり")
			LSEX_OPTION = %RESULTS%
			GOTO START_CHARA
		ENDIF
		IF GROUPMATCH(RESULTS, "調教者をふたなりにする", "初体験無視", "調教対象が処女", "好意少", "好意中", "好意多")
			SIF RESULTS == "調教者をふたなりにする"
				INVERTBIT LFUTANARI_OPTION, 0
			SIF RESULTS == "初体験無視"
				INVERTBIT LMAIDEN_OPTION, 0
			SIF RESULTS == "調教対象が処女"
				INVERTBIT LMAIDEN_OPTION, 1
			SIF RESULTS == "好意少"
				LFAVOR_OPTION = 0
			SIF RESULTS == "好意中"
				LFAVOR_OPTION = 1
			SIF RESULTS == "好意多"
				LFAVOR_OPTION = 2
			GOTO START_CHARA
		ENDIF
		FOR LOCAL, 0, FLAG:Cha総数 + 1
			SIF RESULTS != CSVCALLNAME(LOCAL, 0)
				CONTINUE
			ADDCHARA LOCAL
			BREAK
		NEXT
		TARGET = CHARANUM - 1
		ADDVOIDCHARA
		MASTER = CHARANUM - 1
		SIF LSEX_OPTION == "調教対象が男"
			TALENT:MASTER:オトコ = 1
		SIF LSEX_OPTION == "調教対象がふたなり"
			TALENT:MASTER:ふたなり = 1
		SIF LFUTANARI_OPTION
			TALENT:ふたなり = 1
		SIF GETBIT(LMAIDEN_OPTION, 1)
			TALENT:MASTER:処女 = 1
		CFLAG:好意 = LFAVOR_OPTION * 1000
		DRAWLINE
		PRINTFORML 　%CALLNAME:TARGET%
		;変数を初期化
		CALL SET_CRI_VAR
		FOR LOCAL:1, 0, 11
			;休憩をはじく
			;使役は処理しない
			SIF LOCAL:1 == 8 || LOCAL:1 == 9
				CONTINUE
			CALL SET_CRI_VAR, MENU(LOCAL:1), 1
		NEXT
		LINE = LINECOUNT
		DRAWLINE
		PRINTFORML 　メニュー選択
		DRAWLINE
		VARSET LVAR
		VARSET RESULT
		VARSET LSEX_CRI
		VARSET LMAIDEN_CRI
		IF LSEX_OPTION != "性別無視"
			CALL CHA_CRI_VAR, "性別"
			CALL ACT_MENU_SEX
			CALLF VARIABLE_OF_CRI, "GET", "固定累計"
			FOR LOCAL:1, 0, 30
				LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "変動累計"
			FOR LOCAL:1, 0, 30
				LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
		ENDIF
		IF !GETBIT(LMAIDEN_OPTION, 0)
			VARSET RESULT
			CALL CHA_CRI_VAR, "調教者が処女"
			CALL ACT_MENU_MAIDEN_T
			CALLF VARIABLE_OF_CRI, "GET", "固定累計"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "変動累計"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
			VARSET RESULT
			CALL CHA_CRI_VAR, "調教対象が処女"
			CALL ACT_MENU_MAIDEN_M
			CALLF VARIABLE_OF_CRI, "GET", "固定累計"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "変動累計"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
		ENDIF
		CALL CHA_CRI_VAR, "素質"
		VARSET RESULT
		CALL ACT_MENU_TALENT_T
		CALLF VARIABLE_OF_CRI, "GET", "固定累計"
		FOR LOCAL:1, 0, 30
			LVAR:(LOCAL:1) += RESULT:(LOCAL:1) * 2
		NEXT
		CALLF VARIABLE_OF_CRI, "GET", "変動累計"
		FOR LOCAL:1, 0, 30
			LVAR:(LOCAL:1) += RESULT:(LOCAL:1)
		NEXT
		FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
			SETCOLOR 0xC07070
			PRINTFORM %UNICODE(0x25AE) * ABS(MIN(LVAR:(LOCAL:1), 0)),50,RIGHT%
			RESETCOLOR
			PRINTFORM  %MENU(LOCAL:1)%({LVAR:(LOCAL:1), 4}) 
			SETCOLOR 0x7070C0
			PRINTFORM %UNICODE(0x25AE) * MAX(LVAR:(LOCAL:1), 0),50,LEFT%
			PRINTFORML 
		NEXT
		WHILE LINECOUNT < LINE + LINENUM
			PRINTFORML 
		WEND
		RESETCOLOR
		LOCAL:3 = 0
		FOR LOCAL:1, 0, VARSIZE("TALENT")
			SIF !TALENT:(LOCAL:1)
				CONTINUE
			LTEMPS = 
			FOR LOCAL:2, 0, STRCOUNT(MENULIST(), "/") + 1
				IF LTALENT_MENU_CRI:(LOCAL:1):(LOCAL:2)
					SIF LTEMPS != ""
						LTEMPS += "/"
					LTEMPS += MENU(LOCAL:2) + "_" + TOSTR(LTALENT_MENU_CRI:(LOCAL:1):(LOCAL:2))
				ENDIF
			NEXT
			IF LTEMPS != ""
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %TALENTNAME:(LOCAL:1), 16, LEFT%
				SPLIT LTEMPS, "/", LOCALS
				FOR LOCAL:2, 0, RESULT
					LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
		NEXT
		IF MATCH(LMAIDEN_CRI, 0) != VARSIZE("LMAIDEN_CRI")
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %"初体験補正", 16, LEFT%
			FOR LOCAL:2, 0, 30
				SIF !LMAIDEN_CRI:(LOCAL:2)
					CONTINUE
				LTEMP = LMAIDEN_CRI:(LOCAL:2)
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%MENU(LOCAL:2)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		ENDIF
		IF MATCH(LSEX_CRI, 0) != VARSIZE("LSEX_CRI")
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %"性別補正", 16, LEFT%
			FOR LOCAL:2, 0, 30
				SIF !LSEX_CRI:(LOCAL:2)
					CONTINUE
				LTEMP = LSEX_CRI:(LOCAL:2)
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%MENU(LOCAL:2)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		ENDIF
		LOCAL:3 = 0
		FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
			LTEMPS = 
			FOR LOCAL:2, 0, VARSIZE("TALENT")
				SIF !TALENT:(LOCAL:2)
					CONTINUE
				LTEMP = LTALENT_MENU_CRI:(LOCAL:2):(LOCAL:1)
				SIF !LTEMP
					CONTINUE
				LTEMPS += "/" + TALENTNAME:(LOCAL:2) + "_" + TOSTR(LTEMP)
			NEXT
			SIF LMAIDEN_CRI:(LOCAL:1)
				LTEMPS += "/" + "初体験補正" + "_" + TOSTR(LMAIDEN_CRI:(LOCAL:1))
			SIF LSEX_CRI:(LOCAL:1)
				LTEMPS += "/" + "性別補正" + "_" + TOSTR(LSEX_CRI:(LOCAL:1))
			SIF LTEMPS == ""
				CONTINUE
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %MENU(LOCAL:1), 16, LEFT%
			SPLIT LTEMPS, "/", LOCALS
			FOR LOCAL:2, 1, RESULT
				LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		NEXT
		WHILE LINECOUNT < LINE + LINENUM:1
			PRINTFORML 
		WEND
		WAIT
		FOR LOCAL, 0, STRCOUNT(MENULIST(), "/") + 1
			CALL RES_CRI_VAR, 1
			CALL ACT_M_INIT(LOCAL)
			VARSET LVAR
			VARSET LSEX_CRI
			VARSET LMAIDEN_CRI
			VARSET RESULT
			IF !GETBIT(LMAIDEN_OPTION, 0)
				CALL CHA_CRI_VAR, "調教対象が処女"
				TRYCCALLFORM ACT_ACTM{LOCAL}_MAIDEN_M
					CALLF VARIABLE_OF_CRI, "GET", "固定累計"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "変動累計"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
				CALL CHA_CRI_VAR, "調教者が処女"
				TRYCCALLFORM ACT_ACTM{LOCAL}_MAIDEN_T
					CALLF VARIABLE_OF_CRI, "GET", "固定累計"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "変動累計"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
			ENDIF
			IF LSEX_OPTION != "性別無視"
				CALL CHA_CRI_VAR, "性別"
				TRYCCALLFORM ACT_ACTM{LOCAL}_GENDER_T
					CALLF VARIABLE_OF_CRI, "GET", "固定累計"
					FOR LOCAL:1, 0, 30
						LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "変動累計"
					FOR LOCAL:1, 0, 30
						LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
			ENDIF
			CALL CHA_CRI_VAR, "素質"
			TRYCCALLFORM ACT_ACTM{LOCAL}_TALENT_T
			CATCH
			ENDCATCH
			CALLF VARIABLE_OF_CRI, "GET", "固定累計"
			FOR LOCAL:1, 0, 30
				LVAR:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "変動累計"
			FOR LOCAL:1, 0, 30
				LVAR:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
			LINE = LINECOUNT
			DRAWLINE
			PRINTFORML 　%MENU(LOCAL)%
			DRAWLINE
			FOR LOCAL:1, 0, ACTM_NUM(LOCAL)
				SETCOLOR 0xC07070
				PRINTFORM %UNICODE(0x25AE) * ABS(MIN(LVAR:(LOCAL:1), 0)),50,RIGHT%
				RESETCOLOR
				PRINTFORM  %GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:1))%({LVAR:(LOCAL:1), 4}) 
				SETCOLOR 0x7070C0
				PRINTFORM %UNICODE(0x25AE) * MAX(LVAR:(LOCAL:1), 0),50,LEFT%
				PRINTFORML 
			NEXT
			WHILE LINECOUNT < LINE + LINENUM
				PRINTFORML 
			WEND
			RESETCOLOR
			LOCAL:3 = 0
			FOR LOCAL:1, 0, VARSIZE("TALENT")
				SIF !TALENT:(LOCAL:1)
					CONTINUE
				LTEMPS = 
				FOR LOCAL:2, 0, ACTM_NUM(LOCAL)
					IF LTALENT_CRI:(LOCAL:1):LOCAL:(LOCAL:2)
						SIF LTEMPS != ""
							LTEMPS += "/"
						LTEMPS += GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2)) + "_" + TOSTR(LTALENT_CRI:(LOCAL:1):LOCAL:(LOCAL:2))
					ENDIF
				NEXT
				IF LTEMPS != ""
					SIF LOCAL:3++ == 0
						DRAWLINE
					PRINTFORM %TALENTNAME:(LOCAL:1), 16, LEFT%
					SPLIT LTEMPS, "/", LOCALS
					FOR LOCAL:2, 0, RESULT
						LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
						SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
						PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
						RESETCOLOR
					NEXT
					PRINTL 
				ENDIF
			NEXT
			IF MATCH(LMAIDEN_CRI, 0) != VARSIZE("LMAIDEN_CRI")
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %"初体験補正", 16, LEFT%
				FOR LOCAL:2, 0, 30
					SIF !LMAIDEN_CRI:(LOCAL:2)
						CONTINUE
					LTEMP = LMAIDEN_CRI:(LOCAL:2)
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2))% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
			IF MATCH(LSEX_CRI, 0) != VARSIZE("LSEX_CRI")
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %"性別補正", 16, LEFT%
				FOR LOCAL:2, 0, 30
					SIF !LSEX_CRI:(LOCAL:2)
						CONTINUE
					LTEMP = LSEX_CRI:(LOCAL:2)
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2))% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
			DRAWLINE
			FOR LOCAL:1, 0, ACTM_NUM(LOCAL)
				LTEMPS = 
				FOR LOCAL:2, 0, VARSIZE("TALENT")
					SIF !TALENT:(LOCAL:2)
						CONTINUE
					LTEMP = LTALENT_CRI:(LOCAL:2):LOCAL:(LOCAL:1)
					SIF !LTEMP
						CONTINUE
					LTEMPS += "/" + TALENTNAME:(LOCAL:2) + "_" + TOSTR(LTEMP)
				NEXT
				SIF LMAIDEN_CRI:(LOCAL:1)
					LTEMPS += "/" + "初体験補正" + "_" + TOSTR(LMAIDEN_CRI:(LOCAL:1))
				SIF LSEX_CRI:(LOCAL:1)
					LTEMPS += "/" + "性別補正" + "_" + TOSTR(LSEX_CRI:(LOCAL:1))
				SIF LTEMPS == ""
					CONTINUE
				PRINTFORM %GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:1)), 16, LEFT%
				SPLIT LTEMPS, "/", LOCALS
				FOR LOCAL:2, 1, RESULT
					LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			NEXT
			WHILE LINECOUNT < LINE + LINENUM:1
				PRINTFORML 
			WEND
			WAIT
		NEXT
		WHILE CHARANUM > LCHARANUM
			DELCHARA CHARANUM -  1
		WEND
		GOTO START_CHARA
	CASE "終了"
		TARGET = LTARGET
		MASTER = LMASTER
		WHILE CHARANUM > LCHARANUM
			DELCHARA CHARANUM -  1
		WEND
		RETURN 1
ENDSELECT
;────────────────────────────────────
;セーブインフォ		→SYSTEM_DATAMENU.ERBにて代替
;────────────────────────────────────
;@SAVEINFO
;PUTFORM {DAY+1}日目%GET_TIME()% \@ TARGET >= 0 ? %CALLNAME:TARGET%が%CALLNAME:MASTER%を調教中 # \@


;────────────────────────────────────
;バージョンアップ
;────────────────────────────────────
@VERSION_UP

