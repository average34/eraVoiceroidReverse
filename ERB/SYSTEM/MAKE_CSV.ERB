
@MAKE_CSV

DELALLCHARA

FOR LOCAL, 0, 82
	ADDCHARA LOCAL
NEXT

$INPUT

INPUT

TARGET = RESULT

CLEARLINE LINECOUNT

PRINTL 

PRINTL ;キャラクター番号(NO)
PRINTFORML 番号,{NO}

PRINTL 

PRINTL ;名前(NAME), 呼び名(CALLNAME)
PRINTFORML 名前,%NAME%
PRINTFORML 呼び名,%CALLNAME%

PRINTL 

PRINTL ;基礎(BASE)
FOR LOCAL, 0, VARSIZE("BASE")
	IF BASE:LOCAL != 0
		PRINTFORML 基礎,%BASENAME:LOCAL%,{BASE:LOCAL}
	ENDIF
NEXT

PRINTL 

PRINTL ;素質(TALENT)
FOR LOCAL, 0, VARSIZE("TALENT")
	IF TALENT:LOCAL == 1
		PRINTFORML 素質,%TALENTNAME:LOCAL%,
	ELSEIF TALENT:LOCAL != 0
		PRINTFORML 素質,%TALENTNAME:LOCAL%,{TALENT:LOCAL}
	ENDIF
NEXT

PRINTL 

PRINTL ;能力(ABL)
FOR LOCAL, 0, VARSIZE("ABL")
	IF ABLNAME:LOCAL != ""
		PRINTFORML 能力,%ABLNAME:LOCAL%,{ABL:LOCAL}
	ENDIF
NEXT

PRINTL 

PRINTL ;経験(EXP)
FOR LOCAL, 0, VARSIZE("EXP")
	IF EXP:LOCAL != 0
		PRINTFORML 経験,%EXPNAME:LOCAL%,{EXP:LOCAL}
	ENDIF
NEXT

PRINTL 

PRINTL ;相性(RELATION)
FOR LOCAL, 0, CHARANUM
	IF NAME:LOCAL != "" && RELATION:LOCAL != 0
		PRINTFORML %"相性," + TOSTR(LOCAL) + "," + TOSTR(RELATION:LOCAL) + ",", 15, LEFT%;%NAME:LOCAL%
	ENDIF
NEXT

PRINTL 

PRINTL ;装着物(EQUIP)
PRINTFORML %"装着物," + EQUIPNAME:0 + "," + TOSTR(EQUIP:0) + ",", 25, LEFT%;1,手袋、2,袖、3,羽衣
PRINTFORML %"装着物," + EQUIPNAME:1 + "," + TOSTR(EQUIP:1) + ",", 25, LEFT%;1,ソックス、2,ニーソックス、3,ガーターストッキング、4,足袋
PRINTFORML %"装着物," + EQUIPNAME:2 + "," + TOSTR(EQUIP:2) + ",", 25, LEFT%;1,ドロワーズ、2,パンティ、3,トランクス、4,パンティストッキング、5,褌
PRINTFORML %"装着物," + EQUIPNAME:3 + "," + TOSTR(EQUIP:3) + ",", 25, LEFT%;1,ブラジャー、2,キャミソール、3,さらし、4,Ｔシャツ
PRINTFORML %"装着物," + EQUIPNAME:4 + "," + TOSTR(EQUIP:4) + ",", 25, LEFT%;1,スカート、2,キュロット、3,ズボン、4,袴
PRINTFORML %"装着物," + EQUIPNAME:5 + "," + TOSTR(EQUIP:5) + ",", 25, LEFT%;1,上着
PRINTFORML %"装着物," + EQUIPNAME:6 + "," + TOSTR(EQUIP:6) + ",", 25, LEFT%;1,和服、2,導師服、3,ワンピース

PRINTL 

PRINTL ;キャラ情報(CSTR)
FOR LOCAL, 10, 15
	IF CSTRNAME:LOCAL != ""
		PRINTFORML CSTR,%CSTRNAME:LOCAL%,%CSTR:LOCAL%
	ENDIF
NEXT

PRINTL 

PRINTL ;呼び方リスト(CSTR)
FOR LOCAL, 20, 22
	IF CSTRNAME:LOCAL != ""
		PRINTFORML CSTR,%CSTRNAME:LOCAL%,%CSTR:LOCAL%
	ENDIF
NEXT

PRINTL 

OUTPUTLOG

GOTO INPUT

