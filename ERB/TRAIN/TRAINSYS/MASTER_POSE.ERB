;MASTERの体勢
@MASTER_POSE
;姿勢関連のTCVARを初期化する
;eratohoぱ。等の実装と違って、前の姿勢を考慮はしない路線
;　　だったのですが、メッセージ表示の上でのみ考慮することに変更
;現在の位置情報を前回の位置へ転送
TCVAR:MASTER:前回の姿勢 = TCVAR:MASTER:姿勢
TCVAR:MASTER:前回の位置前後 = TCVAR:MASTER:位置前後
TCVAR:MASTER:前回の位置上下 = TCVAR:MASTER:位置上下
;一度初期化する
TCVAR:MASTER:姿勢 = 0
TCVAR:MASTER:位置前後 = 0
TCVAR:MASTER:位置上下 = 0
TCVAR:MASTER:REACT姿勢変化 = 0

;強制脱衣が発生していれば姿勢を補正
IF TFLAG:強制脱衣
	;前回の姿勢によって補正
	SELECTCASE TCVAR:MASTER:前回の姿勢
		;四つん這いは自由に戻す
		CASE 3
			TCVAR:MASTER:前回の姿勢 = 0
		;膝立ちは座らせる
		CASE 10
			TCVAR:MASTER:前回の姿勢 = 2
	ENDSELECT
		;他は継続する
ENDIF

;自主脱衣が発生していれば位置をリセット
IF TFLAG:自主脱衣
	TCVAR:MASTER:前回の位置前後 = 0
	TCVAR:MASTER:前回の位置上下 = 0
ENDIF

VARSET TCVAR:MASTER, 0, 1, 5
;体力限界なら仰向け
SIF BASE:MASTER:体力 <= 0
	TFLAG:仰向け = 2
;自慰中
IF GETBIT(TEQUIP:自慰中, 0)
	TFLAG:Ｐ使用中++
	TFLAG:手使用中++
ENDIF
;顔面騎乗中
IF TEQUIP:顔面騎乗 || TEQUIP:顔面騎乗アナル
	TFLAG:口使用中 += 2
	;仰向け
	TFLAG:仰向け = 2
ENDIF

;Ｃ愛撫
SIF TEQUIP:調教対象Ｃ使用
	TFLAG:Ｐ使用中++

;ACTごとのPOSE情報を呼ぶ
;※要修正
;ACTごとにACTABLE、MESSAGE、POSEを収めたファイルにするとかいいと思う。まずは関数化から始めます
TRYCALLFORM MASTER_POSE_ACT{TFLAG:ACT}

;とは言うけど関数化しないほうが設定しやすいんですよねここ…
;以下姿勢の設定　ぱ。追加部分

;顔面騎乗、押し倒しによる強制仰向け
;	//MASTER_POSEはACTION_APPLYより先に処理される
;	//そして性交、性奉時のTEQUIP:顔面騎乗等のリセットはACTION_APPLY内で行われる(即ち、この箇所が処理される時点ではリセットされていない)
;	//今回のメニューが性交、性奉なら弾くようにしないと色々と不味いことになる(ACT32:騎乗位時に調教者が上になったりする)
;	//姿勢制御関数刷新が控えてるので、とりあえず最小限の手当だけ(2015/02/01)
IF !MENUMATCH(TFLAG:ACT, "性交") && !MENUMATCH(TFLAG:ACT, "性奉") && (TEQUIP:顔面騎乗 || TEQUIP:顔面騎乗アナル || TEQUIP:押し倒し中 || IS_NOWACTNAME("押し倒す"))
	TCVAR:MASTER:姿勢 = 1
	;TCVAR:MASTER:位置前後 = TEQUIP:押し倒し中 || IS_NOWACTNAME("押し倒す") ? 1 # 0
	TCVAR:MASTER:位置前後 = 1
	TCVAR:MASTER:位置上下 = 1
;性交・性奉系ACTは先に処理
ELSEIF MENUMATCH(TFLAG:ACT, "性交") || MENUMATCH(TFLAG:ACT, "性奉")
	;ACT毎の体位による設定
	SELECTCASE GET_ACTNAME(TFLAG:ACT)
		CASE "正常位"
			TCVAR:MASTER:姿勢 = 1
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 1
		CASE "後背位", "アナルセックス"
			TCVAR:MASTER:姿勢 = 3
			TCVAR:MASTER:位置前後 = 2
			TCVAR:MASTER:位置上下 = 0
		CASE "騎乗位"
			TCVAR:MASTER:姿勢 = 10
			TCVAR:MASTER:位置前後 = 0
			TCVAR:MASTER:位置上下 = 2
		CASE "対面座位"
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 0
		CASE "背面座位"
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 2
			TCVAR:MASTER:位置上下 = 0
		CASE "正常位させる"
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 2
		CASE "後背位させる", "アナルセックスさせる"
			TCVAR:MASTER:姿勢 = 10
			TCVAR:MASTER:位置前後 = 3
			TCVAR:MASTER:位置上下 = 0
		CASE "逆レイプ"
			TCVAR:MASTER:姿勢 = 1
			TCVAR:MASTER:位置前後 = 0
			TCVAR:MASTER:位置上下 = 1
		CASE "対面座位させる"
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 0
		CASE "背面座位させる"
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 3
			TCVAR:MASTER:位置上下 = 0
	ENDSELECT
;性交中
ELSEIF TEQUIP:性交中
	;TEQUIP毎の体位による設定
	SELECTCASE TEQUIP:性交中
		;正常位
		CASE 1
			TCVAR:MASTER:姿勢 = 1
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 1
		;後背位、アナルセックス
		CASE 2, 6
			TCVAR:MASTER:姿勢 = 3
			TCVAR:MASTER:位置前後 = 2
			TCVAR:MASTER:位置上下 = 0
		;騎乗位
		CASE 3
			TCVAR:MASTER:姿勢 = 10
			TCVAR:MASTER:位置前後 = 0
			TCVAR:MASTER:位置上下 = 2
		;対面座位
		CASE 4
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 0
		;背面座位
		CASE 5
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 2
			TCVAR:MASTER:位置上下 = 0
	ENDSELECT
;性交奉仕中
ELSEIF TEQUIP:性交奉仕中
	;TEQUIP毎の体位による設定
	SELECTCASE TEQUIP:性交奉仕中
		;正常位
		CASE 1
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 2
		;後背位、アナルセックス
		CASE 2, 6
			TCVAR:MASTER:姿勢 = 10
			TCVAR:MASTER:位置前後 = 3
			TCVAR:MASTER:位置上下 = 0
		;騎乗位（逆レイプ）
		CASE 3
			TCVAR:MASTER:姿勢 = 1
			TCVAR:MASTER:位置前後 = 0
			TCVAR:MASTER:位置上下 = 1
		;対面座位
		CASE 4
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 1
			TCVAR:MASTER:位置上下 = 0
		;背面座位
		CASE 5
			TCVAR:MASTER:姿勢 = 2
			TCVAR:MASTER:位置前後 = 3
			TCVAR:MASTER:位置上下 = 0
	ENDSELECT
;ACTごとの姿勢情報を代入
;ただしCOM次第では変更になる（暴れるなど）
ELSE
	;ACT分類による設定
	SELECTCASE MENU(GET_ACTM(TFLAG:ACT))
		CASE "愛撫", "道具"
			;どこを愛撫しているか
			SELECTCASE CARESS_SPOT(TARGET)
				;Ａ
				CASE 3
					;口でしている
					IF MA_PLAYER(1) == 1
						;すでに仰向けの場合（難易度高い気がするが）
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 1
						;そうでなければ四つん這い
						ELSE
							TCVAR:MASTER:姿勢 = 3
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 2
						ENDIF
					;手など
					ELSE
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 1
						ELSE
							;前回の姿勢によって補正
							;口と違い、前後・上下の指定はありません
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;起立・膝立ち
								CASE 4, 10
									;3/4で継続
									IF RAND:4
										TCVAR:MASTER:姿勢 = TCVAR:MASTER:前回の姿勢
									;そうでなければ四つん這い
									ELSE
										TCVAR:MASTER:姿勢 = 3
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 2
									ENDIF
								;座り
								CASE 2
									;3/4で膝立ちに変化する
									IF RAND:4
										TCVAR:MASTER:姿勢 = 10
									;そうでなければ四つん這い
									ELSE
										TCVAR:MASTER:姿勢 = 3
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 2
									ENDIF
								;それ以外は四つん這い
								CASEELSE
									TCVAR:MASTER:姿勢 = 3
									TCVAR:MASTER:位置上下 = 0
									TCVAR:MASTER:位置前後 = 2
							ENDSELECT
						ENDIF
					ENDIF
				;Ｃ、Ｖ
				CASE 1, 2
					;性器同士でしている
					IF GROUPMATCH(MC_PLAYER(1), 3, 8) || GROUPMATCH(MV_PLAYER(1), 3, 8)
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 1
							TCVAR:MASTER:位置前後 = 1
						;素股の場合、仰向けでなければ後背位準拠
						ELSEIF PENIS(MASTER) && MC_PLAYER(1) == 8
							TCVAR:MASTER:姿勢 = 10
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 3
						ELSE
							;前回の姿勢によって補正
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向けは継続
								CASE 1
									TCVAR:MASTER:姿勢 = 1
									TCVAR:MASTER:位置上下 = 1
									TCVAR:MASTER:位置前後 = 1
								;それ以外
								CASEELSE
									;Ｃでイった直後は1/2で仰向けに変化する
									IF NOWEX:MASTER:Ｃ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 1
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ座り
									ELSE
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
							ENDSELECT
						ENDIF
					;胸でしている
					ELSEIF MC_PLAYER(1) == 6 || MV_PLAYER(1) == 6
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 1
							TCVAR:MASTER:位置前後 = 1
						ELSE
							;前回の姿勢によって補正
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向けは継続
								CASE 1
									TCVAR:MASTER:姿勢 = 1
									TCVAR:MASTER:位置上下 = 1
									TCVAR:MASTER:位置前後 = 1
								;それ以外
								CASEELSE
									;Ｃでイった直後は1/2で仰向けに変化する
									IF NOWEX:MASTER:Ｃ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 1
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ膝立ち
									ELSE
										TCVAR:MASTER:姿勢 = 10
										TCVAR:MASTER:位置上下 = 2
										TCVAR:MASTER:位置前後 = 1
									ENDIF
							ENDSELECT
						ENDIF
					;足でしている
					ELSEIF MC_PLAYER(1) == 7 || MV_PLAYER(1) == 7
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 1
						ELSE
							;前回の姿勢によって補正
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向けは継続
								CASE 1
									TCVAR:MASTER:姿勢 = 1
									TCVAR:MASTER:位置上下 = 0
									TCVAR:MASTER:位置前後 = 1
								;それ以外
								CASEELSE
									;Ｃでイった直後は1/2で仰向けに変化する
									IF NOWEX:MASTER:Ｃ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ座り
									ELSE
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
							ENDSELECT
						ENDIF
					;口でしている
					ELSEIF MC_PLAYER(1) == 1 || MV_PLAYER(1) == 1
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							;シックスナインは顔面騎乗に流れているはず
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 1
						ELSE
							;前回の姿勢によって補正
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向け
								CASE 1
									;イきそうなら3/4で座りに変化する
									IF TCVAR:MASTER:イきそう && RAND:4
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;1/4で座りに変化する
									ELSEIF !RAND:4
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;座り、自由
								CASE 2, 0
									;イきそうなら3/4で膝立ちに変化する
									IF TCVAR:MASTER:イきそう && RAND:4
										TCVAR:MASTER:姿勢 = 10
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;Ｃでイった直後は1/2で仰向けに変化する
									ELSEIF NOWEX:MASTER:Ｃ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ座り
									ELSE
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;四つん這い
								CASE 3
									;3/4で座りに変化する
									IF RAND:4
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ仰向け
									ELSE
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;起立
								CASE 4
									;Ｃでイった直後は膝立ちに変化する
									IF NOWEX:MASTER:Ｃ絶頂
										TCVAR:MASTER:姿勢 = 10
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 4
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;膝立ち
								CASE 10
									;Ｃでイった直後は座りに変化する
									IF NOWEX:MASTER:Ｃ絶頂
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 10
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
							ENDSELECT
						ENDIF
					;手・道具類など
					ELSE
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 0
							TCVAR:MASTER:位置前後 = 1
						ELSE
							;前回の姿勢によって補正
							;口と違い、前後・上下の指定はありません
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向け
								CASE 1
									;イきそうなら3/4で座りに変化する
									IF TCVAR:MASTER:イきそう && RAND:4
										TCVAR:MASTER:姿勢 = 2
									;1/4で座りに変化する
									ELSEIF !RAND:4
										TCVAR:MASTER:姿勢 = 2
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;座り、自由
								CASE 2, 0
									;イきそうなら3/4で膝立ちに変化する
									IF TCVAR:MASTER:イきそう && RAND:4
										TCVAR:MASTER:姿勢 = 10
									;Ｃでイった直後は1/2で仰向けに変化する
									ELSEIF NOWEX:MASTER:Ｃ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ座り
									ELSE
										TCVAR:MASTER:姿勢 = 2
									ENDIF
								;四つん這い
								CASE 3
									;位置が前でない場合、2/3で後ろから継続
									IF TCVAR:MASTER:位置前後 != 1 && RAND:3
										TCVAR:MASTER:姿勢 = 3
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 2
									;そうでなければ座りに変化する
									ELSE
										TCVAR:MASTER:姿勢 = 2
									ENDIF
								;起立
								CASE 4
									;Ｃでイった直後は膝立ちに変化する
									IF NOWEX:MASTER:Ｃ絶頂
										TCVAR:MASTER:姿勢 = 10
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 4
									ENDIF
								;膝立ち
								CASE 10
									;Ｃでイった直後は座りに変化する
									IF NOWEX:MASTER:Ｃ絶頂
										TCVAR:MASTER:姿勢 = 2
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 10
									ENDIF
							ENDSELECT
						ENDIF
					ENDIF
				;Ｂ
				CASE 4
					;口でしている、or 乳の揉み合い
					IF MB_PLAYER(1) == 1 || TB_PLAYER(0) == 1
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 1
							TCVAR:MASTER:位置前後 = 0
						ELSE
							;前回の姿勢によって補正
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向け
								CASE 1
									;1/4で座りに変化する
									IF !RAND:4
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 1
										TCVAR:MASTER:位置前後 = 0
									ENDIF
								;座り、自由
								CASE 2, 0
									;Ｂでイった直後は1/2で仰向けに変化する
									IF NOWEX:MASTER:Ｂ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 1
										TCVAR:MASTER:位置前後 = 0
									;そうでなければ座り
									ELSE
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;四つん這い
								CASE 3
									;3/4で座りに変化する
									IF RAND:4
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ仰向け
									ELSE
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;起立
								CASE 4
									;Ｂでイった直後は膝立ちに変化する
									IF NOWEX:MASTER:Ｂ絶頂
										TCVAR:MASTER:姿勢 = 10
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 4
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;膝立ち
								CASE 10
									;Ｂでイった直後は座りに変化する
									IF NOWEX:MASTER:Ｂ絶頂
										TCVAR:MASTER:姿勢 = 2
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 10
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 1
									ENDIF
							ENDSELECT
						ENDIF
					;手など
					ELSE
						;すでに仰向けの場合
						IF TFLAG:仰向け
							TCVAR:MASTER:姿勢 = 1
							TCVAR:MASTER:位置上下 = 1
							TCVAR:MASTER:位置前後 = 1
						ELSE
							;前回の姿勢によって補正
							;口と違い、前後・上下の指定はありません
							SELECTCASE TCVAR:MASTER:前回の姿勢
								;仰向け
								CASE 1
									;1/4で座りに変化する
									IF !RAND:4
										TCVAR:MASTER:姿勢 = 2
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 1
										TCVAR:MASTER:位置前後 = 1
									ENDIF
								;座り、自由
								CASE 2, 0
									;Ｂでイった直後は1/2で仰向けに変化する
									IF NOWEX:MASTER:Ｃ絶頂 && RAND:2
										TCVAR:MASTER:姿勢 = 1
										TCVAR:MASTER:位置上下 = 1
										TCVAR:MASTER:位置前後 = 1
									;そうでなければ座り
									ELSE
										TCVAR:MASTER:姿勢 = 2
									ENDIF
								;四つん這い
								CASE 3
									;位置が前でない場合、2/3で後ろから継続
									IF TCVAR:MASTER:位置前後 != 1 && RAND:3
										TCVAR:MASTER:姿勢 = 3
										TCVAR:MASTER:位置上下 = 0
										TCVAR:MASTER:位置前後 = 2
									;そうでなければ座りに変化する
									ELSE
										TCVAR:MASTER:姿勢 = 2
									ENDIF
								;起立
								CASE 4
									;Ｂでイった直後は膝立ちに変化する
									IF NOWEX:MASTER:Ｂ絶頂
										TCVAR:MASTER:姿勢 = 10
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 4
									ENDIF
								;膝立ち
								CASE 10
									;Ｂでイった直後は座りに変化する
									IF NOWEX:MASTER:Ｂ絶頂
										TCVAR:MASTER:姿勢 = 2
									;そうでなければ継続
									ELSE
										TCVAR:MASTER:姿勢 = 10
									ENDIF
							ENDSELECT
						ENDIF
					ENDIF
			ENDSELECT
		CASE "奉仕"
			;すでに仰向けの場合（やりにくいと思うけど…）
			IF TFLAG:仰向け
				TCVAR:MASTER:姿勢 = 1
				TCVAR:MASTER:位置前後 = 0
				TCVAR:MASTER:位置上下 = 1
			ELSE
				;前回の姿勢によって補正
				SELECTCASE TCVAR:MASTER:前回の姿勢
					;仰向け…下から触る
					CASE 1
						TCVAR:MASTER:姿勢 = 1
						TCVAR:MASTER:位置前後 = 0
						TCVAR:MASTER:位置上下 = 1
					;他…座って前から
					;手の場合、COMで積極的従うことで後ろに回ることがある
					CASEELSE
						TCVAR:MASTER:姿勢 = 2
						TCVAR:MASTER:位置前後 = 1
						TCVAR:MASTER:位置上下 = 0
				ENDSELECT
			ENDIF
		;その他
		CASEELSE
			;すでに仰向けの場合
			IF TFLAG:仰向け
				TCVAR:MASTER:姿勢 = 1
			ELSE
				;個別の設定
				SELECTCASE TFLAG:ACT
					;キス
					CASE 15
						;自由の場合座らせる
						TCVAR:MASTER:姿勢 = TCVAR:MASTER:前回の姿勢 ? TCVAR:MASTER:前回の姿勢 # 2
						;自由の場合前に
						TCVAR:MASTER:位置前後 = TCVAR:MASTER:前回の位置前後 ? TCVAR:MASTER:前回の位置前後 # 1
					;自慰、放尿
					CASE 40, 45
						;自由の場合座らせるのみ
						TCVAR:MASTER:姿勢 = TCVAR:MASTER:前回の姿勢 ? TCVAR:MASTER:前回の姿勢 # 2
					;秘貝開帳
					CASE 41
						;座り対面
						TCVAR:MASTER:姿勢 = 2
						TCVAR:MASTER:位置前後 = 1
						TCVAR:MASTER:位置上下 = 0
					;自慰見せつけ、ペニスバンド装着
					CASE 42, 75
						;対面
						TCVAR:MASTER:位置前後 = 1
					;大鏡、野外プレイ、コスプレ、休憩、栄養剤
					CASE 43, 44, 46, 90, 91
						;リセット
						TCVAR:MASTER:姿勢 = 0
						TCVAR:MASTER:位置前後 = 0
						TCVAR:MASTER:位置上下 = 0
					;放置プレイ、公衆便所プレイ
					CASE 45, 74
						;位置リセット、姿勢1/2で自由、1/2で継続
						TCVAR:MASTER:姿勢 = RAND:2 ? TCVAR:MASTER:前回の姿勢 # 0
						TCVAR:MASTER:位置前後 = 0
						TCVAR:MASTER:位置上下 = 0
					;スパンキング、鞭、フィスト類
					CASE 60, 61, 70, 71, 72
						;四つん這い・後ろ
						TCVAR:MASTER:姿勢 = 3
						TCVAR:MASTER:位置前後 = 2
						TCVAR:MASTER:位置上下 = 0
					;縄・目隠し・ボールギャグ、治療
					CASE 63, 64, 65, 92
						;座り・後ろ
						TCVAR:MASTER:姿勢 = 2
						TCVAR:MASTER:位置前後 = 2
						TCVAR:MASTER:位置上下 = 0
					;三角木馬
					CASE 67
						;膝立ち、位置リセット
						TCVAR:MASTER:姿勢 = 10
						TCVAR:MASTER:位置前後 = 0
						TCVAR:MASTER:位置上下 = 0
				ENDSELECT
			ENDIF
	ENDSELECT
ENDIF

;フェラ継続中の場合、前後位置を前に固定
SIF TEQUIP:調教対象Ｃ使用 == 2
	TCVAR:MASTER:位置前後 = 1

;ここまでで自由姿勢だった場合
IF !TCVAR:MASTER:姿勢
	;性交中・Ｐ使用中・自慰中ならば前回の姿勢・位置を踏襲
	IF TEQUIP:性交中 || TEQUIP:性交奉仕中 || TEQUIP:調教対象Ｃ使用 || GETBIT(TEQUIP:自慰中, 0)
		TCVAR:MASTER:姿勢 = TCVAR:MASTER:前回の姿勢
		SIF !TCVAR:MASTER:位置前後
			TCVAR:MASTER:位置前後 = TCVAR:MASTER:前回の位置前後
		SIF !TCVAR:MASTER:位置上下
			TCVAR:MASTER:位置上下 = TCVAR:MASTER:前回の位置上下
	ENDIF
ENDIF

;ここまでで自由前後位置だった場合
IF !TCVAR:MASTER:位置前後 && !TCVAR:MASTER:位置上下
	;愛撫系・道具系ACTならば前回の位置を踏襲
	;前回が自由なら前と後ろにランダムで振る
	SIF GROUPMATCH(GET_ACTM(TFLAG:ACT), 1, 2)
		TCVAR:MASTER:位置前後 = RANGE(TCVAR:MASTER:位置前後, 1, 2) ? TCVAR:MASTER:位置前後 # RAND:2+1
ENDIF

;助手の性交をキャンセル
IF TFLAG:Ｐ使用中 || TFLAG:Ｐ挿入中
	TFLAG:助手に挿入中 = 0
	TFLAG:助手連続交合 = 0
ENDIF

@MASTER_POSE_ASSI, ARG
;助手行動
;手淫
IF TCVAR:ARG:助手ACT == 7
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
;クンニ,フェラ
ELSEIF TCVAR:ARG:助手ACT == 1
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
;貝あわせ,素股
ELSEIF TCVAR:ARG:助手ACT == 2
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
;パイズリ
ELSEIF TCVAR:ARG:助手ACT == 3
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
;足コキ
ELSEIF TCVAR:ARG:助手ACT == 4
	SIF TFLAG:仰向け == 0
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
;ローター, オナホール
ELSEIF TCVAR:ARG:助手ACT == 5
	TFLAG:Ｐ使用中 += 1
;逆レイプ
ELSEIF TCVAR:ARG:助手ACT == 6
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:Ｐ挿入中 = 1
;アナル愛撫
ELSEIF TCVAR:ARG:助手ACT == 10
	TFLAG:Ａ使用中 = 2
;アナル舐め
ELSEIF TCVAR:ARG:助手ACT == 11
	TFLAG:Ａ使用中 = 2
;アナルローター
ELSEIF TCVAR:ARG:助手ACT == 12
	TFLAG:Ａ使用中 = 2
;アナルバイブ
ELSEIF TCVAR:ARG:助手ACT == 13
	TFLAG:Ａ使用中 = 2
;胸愛撫
ELSEIF TCVAR:ARG:助手ACT == 20
	TFLAG:胸使用中 = 1

;乳首舐め
ELSEIF TCVAR:ARG:助手ACT == 21
	TFLAG:胸使用中 = 1
;乳首ローター
ELSEIF TCVAR:ARG:助手ACT == 22
	TFLAG:胸使用中 = 1
;指挿入
ELSEIF TCVAR:ARG:助手ACT == 30
	TFLAG:Ｖ使用中 = 2
;バイブ
ELSEIF TCVAR:ARG:助手ACT == 31
	TFLAG:Ｖ使用中 = 2
;性交
ELSEIF TCVAR:ARG:助手ACT == 32
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:Ｖ使用中 = 1
;キス
ELSEIF TCVAR:ARG:助手ACT == 40
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:口使用中 += 1
;顔面騎乗
ELSEIF TCVAR:ARG:助手ACT == 41
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	TFLAG:口使用中 = 2
;足を舐めさせる
ELSEIF TCVAR:ARG:助手ACT == 42
	TFLAG:口使用中 = 2
ENDIF


;────────────────────────────────────
;ACTごとのPOSE情報。主にMASTERについて
;現状MASTER側だけが情報を持っています。拡張性のためTCVARを利用
;どれも、=0は情報なし・自由ということで。会話とかそんな感じです
;
;結局、口上くらいしかこれらを参照していませんが…そのうち役に立つ？
;
;※要修正
;＠TCVAR:MASTER:1（姿勢）
;	姿勢情報を格納するらしい。TFLAG:仰向け（仰向け）はそのうちこちらに移植されるべきか？
;	一応、現在はTFLAG:仰向け=2から１ターンでは起き上がれないという要素があるので残しつつこちらにも記録することに。
;	ほとんどこのTCVARは参照されていませんが、ここでデータだけは入れておいてもいいよね。
;	(0=自由 1=仰向け 2=座る 3=四つん這い 4=起立 10=膝立ち)
;
;＠TCVAR:MASTER:2（位置前後）
;	MASTERとTARGETの前後位置情報だけ格納。現状MASTER側だけが持っています
;	=0は自由
;	=1で対面（Ｍ→←Ｔ）
;	=2でMASTERの背後（Ｔ→Ｍ→）
;	=3でMASTERが背後（Ｍ→Ｔ→）
;
;＠TCVAR:MASTER:3（位置上下）
;	MASTERとTARGETの上下位置情報。現状MASTER側だけが持っています
;	=1で正位（MASTERが組み敷かれている）
;	=2で逆位（MASTERがTARGETを組み敷いている）

;これでとりあえず、大まかには表せます。
;────────────────────────────────────
;010,愛撫/手淫する（Ｓ/Ｎ/Ｈ）（快Ｃ、快Ｂ、接触、情愛、性行動、不潔）
;────────────────────────────────────
@MASTER_POSE_ACT10
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1

;────────────────────────────────────
;011,胸愛撫/乳首吸い/乳の揉み合い（Ｓ/Ｎ/Ｈ）（快Ｂ、接触、情愛、性行動、痛み、不潔）
;────────────────────────────────────
@MASTER_POSE_ACT11
	TFLAG:胸使用中 = 1
	;乳首吸い、乳の揉み合いは対面
	SIF GROUPMATCH(TFLAG:ACT派生, 1, 2)
		TCVAR:MASTER:位置前後 = 1

;────────────────────────────────────
;012,クンニ/フェラする（Ｓ/Ｎ/Ｈ）（快Ｃ、接触、情愛、性行動、痛み、不潔）
;────────────────────────────────────
@MASTER_POSE_ACT12
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
	SIF !PENIS(MASTER)
		TFLAG:Ｖ使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;013,アナル愛撫（Ｎ/Ｈ）[罰]（快Ａ、接触、性行動、痛み、不潔、逸脱）
;────────────────────────────────────
@MASTER_POSE_ACT13
	TFLAG:Ａ使用中 += 1
;────────────────────────────────────
;014,アナル舐め（Ｎ/Ｈ）[罰]（快Ａ、接触、性行動、痛み、不潔、逸脱）
;────────────────────────────────────
@MASTER_POSE_ACT14
	TFLAG:Ａ使用中 += 1
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;015,キス（Ｓ/Ｎ）（接触、情愛、性行動、痛み、不潔）
;────────────────────────────────────
@MASTER_POSE_ACT15
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:口使用中 += 1
;────────────────────────────────────
;016,貝あわせ/素股する（Ｓ/Ｎ）（快Ｃ、接触、情愛、性行動、不潔、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT16
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
	TFLAG:Ｖ使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;017,パイズリする（Ｓ/Ｎ）（快Ｃ、接触、情愛、痛み、性行動、不潔、快ｂ）
;────────────────────────────────────
@MASTER_POSE_ACT17
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;018,足コキする（Ｓ/Ｎ/Ｈ）[罰]（快Ｃ、接触、情愛、性行動、不潔、逸脱）
;────────────────────────────────────
@MASTER_POSE_ACT18
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;018,指挿入れ
;────────────────────────────────────
@MASTER_POSE_ACT19
	TFLAG:Ｖ使用中 += 1
;────────────────────────────────────
;020,ローター（Ｓ/Ｎ）（快Ｃ、中毒充足）
;────────────────────────────────────
@MASTER_POSE_ACT20
	TFLAG:Ｐ使用中 = 6
;────────────────────────────────────
;021,バイブ（Ｓ/Ｎ/Ｈ）[罰]（快Ｖ、痛み、逸脱、中毒充足、トラウマ）
;────────────────────────────────────
@MASTER_POSE_ACT21
	TFLAG:Ｖ使用中 = 2
;────────────────────────────────────
;022,アナルバイブ（Ｎ/Ｈ）[罰]（快Ａ、痛み、逸脱、中毒充足）
;────────────────────────────────────
@MASTER_POSE_ACT22
	TFLAG:Ａ使用中 = 2
;────────────────────────────────────
;023,アナルビーズ（Ｓ/Ｎ/Ｈ）（快Ａ、痛み、逸脱、中毒充足）
;────────────────────────────────────
@MASTER_POSE_ACT23
	TFLAG:Ａ使用中 = 2
;────────────────────────────────────
;024,クリキャップ/オナホール（Ｓ/Ｎ）（快Ｃ、中毒充足）
;────────────────────────────────────
@MASTER_POSE_ACT24
	TFLAG:Ｐ使用中 = 5
;────────────────────────────────────
;025,ニプルキャップ
;────────────────────────────────────
@MASTER_POSE_ACT25
	TFLAG:胸使用中 = 1
;────────────────────────────────────
;026,ペニスバンド
;────────────────────────────────────
@MASTER_POSE_ACT26
	TFLAG:Ｐ使用中 = 6
	TFLAG:Ａ使用中 = 2
	TFLAG:Ｖ使用中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;030,正常位（Ｓ/Ｎ）（快Ｖ、接触、情愛、性行動、痛み、中毒充足、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT30
	TFLAG:仰向け = 2
	TFLAG:Ｖ使用中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;031,後背位（Ｎ/Ｈ）[罰]（快Ｖ、接触、情愛、性行動、痛み、中毒充足、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT31
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中 = 1
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;032,騎乗位（Ｓ/Ｎ）（快Ｖ、接触、情愛、性行動、痛み、中毒充足、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT32
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;033,対面座位（Ｓ/Ｎ/Ｈ）（快Ｖ、接触、情愛、性行動、痛み、中毒充足、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT33
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;034,背面座位（Ｎ/Ｈ）（快Ｖ、接触、情愛、性行動、痛み、露出、中毒充足、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT34
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中 = 1
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;035,アナルセックス（Ｎ/Ｈ/Ａ）[罰]（快Ａ、接触、情愛、性行動、痛み、逸脱、中毒充足、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT35
	TFLAG:Ａ使用中 = 2
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;103,逆レイプ（Ｎ/Ｈ/Ａ）[罰]（快Ｃ、接触、情愛、性行動、痛み、逸脱、中毒充足、トラウマ、快ｖ）
;────────────────────────────────────
@MASTER_POSE_ACT103
	TFLAG:仰向け = 2
	TFLAG:Ｐ挿入中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;041,秘貝開帳（Ｓ/Ｎ/Ｈ）（快Ｃ、露出、逸脱）
;────────────────────────────────────
@MASTER_POSE_ACT41
	TFLAG:Ｖ使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;045,放尿（Ｎ/Ｈ/Ａ）[罰]（露出、逸脱、トラウマ）
;────────────────────────────────────
@MASTER_POSE_ACT45
	TFLAG:Ｐ使用中 = 3
	TFLAG:Ｖ使用中 = 2
;────────────────────────────────────
;050,手淫/愛撫強制（Ｓ/Ｎ）（不潔）
;────────────────────────────────────
;奉仕系の行動は拒否できるので、反応が出るまで身体の接触がないようにしておきます
@MASTER_POSE_ACT50
	TFLAG:足使用中 += 1
	;対面（助手に事前に前を開けさせるため）
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;051,フェラチオ/クンニ強制（Ｓ/Ｎ/Ｈ）（不潔）
;────────────────────────────────────
@MASTER_POSE_ACT51
	SIF TFLAG:仰向け
		TFLAG:仰向け = 2
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	SIF TEQUIP:調教対象Ｃ使用 == 1 || TEQUIP:調教対象Ｃ使用 == 2 || TEQUIP:調教対象Ｃ使用 == 4
		TFLAG:仰向け = 2
	TFLAG:口使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;052,パイズリ（Ｓ/Ｎ/Ｈ）（不潔）
;────────────────────────────────────
@MASTER_POSE_ACT52
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:胸使用中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;053,素股（Ｓ/Ｎ/Ｈ）（不潔）
;────────────────────────────────────
@MASTER_POSE_ACT53
	TFLAG:Ｐ使用中 = 4
	TFLAG:Ｖ使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;055,足舐め強制（Ｎ/Ｈ/Ａ）[罰]（不潔、逸脱、トラウマ）
;────────────────────────────────────
@MASTER_POSE_ACT55
	TFLAG:口使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;056,イラマチオ（Ｈ/Ａ）[罰]（接触、情愛逆、性行動、痛み、拘束<=頭を掴む事、不潔、逸脱、トラウマ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT56
	TFLAG:口使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1

;────────────────────────────────────
;070,フィストファック（Ｈ/Ａ）[罰]（快Ｖ、接触、痛み、逸脱、中毒充足、トラウマ）
;────────────────────────────────────
@MASTER_POSE_ACT70
	TFLAG:Ｖ使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;071,アナルフィスト（Ｈ/Ａ）[罰]（快Ａ、接触、痛み、逸脱、中毒充足、トラウマ）
;────────────────────────────────────
@MASTER_POSE_ACT71
	TFLAG:Ａ使用中 = 2
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;072,両穴フィスト（Ａ）[罰]（快Ｖ、快Ａ、接触、痛み、逸脱、中毒充足、トラウマ）
;────────────────────────────────────
@MASTER_POSE_ACT72
	TFLAG:Ａ使用中 = 2
	TFLAG:Ｖ使用中 = 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;095,正常位させる（Ｎ/Ｈ/Ａ）[接触]（快Ｃ、接触、情愛、性行動、中毒充足、達成、悦楽、快ｖ）
;────────────────────────────────────
@MASTER_POSE_ACT95
	TFLAG:仰向け = 0
	TFLAG:Ｐ挿入中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;096,後背位させる（Ｎ/Ｈ/Ａ）[接触]（快Ｃ、接触、情愛、性行動、中毒充足、達成、悦楽、快ｖ）
;────────────────────────────────────
@MASTER_POSE_ACT96
	TFLAG:仰向け = 0
	TFLAG:Ｐ挿入中 = 1
	;背面
	TCVAR:MASTER:位置前後 = 3
;────────────────────────────────────
;097,対面座位させる（Ｎ/Ｈ/Ａ）[接触]（快Ｃ、接触、情愛、性行動、中毒充足、達成、悦楽、快ｖ、快ｃ）
;────────────────────────────────────
@MASTER_POSE_ACT97
	TFLAG:仰向け = 0
	TFLAG:Ｐ挿入中 = 1
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;098,背面座位させる（Ｎ/Ｈ/Ａ）[接触]（快Ｃ、接触、情愛、性行動、中毒充足、達成、悦楽、快ｖ、快ｃ、快ｂ）
;────────────────────────────────────
@MASTER_POSE_ACT98
	TFLAG:仰向け = 0
	TFLAG:Ｐ挿入中 = 1
	;背面
	TCVAR:MASTER:位置前後 = 3
;────────────────────────────────────
;099,アナルセックスさせる（Ｎ/Ｈ/Ａ）[接触]（快Ｃ、接触、情愛、性行動、中毒充足、達成、悦楽、快ｖ、快ｃ、快ｂ）
;────────────────────────────────────
@MASTER_POSE_ACT99
	IF	TFLAG:仰向け == 0
		SIF RAND:2 == 0
			TFLAG:仰向け = 2
	ELSE
		TFLAG:仰向け = 2
	ENDIF
	TFLAG:Ｐ挿入中 = 1
	;背面
	TCVAR:MASTER:位置前後 = 3

;※要修正
;↑ここまであんまり弄ってないです。Ｐ使用中フラグの代入とかだいぶ変ですが現状では実害ないので放置
;そのうち直すタスクということで

;────────────────────────────────────
;300,Ｗ手淫
;────────────────────────────────────
@MASTER_POSE_ACT300
	;仰向け判定は手淫と同じ
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	;ＷでＰ使用中
	TFLAG:Ｐ使用中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;301,Ｗフェラ
;────────────────────────────────────
@MASTER_POSE_ACT301
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;302,Ｗキス
;────────────────────────────────────
@MASTER_POSE_ACT302
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:口使用中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;303,Ｗ素股
;────────────────────────────────────
@MASTER_POSE_ACT303
	TFLAG:Ｐ使用中 += 2
	TFLAG:Ｖ使用中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;304,Ｗパイズリ
;────────────────────────────────────
@MASTER_POSE_ACT304
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;305,Ｗ足コキ
;────────────────────────────────────
@MASTER_POSE_ACT305
	SIF RAND:(10 + ABL:欲望 + ABL:技巧) > RAND:(10 + ABL:MASTER:技巧)
		TFLAG:仰向け = 2
	TFLAG:Ｐ使用中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;306,騎乗二穴責め
;────────────────────────────────────
@MASTER_POSE_ACT306
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中++
	TFLAG:Ａ使用中++
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;307,対面二穴責め
;────────────────────────────────────
@MASTER_POSE_ACT307
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中++
	TFLAG:Ａ使用中++
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;308,変則乱れ牡丹
;────────────────────────────────────
@MASTER_POSE_ACT308
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中++
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;309,アナル二穴責め
;────────────────────────────────────
@MASTER_POSE_ACT309
	TFLAG:仰向け = 0
	TFLAG:Ｖ使用中++
	TFLAG:Ａ使用中++
	;背後
	TCVAR:MASTER:位置前後 = 2
;────────────────────────────────────
;310,Ｗ逆レイプ
;────────────────────────────────────
@MASTER_POSE_ACT310
	TFLAG:仰向け = 2
	TFLAG:Ｐ挿入中 += 2
	;対面
	TCVAR:MASTER:位置前後 = 1
;────────────────────────────────────
;311,谷渡り
;────────────────────────────────────
@MASTER_POSE_ACT311
	TFLAG:仰向け = 0
	TFLAG:Ｐ挿入中 += 2
	;背面
	TCVAR:MASTER:位置前後 = 3
