;────────────────────────────────────
;一般追加行動 (Ｃ愛撫1/Ｖ愛撫2/Ａ愛撫3/Ｂ愛撫4)
;────────────────────────────────────
@ACT_EXTRA_M0
LOCAL:1 = (10 + ABL:MASTER:Ｃ感覚) * RAND:100
LOCAL:2 = (10 + ABL:MASTER:Ｖ感覚) * RAND:100
LOCAL:3 = (10 + ABL:MASTER:Ａ感覚) * RAND:100
LOCAL:4 = (10 + ABL:MASTER:Ｂ感覚) * RAND:100

FOR LOCAL:0, 1, 5
	CALLFORM ACT_EXTRA_ABLE{LOCAL:0}
	LOCAL:(LOCAL:0) = RESULT && LOCAL:(LOCAL:0) > -99 ? LOCAL:(LOCAL:0) # -99
NEXT

;TARGETの気力が低いと追加行動が実行出来なくなる処理
;このタイミングで処理する理由
;1.RAND要素があるため、実行判定には組み込めない
;2.TFLAG:229決定後に行うと、MASTERの部位使用フラグを破壊してしまう
SIF GET_ABL(TARGET, "技巧") * BASE:TARGET:気力 < RAND:200 * MAXBASE:TARGET:気力
	LOCAL:1 = -99, -99, -99, -99

SELECTCASE MAX(LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4)
	CASE -99
		TFLAG:追加行動 = 0
	CASE LOCAL:1
		TFLAG:追加行動 = 1
		TFLAG:Ｐ使用中 += 1
	CASE LOCAL:2
		TFLAG:追加行動 = 2
		TFLAG:Ｖ使用中 += 1
	CASE LOCAL:3
		TFLAG:追加行動 = 3
		TFLAG:Ａ使用中 += 1
	CASE LOCAL:4
		TFLAG:追加行動 = 4
		TFLAG:胸使用中 += 1
ENDSELECT
RETURN TFLAG:追加行動


@ACT_EXTRA_MESSAGE_M0
SELECTCASE TFLAG:追加行動
	CASE 1
		PRINTFORML さらに%CALLNAME:TARGET%は%CALLNAME:MASTER%の\@ PENIS(MASTER) ? 男性器を愛撫し # 体をまさぐり \@始めた…
		SOURCE:MASTER:快Ｃ += 75 + ACT_FROM(TARGET, 1) * (2 + TALENT:器用な指先) / 8
		TCVAR:MASTER:今回の勃起 += (100 + GET_ABL(TARGET, "技巧") + GET_ABL(TARGET, "愛撫") + TALENT:器用な指先 * 50) / 2
	CASE 2
		PRINTFORML さらに%CALLNAME:TARGET%は%CALLNAME:MASTER%の秘唇に指を挿入し、中をかき回した…
		SOURCE:MASTER:快Ｖ +=  100 + ACT_FROM(TARGET, 2) * (2 + TALENT:器用な指先) / 4
		;ふたなりはＶで前立腺刺激
		SIF TALENT:MASTER:ふたなり
			TCVAR:MASTER:前立腺刺激 += 1
		TCVAR:MASTER:今回の勃起 += (GET_ABL(TARGET, "技巧") + GET_ABL(TARGET, "愛撫") + TALENT:器用な指先 * 50) / 4
	CASE 3
		PRINTFORML さらに%CALLNAME:TARGET%は%CALLNAME:MASTER%のアヌスを愛撫し始めた…
		SOURCE:MASTER:快Ａ += 60 + ACT_FROM(TARGET, 3) * (2 + TALENT:器用な指先) / 4
		;オトコはＡで前立腺刺激
		SIF TALENT:MASTER:オトコ
			TCVAR:MASTER:前立腺刺激 += 1
		TCVAR:MASTER:今回の勃起 += (100 + GET_ABL(MASTER, "Ａ感覚") + GET_ABL(MASTER, "マゾっ気") * GET_ABL(TARGET, "技巧") / 50) / 2
	CASE 4
		PRINTFORML さらに%CALLNAME:TARGET%は%CALLNAME:MASTER%の乳首を愛撫し始めた…
		SOURCE:MASTER:快Ｂ += 75 + ACT_FROM(TARGET, 4) * (2 + TALENT:器用な指先) / 6
		TCVAR:MASTER:今回の勃起 += 40 + (GET_ABL(TARGET, "技巧") + GET_ABL(TARGET, "愛撫") + TALENT:器用な指先 * 50) / 6
ENDSELECT


;-----------------------------------------------------------
;一般追加行動 の実行判定
;-----------------------------------------------------------
@ACTM_EXTRA_ABLE0
;調教メニューが愛撫/道具以外
SIF MENUMATCH(TFLAG:ACT, "愛撫") !| MENUMATCH(TFLAG:ACT, "道具")
	RETURN 0
;調教行動が足コキする/押し倒すの場合は無理
SIF IS_NOWACTNAME("足コキする/押し倒す")
	RETURN 0
FOR LOCAL:0, 1, 5
	CALLFORM ACT_EXTRA_ABLE{LOCAL:0}
	SIF RESULT
		RETURN 1
NEXT
RETURN 0

;-----------------------------------------------------------
;Ｃ愛撫 の実行判定
;-----------------------------------------------------------
@ACT_EXTRA_ABLE1
;Ｐが塞がっている(Ｐ挿入中/Ｐ使用中/性交奉仕中/オナホール/MASTERが助手に挿入中)
SIF TFLAG:Ｐ挿入中 || TFLAG:Ｐ使用中 || TEQUIP:性交奉仕中 || TEQUIP:オナホール || TFLAG:助手に挿入中
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;Ｖ愛撫 の実行判定
;-----------------------------------------------------------
@ACT_EXTRA_ABLE2
;Ｖが塞がっている(Ｖ使用中/バイブ/Ｖ性交)
SIF TFLAG:Ｖ使用中 || TEQUIP:バイブ || V_SEX() || TEQUIP:ペニスバンド
	RETURN 0
;調教者に壁抜けがない
IF !TALENT:壁抜け
	;調教対象の下半身上着で妨害される
	SIF CLOTHESTYPE(1, 1) == 2
		RETURN 0
ENDIF
;MASTERが処女
SIF TALENT:MASTER:処女
	RETURN 0
;MASTERがオトコ
SIF TALENT:MASTER:オトコ
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;Ａ愛撫 の実行判定
;-----------------------------------------------------------
@ACT_EXTRA_ABLE3
;Ａが塞がっている(Ａ使用中/アナルバイブ/アナルビーズ/浣腸器＋プラグ/Ａ性交)
SIF TFLAG:Ａ使用中 || TEQUIP:アナルバイブ || TEQUIP:アナルビーズ || TEQUIP:浣腸器＋プラグ || TEQUIP:性交中 == 6 || TEQUIP:ペニスバンド
	RETURN 0
;調教者に壁抜けがない
IF !TALENT:壁抜け
	;調教対象の下半身上着で妨害される
	SIF CLOTHESTYPE(1, 1) == 2
		RETURN 0
	;調教対象の下半身下着で強く妨害される
	SIF CLOTHESTYPE(1, 0)
		RETURN 0
ENDIF
;性交奉仕中で、体勢的に無理な体位(後背位/騎乗位/対面座位/背面座位)
SIF GROUPMATCH(TEQUIP:性交奉仕中, 2, 3, 4, 5) > 0
	RETURN 0
;ACTがパイズリ
SIF IS_NOWACTNAME("パイズリする")
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;Ｂ愛撫 の実行判定
;-----------------------------------------------------------
@ACT_EXTRA_ABLE4
;Ｂが塞がっている(Ｂ使用中/ニプルキャップ)
SIF TFLAG:胸使用中 || TEQUIP:ニプルキャップ
	RETURN 0
;フェラ中などは無理
SIF IS_NOWACTNAME("口で愛撫") || TEQUIP:調教対象Ｃ使用 == 2
	RETURN 0
;顔面騎乗中
SIF TEQUIP:顔面騎乗 || TEQUIP:顔面騎乗アナル
	RETURN 0
;調教者に壁抜けがなく、調教対象がオトコ
IF !TALENT:壁抜け && TALENT:MASTER:オトコ
	;調教対象の上半身上着で妨害される
	SIF CLOTHESTYPE(1, 3) == 2
		RETURN 0
	;調教対象の上半身下着で妨害される
	SIF CLOTHESTYPE(1, 2) == 2
		RETURN 0
ENDIF
;性交奉仕中で、体勢的に無理な体位(後背位/背面座位)
SIF GROUPMATCH(TEQUIP:性交奉仕中, 2, 5) > 0
	RETURN 0
RETURN 1
