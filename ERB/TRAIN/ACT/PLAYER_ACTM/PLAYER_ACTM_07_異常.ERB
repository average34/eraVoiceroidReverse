;────────────────────────────────────
;異常 (Fist70/ＡFi71/両Fi72/放置73/公衆74/Ｐ装75/嗤う9/足で18/足舐55/木馬67)
;────────────────────────────────────
@ACT_M7(ARG = 0)
;1以上なら判定を行わない
#DIM LCOUNT, 1

;関数初期化
CALL ACT_M_INIT(7)

;処理開始

LOCALS = ACT_ACTM7

CALL CHA_CRI_VAR, "基準値"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, "調教者の素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "調教対象の素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "調教対象のフィスト適性"
CALLFORM %LOCALS%_FIST

CALL CHA_CRI_VAR, "調教者の能力"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "調教対象の能力"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "調教者のALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "調教者の気力"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "調教者の理性"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "調教者の興味"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "調教対象の体力"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, @"調教者の状態(%CONDITION(TCVAR:状態変化)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, "媚薬"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "縄"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "アイマスク"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "ボールギャグ"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "三角木馬"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "野外プレイ"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "道具をはずした"
CALLFORM %LOCALS%_TAKE_OFF_TOOLS

CALL CHA_CRI_VAR, "調教者自慰中"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "調教対象の衣服"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "もっとハードにして"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "許しを乞う"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "お仕置き"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "連続行動補正"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "口上補正値"
CALL ACT_ACTM_KOJO_CORRECTION_T, 7

CALL CHA_CRI_VAR, "ACT補正値"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARGが1以上なら処理中断
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "実行判定"
CALLFORM %LOCALS%_ABLE

;────────────────────────────────────
;最終判定
;────────────────────────────────────
CALL DEF_CRI_VAR("最大前者")
LOCAL = GET_CRI_VAR("決定")
IF LOCAL == -1
	SIF FLAG:デバッグ
		PRINTL (異常ACT分類に実行可能なACTが存在しません)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "異常")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;基準値
;-------------------------------------------------------------------------
@ACT_ACTM7_REFERENCE

;要らないかもと思ったので試しに無効化@これみ
[SKIPSTART]
CALL ADD_CRI_VAR("フィストファック", 10)
CALL ADD_CRI_VAR("アナルフィスト", 10)
CALL ADD_CRI_VAR("両穴フィスト", 10)
CALL ADD_CRI_VAR("放置プレイ", 10)
CALL ADD_CRI_VAR("公衆肉便器プレイ", 10)
CALL ADD_CRI_VAR("不気味に笑う", 8)
CALL ADD_CRI_VAR("足コキする", 5)
CALL ADD_CRI_VAR("足舐め強制", 5)
CALL ADD_CRI_VAR("三角木馬", 5)
[SKIPEND]

;-------------------------------------------------------------------------
;調教者の素質を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_TALENT_T

;調教者が臆病/気丈
IF TALENT:臆病
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)
ELSEIF TALENT:気丈
	CALL ADD_CRI_VAR("公衆肉便器プレイ", , 5)
ENDIF

;調教者が大人しい/生意気
IF TALENT:大人しい
	CALL ADD_CRI_VAR("フィストファック", -5)
	CALL ADD_CRI_VAR("アナルフィスト", -6)
	CALL ADD_CRI_VAR("両穴フィスト", -7)
ELSEIF TALENT:生意気
	CALL ADD_CRI_VAR("放置プレイ", , 4)
	CALL ADD_CRI_VAR("不気味に笑う", , 6)
	CALL ADD_CRI_VAR("足コキする", 2, 2)
ENDIF

;調教者がプライド高い/低い
IF TALENT:プライド高い
	CALL ADD_CRI_VAR("足舐め強制", , 5)
ELSEIF TALENT:プライド低い
	CALL ADD_CRI_VAR("足舐め強制", , -5)
ENDIF

;調教者が自制的/衝動的
IF TALENT:自制的
	CALL ADD_CRI_VAR("放置プレイ", , 4)
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)
ELSEIF TALENT:衝動的
	CALL ADD_CRI_VAR("公衆肉便器プレイ", , 5)
	CALL ADD_CRI_VAR("三角木馬", 4)
ENDIF

;調教者が無関心/好奇心
IF TALENT:無関心
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 5)
ELSEIF TALENT:好奇心
	CALL ADD_CRI_VAR("足コキする", , 6)
ENDIF

;調教者が感情乏しい/感情豊富な
IF TALENT:感情乏しい
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 3)
ELSEIF TALENT:情緒豊か
	CALL ADD_CRI_VAR("不気味に笑う", , 6)
ENDIF

;調教者が目立ちたがり
IF TALENT:目立ちたがり
	CALL ADD_CRI_VAR("足コキする", 5)
	CALL ADD_CRI_VAR("足舐め強制", 3)
ENDIF

;調教者が抑圧/解放
IF TALENT:抑圧
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)
	CALL ADD_CRI_VAR("不気味に笑う", -5)
	CALL ADD_CRI_VAR("足コキする", -5)
	CALL ADD_CRI_VAR("足舐め強制", -5)
ELSEIF TALENT:解放
	CALL ADD_CRI_VAR("公衆肉便器プレイ", , 6)
	CALL ADD_CRI_VAR("足コキする", , 4)
ENDIF

;調教者が恥じらい/恥薄い
IF TALENT:恥じらい
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)
ELSEIF TALENT:恥薄い
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 5)
ENDIF

;調教者が献身的/受け身
IF TALENT:献身的
	CALL ADD_CRI_VAR("足コキする", 5)
ELSEIF TALENT:受け身
	CALL ADD_CRI_VAR("足舐め強制", 5)
ENDIF

;調教者が男嫌いで調教対象がオトコ。進展度に応じて緩和
SIF TALENT:男嫌い && TALENT:MASTER:オトコ
	CALL ADD_CRI_VAR("足コキする", CFLAG:M好意 + CFLAG:好意 >= 10000? -4 # -7)

;調教者がサド
IF TALENT:サド
	CALL ADD_CRI_VAR("足舐め強制", , 6)
	CALL ADD_CRI_VAR("三角木馬", 3, 4)
ENDIF

;調教者が慎重/短気
IF TALENT:慎重
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)
ELSEIF TALENT:短気
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 5)
ENDIF

;調教者が意地悪/心根優しい
IF TALENT:意地悪
	CALL ADD_CRI_VAR("不気味に笑う", 3)
	CALL ADD_CRI_VAR("足コキする", 3)
ELSEIF TALENT:心根優しい
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)
	CALL ADD_CRI_VAR("三角木馬", -5)
ENDIF

;調教者が狂気
IF TALENT:狂気
	CALL ADD_CRI_VAR("フィストファック", , 4)
	CALL ADD_CRI_VAR("アナルフィスト", , 4)
	CALL ADD_CRI_VAR("両穴フィスト", , 4)
	CALL ADD_CRI_VAR("放置プレイ", , 4)
	CALL ADD_CRI_VAR("公衆肉便器プレイ", , 4)
	CALL ADD_CRI_VAR("不気味に笑う", , 7)
	CALL ADD_CRI_VAR("足コキする", , 4)
	CALL ADD_CRI_VAR("足舐め強制", , 4)
	CALL ADD_CRI_VAR("三角木馬", , 4)
ENDIF

;調教者がＣ敏感
SIF TALENT:Ｃ敏感
	CALL ADD_CRI_VAR("足コキする", 2, 4)

;----------------------------------------------------------------------
;フィスト適性
;経験と双方の素質が絡むので素質から独立
;
;ただ、小柄長身によって乗算をかける意義があんまり感じられないし
;それぞれ経験と素質で、普通に加減すればいいと思うのと
;経験がないとき、マイナスになるっていうのを採用するんなら基礎値の概念使って
;----------------------------------------------------------------------
@ACT_ACTM7_FIST
;小柄体型/巨躯/ＶＡ経験/拡張経験/淫系素質などでフィストの処理をしてみる
VARSET LOCAL

;Ｖ経験/拡張経験/淫系素質によって増減
IF EXP:MASTER:Ｖ経験 < 10
	LOCAL:0 = -6
ELSEIF EXP:MASTER:Ｖ経験 < 20 && EXP:MASTER:Ｖ拡張経験 < 1
	LOCAL:0 = -4
ELSEIF EXP:MASTER:Ｖ経験 < 40 && EXP:MASTER:Ｖ拡張経験 < 3
	LOCAL:0 = -2
ENDIF
SIF TALENT:MASTER:淫壷
	LOCAL:0 += 4

;Ａ経験/拡張経験/淫系素質によって増減
IF EXP:MASTER:Ａ経験 < 10
	LOCAL:1 = -6
ELSEIF EXP:MASTER:Ａ経験 < 20 && EXP:MASTER:Ａ拡張経験 < 1
	LOCAL:1 = -4
ELSEIF EXP:MASTER:Ａ経験 < 40 && EXP:MASTER:Ａ拡張経験 < 3
	LOCAL:1 = -2
ENDIF
SIF TALENT:MASTER:淫尻
	LOCAL:1 += 4

;小柄体型/長身によって増減
IF TALENT:MASTER:小柄 || TALENT:長身
	LOCAL:0 = LOCAL:0 * 3 / 4
	LOCAL:1 = LOCAL:1 * 3 / 4
ELSEIF TALENT:MASTER:長身 || TALENT:小柄
	LOCAL:0 = LOCAL:0 * 4 / 3
	LOCAL:1 = LOCAL:1 * 4 / 3
ENDIF

;加算処理
CALL ADD_CRI_VAR("フィストファック", LOCAL:0)
CALL ADD_CRI_VAR("アナルフィスト", LOCAL:1)
CALL ADD_CRI_VAR("両穴フィスト", LOCAL:0 + LOCAL:1)

;-------------------------------------------------------------------------
;調教対象の素質を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_TALENT_M

;調教対象が臆病/気丈
IF TALENT:MASTER:臆病
	CALL ADD_CRI_VAR("不気味に笑う", 3)
ELSEIF TALENT:MASTER:気丈
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 3)
ENDIF

;調教対象が反抗的/素直
IF TALENT:MASTER:反抗的
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 4)
	CALL ADD_CRI_VAR("三角木馬", 3)
ELSEIF TALENT:MASTER:素直
	CALL ADD_CRI_VAR("足舐め強制", 3)
ENDIF

;調教対象が恥じらい/恥薄い
IF TALENT:MASTER:恥じらい
	CALL ADD_CRI_VAR("放置プレイ", 3)
ELSEIF TALENT:MASTER:恥薄い
	CALL ADD_CRI_VAR("放置プレイ", -3)
ENDIF

;調教対象が痛みに弱い/痛みに強い
IF TALENT:MASTER:痛みに弱い
	CALL ADD_CRI_VAR("三角木馬", 5)
ELSEIF TALENT:MASTER:痛みに強い
	CALL ADD_CRI_VAR("三角木馬", -5)
ENDIF

;調教対象が淫茎
SIF TALENT:MASTER:淫核／淫茎
	CALL ADD_CRI_VAR("足コキする", , 9)

;調教対象がＣ敏感/鈍感
IF TALENT:MASTER:Ｃ敏感
	CALL ADD_CRI_VAR("足コキする", 1, 6)
ELSEIF TALENT:MASTER:Ｃ鈍感
	CALL ADD_CRI_VAR("足コキする", -1, -6)
ENDIF

;調教対象がＶ敏感/鈍感
IF TALENT:MASTER:Ｖ敏感
	CALL ADD_CRI_VAR("ペニスバンド装着", TEQUIP:調教者ペニスバンド ? 0 # 3, TEQUIP:調教者ペニスバンド ? 0 # 1)
ELSEIF TALENT:MASTER:Ｖ鈍感
	CALL ADD_CRI_VAR("ペニスバンド装着", TEQUIP:ペニスバンド ? 0 # -3, TEQUIP:調教者ペニスバンド ? 0 # -1)
ENDIF

;調教者がＶ敏感/鈍感
IF TALENT:TARGET:Ｖ敏感
	CALL ADD_CRI_VAR("ペニスバンド装着", TEQUIP:調教者ペニスバンド ? 0 # 3, TEQUIP:調教者ペニスバンド ? 0 # 1)
ELSEIF TALENT:TARGET:Ｖ鈍感
	CALL ADD_CRI_VAR("ペニスバンド装着", TEQUIP:調教者ペニスバンド ? 0 # -3, TEQUIP:調教者ペニスバンド ? 0 # -1)
ENDIF

;調教者が処女
SIF TALENT:TARGET:処女
	CALL ADD_CRI_VAR("ペニスバンド装着", -10)

;調教対象が蓬莱人
SIF TALENT:不死
	CALL ADD_CRI_VAR("公衆肉便器プレイ", , 9)

;調教対象がふたなり
SIF TALENT:MASTER:ふたなり
	CALL ADD_CRI_VAR("足コキする", TALENT:好奇心 * 3 - TALENT:無関心, 4)

;調教対象が処女
IF TALENT:MASTER:処女
	CALL ADD_CRI_VAR("フィストファック", -10)
	CALL ADD_CRI_VAR("両穴フィスト", -10)
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -10)
ENDIF

;調教対象が童貞
SIF TALENT:MASTER:童貞
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -5)

;調教対象がオトコ
SIF TALENT:MASTER:オトコ
	CALL ADD_CRI_VAR("公衆肉便器プレイ", -3, -4)

;────────────────────────────────────
;能力、パラメーターによる変動
;────────────────────────────────────
;-------------------------------------------------------------------------
;調教者の能力を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_ABL_T

;調教者の会話が[0→0, 5→0～5]を加算(0～5)
CALL ADD_CRI_VAR("不気味に笑う", , LINE_CALC(ABL:会話, "0→0,5→5,LIMIT/0～5"))

;調教者の愛撫が[0→0, 5→2～4]を加算(0～4)
CALL ADD_CRI_VAR("足コキする", LINE_CALC(ABL:愛撫, "0→0,5→2,LIMIT/0～2"), LINE_CALC(ABL:愛撫, "0→0,5→2,LIMIT/0～2"))

;調教者の奉仕が[0→0, 5→3～5]を加算(0～5)
CALL ADD_CRI_VAR("足舐め強制", LINE_CALC(ABL:奉仕, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:奉仕, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;調教対象の能力を参照
;-------------------------------------------------------------------------
@ACT_ACTM7_ABL_M

;調教対象のＶ感覚が[0→0, 5→3]を加算(0～3)
CALL ADD_CRI_VAR("フィストファック", LINE_CALC(ABL:MASTER:Ｖ感覚, "0→0,5→3,LIMIT/0～3"))
;調教対象のＶ感覚が[0→0, 5→2]を加算(0～2)
CALL ADD_CRI_VAR("両穴フィスト", LINE_CALC(ABL:MASTER:Ｖ感覚, "0→0,5→2,LIMIT/0～2"))

;調教対象のＡ感覚が[0→0, 5→3]を加算(0～3)
CALL ADD_CRI_VAR("アナルフィスト", LINE_CALC(ABL:MASTER:Ａ感覚, "0→0,5→3,LIMIT/0～3"))
;調教対象のＡ感覚が[0→0, 5→2]を加算(0～2)
CALL ADD_CRI_VAR("両穴フィスト", LINE_CALC(ABL:MASTER:Ａ感覚, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------
;調教者のALI
;-------------------------------------
@ACT_ACTM7_ALI_T

;アライメントが[-50→3～8, 50→-8～-3]を加算(-8～8)
CALL ADD_CRI_VAR("公衆肉便器プレイ", LINE_CALC(CFLAG:アライメント, "-50→3,50→-3,LIMIT/-3～3"), LINE_CALC(CFLAG:アライメント, "-50→5,50→-5,LIMIT/-5～5"))
;アライメントが[-50→-8～-3, 50→3～8]を加算(-8～8)
CALL ADD_CRI_VAR("足コキする", LINE_CALC(CFLAG:アライメント, "-50→-3,50→3,LIMIT/-3～3"), LINE_CALC(CFLAG:アライメント, "-50→-5,50→5,LIMIT/-5～5"))
;アライメントが[-30→0, 0→3, 30→0]を加算(0～3)
CALL ADD_CRI_VAR("足舐め強制", LINE_CALC(CFLAG:アライメント, "-30→0,0→3,30→0,LIMIT/0～3"))
;アライメントが[-20→0, 0→2, 20→0]を加算(0～2)
CALL ADD_CRI_VAR("フィストファック", LINE_CALC(CFLAG:アライメント, "-20→0,0→2,20→0,LIMIT/0～2"))

;────────────────────────────────────
;ゲージや状態による変動
;────────────────────────────────────
;-------------------------------------
;調教者の気力
;-------------------------------------
@ACT_ACTM7_MENTAL_T

;調教者の気力が[0→3+α*3, 750→0, 1000→-3]を加算(-3～3+α*3)(α=調教者消耗度)
CALL ADD_CRI_VAR("放置プレイ", LINE_CALC(BASE:気力, "0→3,750→0,1000→-3,LIMIT/-3～3") + TFLAG:調教者消耗度 * LINE_CALC(BASE:気力, "0→3,750→0,LIMIT/0～3"))
CALL ADD_CRI_VAR("公衆肉便器プレイ", LINE_CALC(BASE:気力, "0→3,750→0,1000→-3,LIMIT/-3～3") + TFLAG:調教者消耗度 * LINE_CALC(BASE:気力, "0→3,750→0,LIMIT/0～3"))

;-------------------------------------
;調教者の理性
;-------------------------------------
@ACT_ACTM7_REASON_T

;調教者の理性が[0→2～7, 750→0]を加算(0～7)
CALL ADD_CRI_VAR("不気味に笑う", LINE_CALC(BASE:理性, "0→2,750→0,LIMIT/0～2"), LINE_CALC(BASE:理性, "0→5,750→0,LIMIT/0～5"))
;調教者の理性が[500→0, 1000→3]を加算(0～3)
CALL ADD_CRI_VAR("足コキする", LINE_CALC(BASE:理性, "500→0,1000→3,LIMIT/0～3"))
;調教者の理性が[500→0, 1000→2～5]を加算(0～5)
CALL ADD_CRI_VAR("足舐め強制", LINE_CALC(BASE:理性, "500→0,1000→2,LIMIT/0～2"), LINE_CALC(BASE:理性, "500→0,1000→5,LIMIT/0～5"))

;-------------------------------------
;調教者の興味
;-------------------------------------
@ACT_ACTM7_INTEREST_T

;調教者の興味が[0→-3, 550→0]を加算(-3～0)
CALL ADD_CRI_VAR("フィストファック", LINE_CALC(BASE:興味, "0→-3,550→0,LIMIT/-3～0"))
CALL ADD_CRI_VAR("アナルフィスト", LINE_CALC(BASE:興味, "0→-3,550→0,LIMIT/-3～0"))
CALL ADD_CRI_VAR("両穴フィスト", LINE_CALC(BASE:興味, "0→-3,550→0,LIMIT/-3～0"))
;調教者の興味が[0→3, 700→-3]を加算(-3～3)
CALL ADD_CRI_VAR("放置プレイ", LINE_CALC(BASE:興味, "0→3,700→-3,LIMIT/-3～3"))
;調教者の興味が[0→5, 700→-5]を加算(-5～5)
CALL ADD_CRI_VAR("公衆肉便器プレイ", LINE_CALC(BASE:興味, "0→5,700→-5,LIMIT/-5～5"))
;調教者の興味が[0→-5, 700→0]を加算(-5～0)
CALL ADD_CRI_VAR("足コキする", LINE_CALC(BASE:興味, "0→-5,700→0,LIMIT/-5～0"))
CALL ADD_CRI_VAR("足舐め強制", LINE_CALC(BASE:興味, "0→-5,700→0,LIMIT/-5～0"))

;-------------------------------------
;調教対象の体力
;-------------------------------------
@ACT_ACTM7_PHYSICAL_M

;調教対象の体力が[0→-1, 500→0]を加算(-1～0)
CALL ADD_CRI_VAR("フィストファック", LINE_CALC(BASE:MASTER:体力, "0→-1,500→0,LIMIT/-1～0"))
;調教対象の体力が[0→-3, 1000→0]を加算(-3～0)
CALL ADD_CRI_VAR("アナルフィスト", LINE_CALC(BASE:MASTER:体力, "0→-3,1000→0,LIMIT/-3～0"))
;調教対象の体力が[0→-4, 1000→0]を加算(-4～0)
CALL ADD_CRI_VAR("両穴フィスト", LINE_CALC(BASE:MASTER:体力, "0→-4,1000→0,LIMIT/-4～0"))
;※キャンセル時は正の補正
;調教対象の体力が[0→9/-9, 1500→0]を加算(-9～9)
LOCAL = TEQUIP:三角木馬 ? 1 # -1
CALL ADD_CRI_VAR("三角木馬", LOCAL * LINE_CALC(BASE:MASTER:体力, "0→9,1500→0,LIMIT/0～9"))

;-------------------------------------
;調教者の状態
;-------------------------------------
@ACT_ACTM7_CONDITION_T

;調教者の状態(0=通常/1=疲弊/2=衰弱/3=無気力/4=朦朧/5=情欲/6=怒り/7=退屈/8=鬱屈)
SELECTCASE GET_CONDITION(TARGET)
	CASE "疲弊", "衰弱"
		CALL ADD_CRI_VAR("フィストファック", -TFLAG:調教者消耗度)
		CALL ADD_CRI_VAR("アナルフィスト", -TFLAG:調教者消耗度)
		CALL ADD_CRI_VAR("両穴フィスト", -TFLAG:調教者消耗度 - 3)
		CALL ADD_CRI_VAR("足コキする", -TFLAG:調教者消耗度 * 2)
		CALL ADD_CRI_VAR("足舐め強制", -TFLAG:調教者消耗度 / 2)
		CALL ADD_CRI_VAR("三角木馬", -TFLAG:調教者消耗度, -5)
	CASE "退屈"
		CALL ADD_CRI_VAR("放置プレイ", 5)
		CALL ADD_CRI_VAR("公衆肉便器プレイ", 5)
ENDSELECT

;────────────────────────────────────
;前回の行動や状況による変動
;────────────────────────────────────
;-------------------------------------
;媚薬
;-------------------------------------
@ACT_ACTM7_DRAG
SIF TEQUIP:媚薬
	CALL ADD_CRI_VAR("放置プレイ", TALENT:意地悪 * 3 - TALENT:心根優しい * 3, 4)

;-------------------------------------
;縄
;-------------------------------------
@ACT_ACTM7_ROPE
SIF TEQUIP:縄
	CALL ADD_CRI_VAR("放置プレイ", 5)

;-------------------------------------
;アイマスク
;-------------------------------------
@ACT_ACTM7_EYEMASK
SIF TEQUIP:アイマスク
	CALL ADD_CRI_VAR("放置プレイ", 3)

;-------------------------------------
;ボールギャグ
;-------------------------------------
@ACT_ACTM7_GAG
SIF TEQUIP:ボールギャグ
	CALL ADD_CRI_VAR("放置プレイ", , 3)

;-------------------------------------
;三角木馬
;-------------------------------------
@ACT_ACTM7_HORSE
SIF TEQUIP:三角木馬
	CALL ADD_CRI_VAR("放置プレイ", , 7)

;-------------------------------------
;野外プレイ
;-------------------------------------
@ACT_ACTM7_OUT
IF TEQUIP:野外プレイ
	CALL MUL_CRI_VAR("放置プレイ", 150)
	CALL MUL_CRI_VAR("公衆肉便器プレイ", 125)
ENDIF

;-------------------------------------
;要求されて道具をはずした
;-------------------------------------
@ACT_ACTM7_TAKE_OFF_TOOLS
;キャンセルに応じて装着を躊躇
SIF !TEQUIP:三角木馬
	CALL ADD_CRI_VAR("三角木馬", -CANCEL_TEQUIP_COUNT("三角木馬") / 20)

;-------------------------------------
;調教者自慰中
;-------------------------------------
@ACT_ACTM7_MASTURBATION_T
;調教者のみ自慰中
SIF TEQUIP:自慰中 == 2
	CALL ADD_CRI_VAR("放置プレイ", , 9)

;-------------------------------------
;調教対象の衣服
;-------------------------------------
@ACT_ACTM7_CLOTH_M
;調教対象の下半身下着があるとき
SIF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("三角木馬", -2)

;調教対象の下半身上着に妨害されるとき
IF CLOTHESTYPE(1, 1) == 2
	;調教者に壁抜けがない
	IF !TALENT:壁抜け
		CALL ADD_CRI_VAR("フィストファック", -5)
		CALL ADD_CRI_VAR("アナルフィスト", -5)
		CALL ADD_CRI_VAR("両穴フィスト", -5)
	ENDIF
	CALL ADD_CRI_VAR("三角木馬", -5)
ENDIF

;-------------------------------------
;もっとハードにして
;-------------------------------------
@ACT_ACTM7_PLEASE_HARD
IF IS_NOWCOMNAME("もっとハードにして")
	CALL ADD_CRI_VAR("放置プレイ", -10)
	CALL ADD_CRI_VAR("公衆肉便器プレイ", 2)
	CALL ADD_CRI_VAR("不気味に笑う", -3)
	CALL ADD_CRI_VAR("足コキする", -3)
ENDIF

;-------------------------------------
;許しを乞う
;-------------------------------------
@ACT_ACTM7_PLEASE_HELP
;許しを乞ったかつ、調教者の状態が鬱屈以外
SIF IS_NOWCOMNAME("許しを乞う") && TCVAR:状態変化 < 8
	CALL ADD_CRI_VAR("放置プレイ", 5)

;-------------------------------------
;お仕置き中
;-------------------------------------
@ACT_ACTM7_OSHIOKI
IF TFLAG:お仕置きフラグ
	CALL ADD_CRI_VAR("公衆肉便器プレイ", TFLAG:お仕置きフラグ / 2)
	CALL ADD_CRI_VAR("三角木馬", TFLAG:お仕置きフラグ / 2)
ENDIF

;────────────────────────────────────
;同じ行動連続実行の確率をダウンします
;────────────────────────────────────
@ACT_ACTM7_PREV
SELECTCASE TFLAG:前ACT
	CASE 9, 18, 55, 67, 70 TO 74
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:前ACT), -3 * TFLAG:同一行動ボーナス, -4 * TFLAG:同一行動ボーナス)
ENDSELECT

;────────────────────────────────────
;補正値計算
;────────────────────────────────────
@ACT_ACTM7_CORRECTION(ARG)
#DIM LCOUNT

;ARGが0以外なら補正値を計算し、以後の処理を行わない
IF ARG
	;9,18,55,67に関してはここで計算しない
	FOR LCOUNT, 70, 75
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "累計"))
	NEXT
ELSE
	;補正値を判定値に加算する
	CALL ACT_ACTM_CORRECTION(7)
ENDIF

;────────────────────────────────────
;その他変動や実行不可能の判定
;────────────────────────────────────
@ACT_ACTM7_ABLE
#DIM LCOUNT

;不可能判定
CALL ACT_ACTM_ABLE(7)

