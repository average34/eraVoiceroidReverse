
;------------------------------------------------------------------------
;EDIT	001		KOremi			2011/11/04		全体の書き直し、並べ替え
;												ACT_APPLY_PROCESS.ERBから地の文を切り出す
;												強制脱衣、自主脱衣処理を調整
;------------------------------------------------------------------------

;────────────────────────────────────
;拒絶時地の文(リアクションによって調教がキャンセルされる)
;────────────────────────────────────

;────────────────────────────────────
;性交のキャンセル
;────────────────────────────────────
@CANCEL_INSERT_TO_M_MESSAGE
PRINTL 
;性交開始時
IF TFLAG:ACT分類 == 3
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%と性交しようとしたが、%CALLNAME:MASTER%が暴れてうまくいかなかった…
;性交中
ELSE
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%に挿入していたが、%CALLNAME:MASTER%が暴れたので一旦やめるしかなかった…
ENDIF
RETURN 1
;────────────────────────────────────
;性交奉仕のキャンセル
;────────────────────────────────────
@CANCEL_INSERT_TO_T_MESSAGE
PRINTL 
;性交開始時
IF TFLAG:ACT分類 == 10
	;ACTがアナルセックスさせるなら
	IF IS_NOWACTNAME("アナルセックスさせる")
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の\@ TEQUIP:ペニスバンド ? 張り型 # ペニス \@をアヌスに挿入させようとしたが、%CALLNAME:MASTER%が暴れてうまくいかなかった…
	;その他
	ELSE
		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の\@ TEQUIP:ペニスバンド ? 張り型 # ペニス \@をヴァギナに挿入させようとしたが、%CALLNAME:MASTER%が暴れたので一旦やめるしかなかった…
	ENDIF
;性交中
ELSE
	;アナルセックス状態であれば
	IF TEQUIP:性交奉仕中 == 6
		PRINTFORML %CALLNAME:MASTER%が暴れたため、%CALLNAME:TARGET%のアヌスに挿入されていた\@ TEQUIP:ペニスバンド ? 張り型 # ペニス \@が抜けてしまった…
	;その他
	ELSE
		PRINTFORML %CALLNAME:MASTER%が暴れたため、%CALLNAME:TARGET%に挿入されていた\@ TEQUIP:ペニスバンド ? 張り型 # ペニス \@が抜けてしまった…
	ENDIF
ENDIF
RETURN 1
;────────────────────────────────────
;加虐、露出プレイのキャンセル（痛み、拘束、露出、逸脱）
;────────────────────────────────────
;拘束キャンセル
@CANCEL_TEQUIP_RESTRAINT_MESSAGE
PRINTL 
PRINTFORML %CALLNAME:MASTER%の哀願を聞き入れ、%CALLNAME:TARGET%は%CALLNAME:MASTER%の拘束を解いた…
RETURN 1
;三角木馬キャンセル
@CANCEL_TEQUIP_W_HORSE_MESSAGE
PRINTL 
PRINTFORML %CALLNAME:MASTER%の哀願を聞き入れ、%CALLNAME:TARGET%は%CALLNAME:MASTER%を木馬から降ろした…
RETURN 1
;野外プレイキャンセル
@CANCEL_TEQUIP_OUTDOOR_MESSAGE
PRINTL 
PRINTFORML %CALLNAME:MASTER%の哀願を聞き入れ、%CALLNAME:TARGET%は%CALLNAME:MASTER%を連れて部屋に戻ってきた…
RETURN 1
;大鏡キャンセル
@CANCEL_TEQUIP_MIRROR_MESSAGE
PRINTL 
PRINTFORML %CALLNAME:MASTER%の哀願を聞き入れ、%CALLNAME:TARGET%は%CALLNAME:MASTER%を連れて鏡の前から離れた…
RETURN 1
;コスプレキャンセル
@CANCEL_TEQUIP_COSPLAY_MESSAGE
PRINTL 
PRINTFORML %CALLNAME:MASTER%の哀願を聞き入れ、%CALLNAME:TARGET%は%CALLNAME:MASTER%の着替えを許し、コスプレ衣装を回収した…
RETURN 1

;────────────────────────────────────
;特殊状況時地の文(特別な状況が起きた時の地の文)
;────────────────────────────────────

;────────────────────────────────────
;自慰絶頂時の地の文表示
;────────────────────────────────────
@ONANIE_EX_MESSAGE
;ACTが自慰の場合内容が被るので表示しない
SIF TFLAG:ACT == GET_ACTNUM("自慰")
	RETURN 0
;ペニスあり
IF PENIS(MASTER)
	;REACT分類が積極的従う系
	IF IS_COMGRONAME("積極的従う")
		;顔射自慰絶頂
		IF ME_PLAYER(1) == 9
			PRINTFORML %CALLNAME:MASTER%が自身の陰茎をしごきながら射精が近いことを伝えると、
			PRINTFORML %CALLNAME:TARGET%は鈴口の前で舌をてろんと突き出して、口の中まで飛ばすように命令した…
		;その他
		ELSE
			PRINTFORML %CALLNAME:MASTER%が自身の陰茎をしごきながら射精が近いことを伝えると、
			PRINTFORML %CALLNAME:TARGET%は卑猥な言葉をささやきながら一人でイくように命じた…
		ENDIF
	;REACT分類がその他
	ELSE
		;顔射自慰絶頂
		IF ME_PLAYER(1) == 9
			PRINTFORML %CALLNAME:TARGET%は自身の陰茎をしごいている%CALLNAME:MASTER%の絶頂を察すると
			PRINTFORML 鈴口の前で舌をてろんと突き出して、口の中まで飛ばすように命令した…
		;その他
		ELSE
			PRINTFORML %CALLNAME:TARGET%は自身の陰茎をしごいている%CALLNAME:MASTER%の絶頂を察すると
			PRINTFORML 卑猥な言葉をささやきながら一人でイくように命じた…
		ENDIF
	ENDIF
ELSE
	;REACT分類が積極的従う系
	IF IS_COMGRONAME("積極的従う")
		PRINTFORML %CALLNAME:MASTER%が自身の陰核をこね回しながら絶頂が近いことを伝えると、
		PRINTFORML %CALLNAME:TARGET%は卑猥な言葉をささやきながら一人でイくように命じた…
	;REACT分類がその他
	ELSE
		PRINTFORML %CALLNAME:TARGET%は自身の陰核を弄っている%CALLNAME:MASTER%の絶頂を察すると
		PRINTFORML 卑猥な言葉をささやきながら一人でイくように命じた…
	ENDIF
ENDIF
RETURN 1

;────────────────────────────────────
;実行時地の文(ACT種によらない地の文)
;────────────────────────────────────

;────────────────────────────────────
;媚薬・ローション使用時の地の文/口上表示
;────────────────────────────────────
@USE_MESSAGE
;媚薬・ローションを使用した旨のメッセージを表示します
;ここの改行処理はあやしいので後でいろいろ調整が必要

;RESULT初期化
RESULT = 0

;媚薬等使用口上呼び出し
IF TFLAG:媚薬等使用口上 && ((ASSI:1) < 0 || TCVAR:(ASSI:1):助手ACT != 71)
	PRINTL 
	CALL KOJO_USE
ENDIF

;口上表示後地の文を表示する場合
SIF RESULT && TFLAG:媚薬等使用地の文スキップ
	PRINTL 
;地の文表示種別によって分岐
SELECTCASE TFLAG:媚薬等使用地の文スキップ
	;ローション
	CASE 1
		;助手協力時
		IF (ASSI:1) >= 0 && TCVAR:(ASSI:1):助手ACT == 71
			ALIGNMENT RIGHT
			PRINTFORML %CALLNAME:(ASSI:1)%は%CALLNAME:MASTER%の体にローションを塗った…
			ALIGNMENT LEFT
		;助手非協力時
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の体にローションを塗った…
		ENDIF
	;媚薬
	CASE 2
		;助手協力時
		IF (ASSI:1) >= 0 && TCVAR:(ASSI:1):助手ACT == 71
			ALIGNMENT RIGHT
			PRINTFORML %CALLNAME:(ASSI:1)%は%CALLNAME:MASTER%に媚薬を使った…
			ALIGNMENT LEFT
		;キス時
		ELSEIF IS_NOWACTNAME("キス")
			PRINTFORML %CALLNAME:TARGET%は自ら媚薬を飲んで、キスしながらそれを口移しで%CALLNAME:MASTER%に飲ませた…
		;助手非協力時
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%に媚薬を使った…
		ENDIF
	;ローション＆媚薬
	CASE 3
		;助手協力時
		IF (ASSI:1) >= 0 && TCVAR:(ASSI:1):助手ACT == 71
			ALIGNMENT RIGHT
			PRINTFORML %CALLNAME:(ASSI:1)%は%CALLNAME:MASTER%に媚薬を使った…
			PRINTFORML さらに%CALLNAME:(ASSI:1)%は火照る%CALLNAME:MASTER%の体にローションを塗った…
			ALIGNMENT LEFT
		;キス時
		ELSEIF IS_NOWACTNAME("キス")
			PRINTFORML %CALLNAME:TARGET%は自ら媚薬を飲んで、キスしながらそれを口移しで%CALLNAME:MASTER%に飲ませた…
			PRINTFORML さらに口づけしたまま火照る%CALLNAME:MASTER%の体にローションを塗った…
		;助手非協力時
		ELSE
			PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%に媚薬を使った…
			PRINTFORML さらに%CALLNAME:TARGET%は火照る%CALLNAME:MASTER%の体にローションを塗った…
		ENDIF
ENDSELECT
WAIT

;-----------EDIT 001 START--------------
;主に引数によって判定する方式に変更
;---------------------------------------
;────────────────────────────────────
;強制脱衣、自主脱衣時の地の文/口上表示
;────────────────────────────────────
;強制脱衣・自主脱衣のメッセージを表示します

;脱衣寸前での地の文/口上
@DATUI_MESSAGE(ARG, ARG:1)

;脱衣前に服装を保存する(脱衣直後での地の文で利用)
CALLF CLOTHE_SAVE(TARGET)
CALLF CLOTHE_SAVE(MASTER)

;助手が脱衣の手伝いをする場合
IF NOWASSIACTNAME(ASSI:1) == "脱衣"
	ALIGNMENT RIGHT
	PRINTFORML %CALLNAME:(ASSI:1)%は突然、%CALLNAME:MASTER%の服に手をかけた…
	ALIGNMENT LEFT
	;調教者脱衣も発生
	SIF ARG:1 > 0
		PRINTFORML %CALLNAME:TARGET%も自分の衣服に手をかけ、脱ぎ始めた…
	WAIT
;調教対象、調教者ともに脱衣
ELSEIF ARG > 0 && ARG:1 > 0
	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%と自分の服に手をかけた…
;調教対象脱衣
ELSEIF ARG > 0
	PRINTFORMW %CALLNAME:TARGET%は突然、%CALLNAME:MASTER%の服に手をかけた…
;調教者脱衣
ELSEIF ARG:1 > 0
	PRINTFORMW %CALLNAME:TARGET%は自分の衣服に手をかけ、脱ぎ始めた…
ENDIF

;RESULT初期化
RESULT = 0
;脱衣時口上呼び出し
CALL KOJO_DATUI

;調教対象脱衣失敗
IF TFLAG:強制脱衣 < 0
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:MASTER%の服を剥ごうとしたが、%CALLNAME:MASTER%に邪魔された…
;調教対象脱衣成功
ELSEIF ARG > 0
	;LOCAL初期化
	VARSET LOCAL
	;助手が脱衣の手伝いをする場合
	IF NOWASSIACTNAME(ASSI:1) == "脱衣"
		;調教者脱衣も発生
		SIF ARG:1 > 0
			ALIGNMENT RIGHT
		PRINTFORM %CALLNAME:(ASSI:1)%\@ ARG:1 > 0 ? が # は \@%CALLNAME:MASTER%の
	;助手が脱衣の手伝いをしない場合
	ELSE
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%の
	ENDIF
	;脱衣枚数確認(LOCAL:1に脱衣枚数を代入)
	FOR LOCAL, 0, 7
		SIF GETBIT(ARG, LOCAL)
			LOCAL:1 ++
	NEXT
	;最終着衣枚数確認(LOCAL:2に最終着衣枚数を代入)
	FOR LOCAL, 0, 7
		SIF TEQUIP:MASTER:LOCAL != 0 && !GETBIT(ARG, LOCAL)
			LOCAL:2 ++
	NEXT
	;最終着衣が全裸なら
	IF LOCAL:2 == 0
		;脱衣枚数が3枚以上
		IF LOCAL:1 > 2
			PRINT 纏う衣装をすべて
		;脱衣枚数が2枚
		ELSEIF LOCAL:1 == 2
			PRINTFORM 衣類すべてを
		;脱衣枚数が1枚(脱衣側を表示)
		ELSE
			CALL DATUI_MESSAGE_SORT(0, ARG, MASTER)
		ENDIF
	;下着のみ脱衣時、残着衣あり
	ELSEIF !(ARG & ~12) && (LOCAL:2 >= 0)
		;下半身、上半身上着
		IF TEQUIP:MASTER:下半身上着 && TEQUIP:MASTER:上半身上着
			PRINTFORM %OUTER_T(MASTER, 0, 1)%と%OUTER_B(MASTER, 0, 1)%を残して下着だけ
		;下半身上着
		ELSEIF TEQUIP:MASTER:下半身上着
			PRINTFORM %OUTER_B(MASTER, 0, 1)%を残して下着だけ
		;上半身、全身上着
		ELSEIF TEQUIP:MASTER:上半身上着 || TEQUIP:MASTER:全身上着
			PRINTFORM %OUTER_T(MASTER, 0, 1)%を残して下着だけ
		;片方の下着のみ脱衣(脱衣側を表示)
		ELSEIF LOCAL:1 == 1
			CALL DATUI_MESSAGE_SORT(0, ARG, MASTER)
		;その他
		ELSE
			PRINTFORM 下着すべてを
		ENDIF
	;最終着衣が全裸以外なら
	ELSE
		;脱衣枚数が最終着衣以下なら脱衣側を表示
		IF LOCAL:1 <= LOCAL:2
			CALL DATUI_MESSAGE_SORT(0, ARG, MASTER)
		;そうでなければ着衣側を表示
		ELSE
			CALL DATUI_MESSAGE_SORT(1, ARG, MASTER)
		ENDIF
	ENDIF
	;脱いだ結果全裸
	IF LOCAL:2 == 0
		PRINTFORML 強引に\@ ARG:1 > 0 ? 剥いで全裸にすると、 # 剥ぎ、全裸にした… \@
	;脱いだ結果全裸以外
	ELSE
		PRINTFORML 強引に\@ ARG:1 > 0 ? 剥ぐと、 # 剥いだ… \@
	ENDIF
	ALIGNMENT LEFT
ENDIF
;調教者脱衣発生
IF ARG:1 > 0
	;LOCAL初期化
	VARSET LOCAL
	;調教対象脱衣失敗時
	IF TFLAG:強制脱衣 < 0
		PRINTFORM %CALLNAME:TARGET%は諦めて自分の
	;助手が脱衣の手伝いをする場合
	ELSEIF NOWASSIACTNAME(ASSI:1) == "脱衣"
		PRINTFORM %CALLNAME:TARGET%も自分の
	;調教対象脱衣成功時
	ELSEIF ARG > 0
		PRINT 自分も
	;調教対象脱衣不発生時
	ELSE
		PRINTFORM %CALLNAME:TARGET%は自分の
	ENDIF
	;脱衣枚数確認(LOCAL:1に脱衣枚数を代入)
	FOR LOCAL, 0, 7
		SIF GETBIT(ARG:1, LOCAL)
			LOCAL:1 ++
	NEXT
	;最終着衣枚数確認(LOCAL:2に最終着衣枚数を代入)
	FOR LOCAL, 0, 7
		SIF TEQUIP:LOCAL != 0 && !GETBIT(ARG:1, LOCAL)
			LOCAL:2 ++
	NEXT
	;最終着衣が全裸なら
	IF LOCAL:2 == 0
		;脱衣枚数が3枚以上
		IF LOCAL:1 > 2
			PRINT 纏う衣装をすべて
		;脱衣枚数が2枚
		ELSEIF LOCAL:1 == 2
			PRINTFORM 衣類すべてを
		;脱衣枚数が1枚(脱衣側を表示)
		ELSE
			CALL DATUI_MESSAGE_SORT(0, ARG:1, TARGET)
		ENDIF
	;下着のみ脱衣時、残着衣あり
	ELSEIF !(ARG:1 & ~12) && (LOCAL:2 >= 0)
		;下半身、上半身上着
		IF TEQUIP:下半身上着 && TEQUIP:上半身上着
			PRINTFORM %OUTER_T(TARGET, 0, 1)%と%OUTER_B(TARGET, 0, 1)%を残して下着だけ
		;下半身上着
		ELSEIF TEQUIP:下半身上着
			PRINTFORM %OUTER_B(TARGET, 0, 1)%を残して下着だけ
		;上半身、全身上着
		ELSEIF TEQUIP:上半身上着 || TEQUIP:全身上着
			PRINTFORM %OUTER_T(TARGET, 0, 1)%を残して下着だけ
		;片方の下着のみ脱衣(脱衣側を表示)
		ELSEIF LOCAL:1 == 1
			CALL DATUI_MESSAGE_SORT(0, ARG:1, TARGET)
		;その他
		ELSE
			PRINTFORM 下着すべてを
		ENDIF
	;最終着衣が全裸以外なら
	ELSE
		;脱衣枚数が最終着衣以下なら脱衣側を表示
		IF LOCAL:1 <= LOCAL:2
			CALL DATUI_MESSAGE_SORT(0, ARG:1, TARGET)
		;そうでなければ着衣側を表示
		ELSE
			CALL DATUI_MESSAGE_SORT(1, ARG:1, TARGET)
		ENDIF
	ENDIF
	;脱いだ結果全裸
	IF LOCAL:2 == 0
		PRINTFORML 脱ぎ、全裸になった…
	;脱いだ結果全裸以外
	ELSE
		PRINTFORML 脱ぎ捨てた…
	ENDIF
ENDIF
;調教対象脱衣失敗時
SIF TFLAG:強制脱衣 < 0
	PRINTFORML %CALLNAME:TARGET%はかなり怒っているようだ…
WAIT

RETURN RESULT

;脱衣直後での地の文/口上
@DATUI_MESSAGE2(ARG, ARG:1)
PRINTL 
;調教対象、調教者脱衣時結果を表示
SIF ARG > 0
	CALL CLOTHE_SHOW(MASTER)
SIF ARG:1 > 0
	CALL CLOTHE_SHOW(TARGET)
SIF  ARG > 0 || ARG:1 > 0
	WAIT

@DATUI_MESSAGE_SORT(ARG, ARG:1, ARG:2)
;脱衣(最終着衣)種を外側から順に表示
;ARG:0	0 = 脱衣種　1 = 最終着衣種
;ARG:1	脱衣指定部位
;ARG:2	対象者

;LOCAL初期化
VARSET LOCAL

;外側の着衣から確認
LOCAL = 6
;無限ループ
WHILE 1
	;確認着衣を脱衣(着衣)していた場合
	IF  TEQUIP:(ARG:2):LOCAL != 0 && !ARG == GETBIT(ARG:1, LOCAL)
		;二つ目以降は繋ぎ語も表示
		SIF LOCAL:1
			PRINT と
		;脱衣(着衣)内容を表示
		PRINTS CLOTHES(LOCAL, ARG:2)
		;表示数カウント
		LOCAL:1++
	ENDIF
	;確認する部位が無ければ脱出
	SIF LOCAL == 0
		BREAK
	;確認着衣を内側へシフト
	LOCAL--
WEND
;脱衣種表示時
IF ARG == 0
	PRINT を
;最終着衣種表示時
ELSE
	PRINT 以外の衣装全てを
ENDIF
;-----------EDIT 001 END  --------------
