;────────────────────────────────────
;020,ローター（Ｓ/Ｎ）（快Ｃ、中毒充足）
;────────────────────────────────────
@ACTION_APPLY_20
;愛撫補正値、道具使い素質に応じて快ＣSOURCE増加
SOURCE:MASTER:快Ｃ = 150 + ACT_FROM(TARGET, 11) * (3 + TALENT:道具使い) / 3
;調教対象Ｃ感覚、道具強化に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = GET_ABL(MASTER, "Ｃ感覚") * (GET_ABL(MASTER, "Ｃ感覚") - 10) * (ITEM:9 + 1) / 10
;脱衣の処理
;着衣プレイ好き素質がある場合
IF TALENT:着衣プレイ好き
	;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
	SIF GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3
		TFLAG:強制脱衣 = 9
;1/3の確率
ELSEIF !RAND:3
	;下半身強制脱衣
	TFLAG:強制脱衣 = 5
ENDIF
;調教対象下半身上着がスカートでなく、下半身全部強制脱衣でない場合、下半身上着強制脱衣
SIF TEQUIP:MASTER:下半身上着 > 1 && TFLAG:強制脱衣 != 5
	TFLAG:強制脱衣 = 2
;中毒充足SOURCEが正の値なら快ＣSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ｃ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足
;調教者技巧、道具に応じて勃起度上昇
TCVAR:MASTER:今回の勃起 += 150 + (GET_ABL(TARGET, "技巧") + GET_ABL(TARGET, "道具"))
TFLAG:経過時間 += 1

;────────────────────────────────────
;021,バイブ（Ｓ/Ｎ/Ｈ）[罰]（快Ｖ、痛み、逸脱、中毒充足、トラウマ）
;────────────────────────────────────
@ACTION_APPLY_21
;装着時
IF TFLAG:ACT派生 == 0
	;道具強化、愛撫補正値、道具使い素質に応じて快ＶSOURCE増加
	SOURCE:MASTER:快Ｖ = 150 + ITEM:9 * 200 + ACT_FROM(TARGET, 12) * (TALENT:道具使い + 3) / 3
	;脱衣の処理
	;着衣プレイ好き素質がある場合
	IF TALENT:着衣プレイ好き
		;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
		SIF GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3
			TFLAG:強制脱衣 = 9
	;抑圧/恥じらい素質がない場合
	ELSEIF !TALENT:抑圧 && !TALENT:恥じらい
		;下半身全部強制脱衣
		TFLAG:強制脱衣 = 5
	ENDIF
	;調教対象下半身上着がスカートでなく、下半身全部強制脱衣でない場合、下半身上着強制脱衣
	SIF TEQUIP:MASTER:下半身上着 > 1 && TFLAG:強制脱衣 != 5
		TFLAG:強制脱衣 = 2
;必要ならばローション、媚薬を使用
	CALL LOVEPOTION_USE
	CALL LOTION_USE
	;潤滑不足時、快ＶSOURCEに減少補正、痛みSOURCE増加
	CALL SHORTAGE_LUBRICATION("500/M快Ｖ,T,60/M痛み,A,500")
	;Ｖ経験不足時、快ＶSOURCEに減少補正、痛み、逸脱SOURCE増加
	IF EXP:MASTER:Ｖ経験 < 1
		TIMES SOURCE:MASTER:快Ｖ , 0.30
		SOURCE:MASTER:痛み += 1000
		SOURCE:MASTER:逸脱 += 1000
		;合意なし、かつ調教対象に十分な性知識がある状態で
		;処女を奪おうとするとトラウマSOURCE増加
		SIF !CFLAG:合意 && EXP:MASTER:性知識 > 4
			SOURCE:MASTER:トラウマ += 2500
	ELSEIF EXP:MASTER:Ｖ経験 < 4
		TIMES SOURCE:MASTER:快Ｖ , 0.80
		SOURCE:MASTER:痛み += 100
		SOURCE:MASTER:逸脱 += 250
	ELSEIF EXP:MASTER:Ｖ経験 < 6
		TIMES SOURCE:MASTER:快Ｖ , 0.95
		SOURCE:MASTER:痛み += 10
		SOURCE:MASTER:逸脱 += 50
	ENDIF
	;バイブ装着
	TEQUIP:バイブ = 1 + ITEM:9
;解除時
ELSE
	;道具使い素質、愛撫補正値に応じて快ＶSOURCE増加
	SOURCE:MASTER:快Ｖ = 10 + (TALENT:道具使い + 3) * ACT_FROM(TARGET, 12) / 3
	;バイブ解除
	TEQUIP:バイブ = 0
ENDIF
;調教対象Ｖ感覚、道具強化に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = GET_ABL(MASTER, "Ｖ感覚") * (GET_ABL(MASTER, "Ｖ感覚") - 20) * (ITEM:9 + 1) / 5
;中毒充足SOURCEが正の値なら快ＶSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ｖ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足
;トラウマ(刻印)の元がこの調教だった場合
IF CFLAG:MASTER:トラウマ == 21
	;逸脱SOURCEをトラウマSOURCEに加算し、1.5倍にする
	SOURCE:MASTER:トラウマ += SOURCE:MASTER:逸脱
	TIMES SOURCE:MASTER:トラウマ , 1.50
ENDIF
TCVAR:MASTER:前立腺刺激 += 1
;勃起度上昇
TCVAR:MASTER:今回の勃起 += 100

TFLAG:経過時間 += 2

@ACTION_APPLY2_21
;MASTERが処女なら、処女喪失が起こる
TCVAR:MASTER:処女喪失 = TALENT:MASTER:処女


;────────────────────────────────────
;022,アナルバイブ（Ｎ/Ｈ）[罰]（快Ａ、痛み、逸脱、中毒充足）
;────────────────────────────────────
@ACTION_APPLY_22
;装着時
IF TFLAG:ACT派生 == 0
	;道具強化、道具使い素質、愛撫補正値に応じて快ＡSOURCE増加
	SOURCE:MASTER:快Ａ = 100 + ITEM:9 * 200 + (TALENT:道具使い + 3) * ACT_FROM(TARGET, 13) / 3
	;脱衣の処理
	;着衣プレイ好き素質がある場合
	IF TALENT:着衣プレイ好き
		;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
		SIF GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3
			TFLAG:強制脱衣 = 9
	;抑圧/恥じらい素質がない場合
	ELSEIF !TALENT:抑圧 && !TALENT:恥じらい
		;下半身全部強制脱衣
		TFLAG:強制脱衣 = 5
	ENDIF
	;調教対象下半身上着がスカートでなく、下半身全部強制脱衣でない場合、下半身上着強制脱衣
	SIF TEQUIP:MASTER:下半身上着 > 1 && TFLAG:強制脱衣 != 5
		TFLAG:強制脱衣 = 2
	;必要ならばローション、媚薬を使用
	CALL LOVEPOTION_USE
	CALL LOTION_USE
	;潤滑不足時、快ＡSOURCEに減少補正、痛みSOURCE増加
	CALL SHORTAGE_LUBRICATION("500/M快Ａ,T,50/M痛み,A,600")
	;Ａ経験不足時、快ＡSOURCEに減少補正、痛み、逸脱SOURCE増加
	IF EXP:MASTER:Ａ経験 < 1
		TIMES SOURCE:MASTER:快Ａ , 0.50
		SOURCE:MASTER:痛み += 800
		SOURCE:MASTER:逸脱 += 1500
	ELSEIF EXP:MASTER:Ａ経験 < 6
		TIMES SOURCE:MASTER:快Ａ , 0.75
		SOURCE:MASTER:痛み += 300
		SOURCE:MASTER:逸脱 += 800 - EXP:MASTER:Ａ経験 * 100
	ELSEIF EXP:MASTER:Ａ経験 < 11
		TIMES SOURCE:MASTER:快Ａ , 0.90
		SOURCE:MASTER:痛み += 120
		SOURCE:MASTER:逸脱 += 200 - EXP:MASTER:Ａ経験 * 10
	ELSEIF EXP:MASTER:Ａ経験 < 21
		SOURCE:MASTER:逸脱 += 60
	ENDIF
	;アナルバイブ装着
	TEQUIP:アナルバイブ = 1 + ITEM:9
;解除時
ELSE
	;道具使い素質、愛撫補正値に応じて快ＡSOURCE増加
	SOURCE:MASTER:快Ａ = 10 + (TALENT:道具使い + 3) * ACT_FROM(TARGET, 13) / 3
	;アナルバイブ解除
	TEQUIP:アナルバイブ = 0
ENDIF
;調教対象Ａ感覚、道具強化に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = (GET_ABL(MASTER, "Ａ感覚") + 10) * (GET_ABL(MASTER, "Ａ感覚") - 20) * (ITEM:9 + 1) / 5
;中毒充足SOURCEが正の値なら快ＡSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ａ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足

;オトコはＡで前立腺刺激
SIF TALENT:MASTER:オトコ
	TCVAR:MASTER:前立腺刺激 += 1
;勃起度上昇
TCVAR:MASTER:今回の勃起 += 150

TFLAG:経過時間 += 2

;────────────────────────────────────
;023,アナルビーズ（Ｓ/Ｎ/Ｈ）（快Ａ、痛み、逸脱、中毒充足）
;────────────────────────────────────
@ACTION_APPLY_23
;装着時
IF TFLAG:ACT派生 == 0
	;アナルビーズ装着
	TEQUIP:アナルビーズ = 1 + RAND:2
	;愛撫補正値、道具強化、道具使い素質に応じて快ＡSOURCE増加
	SOURCE:MASTER:快Ａ = 50 + ACT_FROM(TARGET, 13) * (3 + TEQUIP:アナルビーズ + TALENT:道具使い) / 5
	;脱衣の処理
	;着衣プレイ好き素質がある場合
	IF TALENT:着衣プレイ好き
		;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
		SIF GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3
			TFLAG:強制脱衣 = 9
	;抑圧/恥じらい素質がない場合
	ELSEIF !TALENT:抑圧 && !TALENT:恥じらい
		;下半身全部強制脱衣
		TFLAG:強制脱衣 = 5
	ENDIF
	;調教対象下半身上着がスカートでなく、下半身全部強制脱衣でない場合、下半身上着強制脱衣
	SIF TEQUIP:MASTER:下半身上着 > 1 && TFLAG:強制脱衣 != 5
		TFLAG:強制脱衣 = 2
	;必要ならばローション、媚薬を使用
	CALL LOVEPOTION_USE
	CALL LOTION_USE
	;潤滑不足時、快ＡSOURCEに減少補正、痛みSOURCE増加
	CALL SHORTAGE_LUBRICATION("500/M快Ａ,T,60/M痛み,A,500")
	;Ａ経験不足時、快ＡSOURCEに減少補正、痛み、逸脱SOURCE増加
	IF EXP:MASTER:Ａ経験 < 1
		TIMES SOURCE:MASTER:快Ａ , 0.80
		SOURCE:MASTER:痛み += 450
		SOURCE:MASTER:逸脱 += 1200
	ELSEIF EXP:MASTER:Ａ経験 < 6
		TIMES SOURCE:MASTER:快Ａ , 0.90
		SOURCE:MASTER:痛み += 200
		SOURCE:MASTER:逸脱 += 700 - EXP:MASTER:Ａ経験 * 100
	ELSEIF EXP:MASTER:Ａ経験 < 11
		SOURCE:MASTER:痛み += 60
		SOURCE:MASTER:逸脱 += 150 - EXP:MASTER:Ａ経験 * 10
	ELSEIF EXP:MASTER:Ａ経験 < 21
		SOURCE:MASTER:逸脱 += 20
	ENDIF
;解除時
ELSEIF TFLAG:ACT派生 == 1
	;愛撫補正値、アナルビーズ挿入量、道具使い素質に応じて快ＡSOURCE増加
	SOURCE:MASTER:快Ａ = ACT_FROM(TARGET, 13) * (3 + TEQUIP:アナルビーズ + TALENT:道具使い) / 3
	;潤滑不足時、快ＡSOURCEに減少補正、痛みSOURCE増加
	CALL SHORTAGE_LUBRICATION("500/M快Ａ,T,80/M痛み,A,1000")
	;Ａ経験不足時、快ＡSOURCEに減少補正、痛み、逸脱SOURCE増加
	IF EXP:MASTER:Ａ経験 < 1
		TIMES SOURCE:MASTER:快Ａ , 0.80
		SOURCE:MASTER:痛み += 750
		SOURCE:MASTER:逸脱 += 700
	ELSEIF EXP:MASTER:Ａ経験 < 6
		TIMES SOURCE:MASTER:快Ａ , 0.90
		SOURCE:MASTER:痛み += 350
		SOURCE:MASTER:逸脱 += 500 - EXP:MASTER:Ａ経験 * 20
	ELSEIF EXP:MASTER:Ａ経験 < 11
		SOURCE:MASTER:痛み += 150
		SOURCE:MASTER:逸脱 += 200 - EXP:MASTER:Ａ経験 * 10
	ELSEIF EXP:MASTER:Ａ経験 < 21
		SOURCE:MASTER:逸脱 += 80 - EXP:MASTER:Ａ経験 * 5
	ENDIF
	;アナルビーズ解除
	TEQUIP:アナルビーズ = 0
	;オトコはＡで前立腺刺激（解除時はより刺激が強い）
	SIF TALENT:MASTER:オトコ
		TCVAR:MASTER:前立腺刺激 += 1
;追加時
ELSE
	;アナルビーズ追加
	TEQUIP:アナルビーズ += 1 + RAND:3
	;愛撫補正値、アナルビーズ挿入量、道具使い素質に応じて快ＡSOURCE増加
	SOURCE:MASTER:快Ａ = 50 + ACT_FROM(TARGET, 13) * (3 + TEQUIP:アナルビーズ + TALENT:道具使い) / 6
	;潤滑不足時、快ＡSOURCEに減少補正、痛みSOURCE増加
	CALL SHORTAGE_LUBRICATION("500/M快Ａ,T,70/M痛み,A,250")
	;Ａ経験不足時、快ＡSOURCEに減少補正、痛み、逸脱SOURCE増加
	IF EXP:MASTER:Ａ経験 < 1
		TIMES SOURCE:MASTER:快Ａ , 0.80
		SOURCE:MASTER:痛み += 250
		SOURCE:MASTER:逸脱 += 500
	ELSEIF EXP:MASTER:Ａ経験 < 6
		TIMES SOURCE:MASTER:快Ａ , 0.90
		SOURCE:MASTER:痛み += 50
		SOURCE:MASTER:逸脱 += 300 - EXP:MASTER:Ａ経験 * 50
	ELSEIF EXP:MASTER:Ａ経験 < 11
		SOURCE:MASTER:逸脱 += 40 - EXP:MASTER:Ａ経験 * 3
	ELSEIF EXP:MASTER:Ａ経験 < 21
		SOURCE:MASTER:逸脱 += 5
	ENDIF
ENDIF
;調教対象Ａ感覚に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = (GET_ABL(MASTER, "Ａ感覚") + 10) * (GET_ABL(MASTER, "Ａ感覚") - 20) / 5
;中毒充足SOURCEが正の値なら快ＡSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ａ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足

;オトコはＡで前立腺刺激
SIF TALENT:MASTER:オトコ
	TCVAR:MASTER:前立腺刺激 += 1
;勃起度上昇
TCVAR:MASTER:今回の勃起 += 100
TFLAG:経過時間 += 1

;────────────────────────────────────
;024,クリキャップ/オナホール（Ｓ/Ｎ）（快Ｃ、中毒充足）
;────────────────────────────────────
@ACTION_APPLY_24
;クリキャップ装着時
IF TFLAG:ACT派生 == 0
	;脱衣の処理
	;着衣プレイ好き素質がある場合
	IF TALENT:着衣プレイ好き
		;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
		SIF GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3
			TFLAG:強制脱衣 = 9
	;抑圧/恥じらい素質がない場合
	ELSEIF !TALENT:抑圧 && !TALENT:恥じらい
		;下半身全部強制脱衣
		TFLAG:強制脱衣 = 5
	ENDIF
	;調教対象下半身上着がスカートでなく、下半身全部強制脱衣でない場合、下半身上着強制脱衣
	SIF TEQUIP:MASTER:下半身上着 > 1 && TFLAG:強制脱衣 != 5
		TFLAG:強制脱衣 = 2
	;道具強化、道具使い素質、愛撫補正値に応じて快ＣSOURCE増加
	SOURCE:MASTER:快Ｃ = 150 + ITEM:9 * 100 + (TALENT:道具使い + 3) * ACT_FROM(TARGET, 11) / 3
	;クリキャップ装着
	TEQUIP:クリキャップ = 1 + ITEM:9
;クリキャップ解除時
ELSEIF TFLAG:ACT派生 == 1
	;快ＣSOURCE増加
	SOURCE:MASTER:快Ｃ = 50
	;クリキャップ解除
	TEQUIP:クリキャップ = 0
;オナホール装着時
ELSEIF TFLAG:ACT派生 == 2
	;脱衣の処理
	;着衣プレイ好き素質がある場合
	IF TALENT:着衣プレイ好き
		;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
		SIF GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3
			TFLAG:強制脱衣 = 9
	;抑圧/恥じらい素質がない場合
	ELSEIF !TALENT:抑圧 && !TALENT:恥じらい
		;下半身全部強制脱衣
		TFLAG:強制脱衣 = 5
	ENDIF
	;調教対象下半身上着がスカートでなく、下半身全部強制脱衣でない場合、下半身上着強制脱衣
	SIF TEQUIP:MASTER:下半身上着 > 1 && TFLAG:強制脱衣 != 5
		TFLAG:強制脱衣 = 2
	;道具強化、道具使い素質、愛撫補正値に応じて快ＣSOURCE増加
	SOURCE:MASTER:快Ｃ = 150 + ITEM:9 * 100 + (TALENT:道具使い + 3) * ACT_FROM(TARGET, 11) / 3
	;オナホール装着
	TEQUIP:オナホール = 1 + ITEM:9
;オナホール解除時
ELSE
	;快ＣSOURCE増加
	SOURCE:MASTER:快Ｃ = 50
	;オナホール解除
	TEQUIP:オナホール = 0
ENDIF
;調教対象Ｃ感覚、道具強化に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = GET_ABL(MASTER, "Ｃ感覚") * (GET_ABL(MASTER, "Ｃ感覚") - 10) * (ITEM:9 + 3) / 20
;中毒充足SOURCEが正の値なら快ＣSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ｃ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足
;勃起度上昇
TCVAR:MASTER:今回の勃起 += 200
TFLAG:経過時間 += 1

;────────────────────────────────────
;025,ニプルキャップ（Ｓ/Ｎ）（快Ｂ、中毒充足）
;────────────────────────────────────
@ACTION_APPLY_25
;装着時
IF TFLAG:ACT派生 == 0
	;脱衣の処理
	;着衣プレイ好き/抑圧/恥じらい素質がない場合
	IF !TALENT:着衣プレイ好き && !TALENT:抑圧 && !TALENT:恥じらい
		;上半身全部強制脱衣
		TFLAG:強制脱衣 = 4
		;1/3の確率で
		SIF !RAND:3
			;上半身上着強制脱衣に軽減
			TFLAG:強制脱衣 = 1
	ENDIF
	;道具強化、愛撫補正値、道具使い素質に応じて快ＢSOURCE増加
	SOURCE:MASTER:快Ｂ = 150 + ITEM:9 * 100 + ACT_FROM(TARGET, 14) * (TALENT:道具使い + 3) / 3
	;ニプルキャップ装着
	TEQUIP:ニプルキャップ = 1 + ITEM:9
;解除時
ELSE
	;快ＢSOURCE増加
	SOURCE:MASTER:快Ｂ = 50
	;ニプルキャップ解除
	TEQUIP:ニプルキャップ = 0
ENDIF
;調教対象Ｂ感覚、道具強化に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = (GET_ABL(MASTER, "Ｂ感覚") - 10) * (GET_ABL(MASTER, "Ｂ感覚") - 20) * (ITEM:9 + 1) / 4
;中毒充足SOURCEが正の値なら快ＢSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ｂ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足
;勃起度上昇
TCVAR:MASTER:今回の勃起 += 30
TFLAG:経過時間 += 1

;────────────────────────────────────
;026,ペニスバンド（Ｓ/Ｎ/Ｈ）[罰]（快Ｖ、痛み、逸脱、中毒充足、トラウマ）
;────────────────────────────────────
@ACTION_APPLY_26
;装着時
IF TFLAG:ACT派生 == 0
	;道具強化、愛撫補正値、道具使い素質に応じて快ＶSOURCE増加
	SOURCE:MASTER:快Ｖ = 100 + ITEM:9 * 150 + ACT_FROM(TARGET, 12) * (TALENT:道具使い + 3) / 3
	;脱衣の処理
	;着衣プレイ好き素質があり、下半身上着に妨害されない場合
	;調教レベル、解放素質に応じて成功率上昇し、下着だけ強制脱衣
	IF TALENT:着衣プレイ好き && (GET_TRAINLV(TARGET) + TALENT:解放 * 3 > 2 + RAND:3) && CLOTHESTYPE(1, 1) == 1
		TFLAG:強制脱衣 = 9
	ELSE
		;下半身全部強制脱衣
		TFLAG:強制脱衣 = 5
	ENDIF
;必要ならばローション、媚薬を使用
	CALL LOVEPOTION_USE
	CALL LOTION_USE
	;潤滑不足時、快ＶSOURCEに減少補正、痛みSOURCE増加
	CALL SHORTAGE_LUBRICATION("500/M快Ｖ,T,60/M痛み,A,500")
	;Ｖ経験不足時、快ＶSOURCEに減少補正、痛み、逸脱SOURCE増加
	IF EXP:MASTER:Ｖ経験 < 1
		TIMES SOURCE:MASTER:快Ｖ , 0.30
		SOURCE:MASTER:痛み += 1000
		SOURCE:MASTER:逸脱 += 1000
		;合意なし、かつ調教対象に十分な性知識がある状態で
		;処女を奪おうとするとトラウマSOURCE増加
		SIF !CFLAG:合意 && EXP:MASTER:性知識 > 4
			SOURCE:MASTER:トラウマ += 2500
	ELSEIF EXP:MASTER:Ｖ経験 < 4
		TIMES SOURCE:MASTER:快Ｖ , 0.80
		SOURCE:MASTER:痛み += 100
		SOURCE:MASTER:逸脱 += 250
	ELSEIF EXP:MASTER:Ｖ経験 < 6
		TIMES SOURCE:MASTER:快Ｖ , 0.95
		SOURCE:MASTER:痛み += 10
		SOURCE:MASTER:逸脱 += 50
	ENDIF
	;ペニスバンド装着
	TEQUIP:ペニスバンド = 1 + ITEM:9
	;両立しないものを解除
	TEQUIP:バイブ = 0
	TEQUIP:クリキャップ = 0
;解除時
ELSE
	;道具使い素質、愛撫補正値に応じて快ＶSOURCE増加
	SOURCE:MASTER:快Ｖ = 10 + (TALENT:道具使い + 3) * ACT_FROM(TARGET, 12) / 3
	;ペニスバンド解除
	TEQUIP:ペニスバンド = 0
ENDIF
;調教対象Ｖ感覚、道具強化に応じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = GET_ABL(MASTER, "Ｖ感覚") * (GET_ABL(MASTER, "Ｖ感覚") - 20) * (ITEM:9 + 1) / 5
;中毒充足SOURCEが正の値なら快ＶSOURCEに応じて増加
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 > 0 ? SOURCE:MASTER:中毒充足 + SOURCE:MASTER:快Ｖ / 5 # 0
;中毒充足SOURCEが負の値をとらないよう調整
SOURCE:MASTER:中毒充足 = SOURCE:MASTER:中毒充足 < 0 ? 0 # SOURCE:MASTER:中毒充足
;トラウマ(刻印)の元がこの調教だった場合
IF CFLAG:MASTER:トラウマ == 21
	;逸脱SOURCEをトラウマSOURCEに加算し、1.5倍にする
	SOURCE:MASTER:トラウマ += SOURCE:MASTER:逸脱
	TIMES SOURCE:MASTER:トラウマ , 1.50
ENDIF
TCVAR:MASTER:前立腺刺激 += 1

;勃起度上昇
TCVAR:MASTER:今回の勃起 += 100

TFLAG:経過時間 += 2

@ACTION_APPLY2_26
;MASTERが処女なら、処女喪失が起こる
TCVAR:MASTER:処女喪失 = TALENT:MASTER:処女
